! TUNNEL_V0_POST

!!
! Componentes para c√°lculo de flecha
CSYS,CS_Tunnel
ESEL,S,TYPE,,1
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,Eff_GalleryHeight
NSLE,R
CM,COMPN_leftExt,NODE
*get,NC,NODE,,COUNT
ESEL,S,TYPE,,1
NSEL,S,LOC,X,Eff_GalleryWidth
NSEL,R,LOC,Y,Eff_GalleryHeight
NSLE,R
CM,COMPN_rightExt,NODE
maxDeflection=0
CSYS,0
!!


!!
*do,nthLS,1,LoadStepCount,1
    SET,nthLS,LAST
    ETABLE,'UY',U,Y
    *if,nthLS,EQ,1,THEN
        SMIN,'MIN_UY','UY',,1
    *else
        SMIN,'MIN_UY','UY','MIN_UY',1,1
    *endif
*enddo
SMULT,'SOIL_SY','MIN_UY',,-SoilStiffness*1e-3

CMSEL,S,COMPA_Floor
CMSEL,A,COMPA_HDW_Slab
CMSEL,A,COMPA_HDW_Stiffener
CMSEL,A,COMPA_HDW_Footing
ESLA,S
NSLE,S
PLETAB,'MIN_UY',1
TAKEPIC,'SETTLEMENTS',1500
PLETAB,'SOIL_SY',1
TAKEPIC,'SOILPRES',1500
*get,PeakSigmaY,PLNSOL,,MAX
!!


!!
! Vertical stresses along the axis of the bototm slab
CSYS,CS_Tunnel

*afun,DEG
zi=-GEO_InletLength*COS(TunnelSlope)
ze=(GalleryLength+GEO_InletLength)*COS(TunnelSlope)
yi=-GEO_InletLength*SIN(TunnelSlope)
ye=(GalleryLength+GEO_InletLength)*SIN(TunnelSlope)
*afun,RAD
VIEW1W

ALLSEL
PATH,'BS_AXIS',2,,200
PATH,'BS_AXIS'
PPATH,1,,Eff_GalleryWidth/2,yi,zi,CS_Tunnel
PPATH,2,,Eff_GalleryWidth/2,ye,ze,CS_Tunnel
PDEF,'SOIL_SY',ETAB,'SOIL_SY'

*get,SigmaYMin,PATH,,MIN,'SOIL_SY'
*get,SigmaYMax,PATH,,MAX,'SOIL_SY'
CLIMS,NINT(SigmaYMin-0.5),NINT(SigmaYMax+0.5)

NSLK,S
/view,1,-1,0,0
/angle,1,
/auto,1
LPLOT
/dist,1,0.95,1
/focus,1,,0.05,,2
GO2PNG,1500
LPLOT
/noerase
APLOT
PLPAGM,'SOIL_SY',50,NODE
/erase
ENDPNG,'BS_SOIL_SY'
CLIMS
CMDELE,COMPL_temp
!!


!!
CSYS,0

basePathScale=20

CMSEL,S,COMPA_HDW_Footing
LSLA,S
KSLL,S

*get,ACount,AREA,,COUNT

*del,pathScales
*dim,pathScales,ARRAY,ACount
mnSY=0
mxSY=0
ANum=0
*do,nthArea,1,ACount,1
    CMSEL,S,COMPA_HDW_Footing
    ANum=ARNEXT(ANum)
    ASEL,S,AREA,,ANum
    LSLA,S
    KSLL,S
    CM,COMPL_temp,LINE
    *get,ZMax,KP,,MXLOC,Z
    *get,ZMin,KP,,MNLOC,Z

    CMSEL,S,COMPL_temp
    LSEL,R,LOC,Z,ZMin
    *get,LBeginning,LINE,,NUM,MIN
    Xb=LX(LBeginning, 0.5)
    Yb=LY(LBeginning, 0.5)
    Zb=LZ(LBeginning, 0.5)

    CMSEL,S,COMPL_temp
    LSEL,R,LOC,Z,ZMax
    *get,LEnd,LINE,,NUM,MIN
    Xe=LX(LEnd, 0.5)
    Ye=LY(LEnd, 0.5)
    Ze=LZ(LEnd, 0.5)

    ESLA,S
    NSLE,S

    pathName=STRCAT('FOOT_',CHRVAL(nthArea))
    PATH,%pathName%,2,,100
    PPATH,1,,Xb,Yb,Zb
    PPATH,2,,Xe,Ye,Ze
    PDEF,'SOIL_SY',ETAB,'SOIL_SY'

    *get,SYMin,PATH,,MIN,'SOIL_SY'
    *get,SYMax,PATH,,MAX,'SOIL_SY'
    *if,nthArea,EQ,1,THEN
        mnSY=SYMin
        mxSY=SYMax
    *else
        COMPARE,'mnSY','MIN',2,mnSY,SYMin
        COMPARE,'mxSY','MAX',2,mxSY,SYMax
    *endif

    pathScales(nthArea)=SYMax/PeakSigmaY
*enddo

CMSEL,S,COMPA_Headwalls
ESLA,S
NSLE,S
LSLA,S
KSLL,S
VIEW1W
!/view,1,1,1,1
/view,1,1,0,0
/auto,1
LPLOT
GO2PNG,1500
/dist,1,0.95,1
/focus,1,,0.05,,2
/device,VECTOR,ON
/eshape,0
/type,1,0
EPLOT
/noerase

/device,VECTOR,OFF
CLIMS,mnSY,mxSY
*do,nthArea,1,ACount,1
    pathName=STRCAT('FOOT_',CHRVAL(nthArea))
    scale=pathScales(nthArea)
    PATH,%pathName%
    PLPAGM,'SOIL_SY',scale*basePathScale
*enddo

/erase
/view,1,1,1,1
/auto,1
/dist,1,0.9,1
/angle,1
!GO2PNG,1500
/replot,FAST
/USER,  1
ENDPNG
!!
