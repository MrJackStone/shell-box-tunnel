!!
!!
!! 5. LOAD STEPS
!!
!!
/solu

ALLSEL

ANTYPE,0
NLGEOM,ON

NSUBST,SubstepCount,SubstepCount*2,SubstepCount/2

OUTRES,ERASE
OUTRES,ALL,LAST

FCUM,ADD



!!
!! 5.1 FIRST LOAD STEP: DEAD LOADS
!!
LoadStepIndex=1

ACEL,,Gravity

! Apply soil pressures
TUNNEL_V0_DEADLOADS

*get,parType,PARM,GEO_Headwall,TYPE
*if,parType,EQ,0,AND,GEO_Headwall,GT,0,THEN
    TUNNEL_V0_HDWLOAD
*endif

ALLSEL
TIME,LoadStepIndex
LSWRITE,1


!! 5.1.5 CAPTURE IMAGE
!!
/psymb,FBCS,0
/pbc,F,,1
/pbc,U,,0
/pbc,ROT,,0
/device,VECTOR,ON
/edge,ALL,1
/eshape,0
ESEL,S,MAT,,1
VIEW4W
EPLOT
fName=STRCAT('LOADS4W_',CHRVAL(LoadStepIndex))
/graphics,FULL
TAKEPIC,fName,1200,1,1
/device,VECTOR,OFF
/edge,ALL,0



!!
!! 5.2 SECOND LOAD STEP: TEMPERATURE VARIATION
!!
*get,parmType,PARM,DeltaTemp,TYPE
*if,parmType,EQ,0,AND,DeltaTemp,NE,0,THEN
    LoadStepIndex=LoadStepIndex+1
    ALLSEL
    LSREAD,1
    FDELE,ALL,ALL
    ACEL,0,Gravity/100,0
    ESEL,S,TYPE,,1
    NSLE,S
    BF,ALL,TEMP,DeltaTemp

    ANTYPE,0,NEW
    ALLSEL
    TIME,LoadStepIndex

    NSUBST,SubstepCount,SubstepCount*2,SubstepCount/2

    OUTRES,ERASE
    OUTRES,ALL,LAST

    LSWRITE,LoadStepIndex
*endif



!!
!! 5.3 REMAINING LOAD STEPS: LOAD TRAIN MOVEMENT
!!
lsel,u,line,,all
cm,COMPL_ltpos,line

*do,xOffIndex,1,xOffsetCount,1

    xOffset=xOffsets(xOffIndex)

    LoadStepIndex=LoadStepIndex+1

    ! Import loading from first load step
    ALLSEL
    FDELE,ALL,ALL
    LSREAD,1


    !! 5.3.1 ROOF & APPROACH SLAB
    !!
    *msg,UI,GalleryIndex,LoadStepIndex
GALLERY # %4I %/&
LOAD STEP %4I %/&
APPLYING LOADS ON ROOF SLAB...

    CMSEL,S,COMPA_Roof
    cmsel,a,COMPA_Approach
    NSLA,S,1
    TUNNEL_V0_VERBOUSSINESQ
    ALLSEL


    !! 5.3.2 LEFT WALL
    !!
    *msg,UI,GalleryIndex,LoadStepIndex
GALLERY # %4I %/&
LOAD STEP %4I %/&
APPLYING LOADS ON LEFT WALL...

    CMSEL,S,COMPA_LeftWall
    NSLA,S,1
    TUNNEL_V0_HORBOUSSINESQ, 1
    ALLSEL


    !! 5.3.3 RIGHT WALL
    !!
    *msg,UI,GalleryIndex,LoadStepIndex
GALLERY # %4I %/&
LOAD STEP %4I %/&
APPLYING LOADS ON RIGHT WALL...

    CMSEL,S,COMPA_RightWall
    NSLA,S,1
    TUNNEL_V0_HORBOUSSINESQ, -1
    ALLSEL


    !! 5.3.4 FLOOR SLAB
    !!
    CMSEL,S,COMPA_Floor
    NSLA,S,1
    ! Water buoyancy pressure
    *if,WaterHeight,GT,0,THEN

        *msg,UI,GalleryIndex,LoadStepIndex
GALLERY # %4I %/&
LOAD STEP %4I %/&
APPLYING BUOYANCY LOADS ON FLOOR SLAB...

        F,ALL,FY,BuoyancyLoad

    *endif
    ALLSEL

    !! 5.3.5 WINGS
    !!
    *if,GEO_RetMode,EQ,1,THEN

        *msg,UI,GalleryIndex,LoadStepIndex
GALLERY # %4I %/&
LOAD STEP %4I %/&
APPLYING LOADS ON WINGS...

        CMSEL,S,COMPA_HDW_Walls
        CMSEL,A,COMPA_HDW_TopBeam
        NSLA,S,1
        TUNNEL_V0_HDWBOUSSINESQ
        ALLSEL

    *endif



    !! 5.3.6 CAPTURE IMAGE
    !!
    /psymb,FBCS,0
    /pbc,F,,1
    /pbc,U,,0
    /pbc,ROT,,0
    /device,VECTOR,ON
    /edge,ALL,1
    /eshape,0
    ESEL,S,MAT,,1
    VIEW4W
    EPLOT
    fName=STRCAT('LOADS4W_',CHRVAL(LoadStepIndex))
    /graphics,FULL
    TAKEPIC,fName,1200,1,1
    /device,VECTOR,OFF
    /edge,ALL,0


    !!
    !! 5.4 WRITE LOAD STEP
    !!
    ANTYPE,0,NEW
    ALLSEL
    TIME,LoadStepIndex

    NSUBST,SubstepCount,SubstepCount*2,SubstepCount/2

    OUTRES,ERASE
    OUTRES,ALL,LAST

    LSWRITE,LoadStepIndex

*enddo

LoadStepCount=LoadStepIndex


