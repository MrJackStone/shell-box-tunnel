!!
!!
!! 4. FINITE ELEMENT MODEL
!!
!!
*msg,UI,GalleryIndex,'CREATING FINITE ELEMENT MODEL...'
GALLERY # %4I %/&
%35C

/prep7

CSYS,0

!!
!! 4.1 TUNNEL
!!
! Left wall attributes
CMSEL,S,COMPA_LeftWall
AATT,1,,1,CS_left,SEC_LWALL

! Right wall attributes
CMSEL,S,COMPA_RightWall
AATT,1,,1,CS_right,SEC_RWALL

! Inner walls attributes
CMSEL,S,COMPA_InnerWalls
AATT,1,,1,CS_right,SEC_IWALL

! Floor slab attributes
CMSEL,S,COMPA_Floor
AATT,1,,1,CS_bot,SEC_FLOOR

! Roof slab attributes
CMSEL,S,COMPA_Roof
AATT,3,,1,CS_top,SEC_ROOF

*if,GEO_corbel,GE,1,THEN
    ! Corbels
    xWall=0
    *do,nthWall,1,GalleryModules+1,1

        ! Geometric limits
        xLeft=xWall-GEO_corbelXOff_BOT
        xRight=xWall+GEO_corbelXOff_BOT
        ASEL,S,LOC,X,xLeft,xRight
        CM,COMPA_insideDX,AREA

        ! Coordinate system numbers
        nthCS_ROOF_left   = SEC_ROOF_LCORBEL  + nthWall
        nthCS_ROOF_right  = SEC_ROOF_RCORBEL  + nthWall
        nthCS_FLOOR_left  = SEC_FLOOR_LCORBEL + nthWall
        nthCS_FLOOR_right = SEC_FLOOR_RCORBEL + nthWall
        nthCS_WALL_bot    = SEC_WALL_BCORBEL  + nthWall
        nthCS_WALL_top    = SEC_WALL_TCORBEL  + nthWall

        ! Walls
        *if,xWall,EQ,0,THEN
            nthWallCS=CS_left
        *else
            nthWallCS=CS_right
        *endif

        *if,GEO_corbel,EQ,1,OR,GEO_corbel,EQ,2,THEN
            ! Top slab, left side
            CMSEL,S,COMPA_ROOF_leftCorbel
            CMSEL,R,COMPA_insideDX
            AATT,1,,1,CS_top,nthCS_ROOF_left
            ! Top slab, right side
            CMSEL,S,COMPA_ROOF_rightCorbel
            CMSEL,R,COMPA_insideDX
            AATT,1,,1,CS_top,nthCS_ROOF_right
            ! Top, walls
            CMSEL,S,COMPA_WALL_topCorbel
            CMSEL,R,COMPA_insideDX
            AATT,1,,1,nthWallCS,nthCS_WALL_top
        *endif

        *if,GEO_corbel,EQ,1,OR,GEO_corbel,EQ,3,THEN
            ! Bottom slab, left side
            CMSEL,S,COMPA_FLOOR_leftCorbel
            CMSEL,R,COMPA_insideDX
            AATT,1,,1,CS_bot,nthCS_FLOOR_left
            ! Bottom slab, right side
            CMSEL,S,COMPA_FLOOR_rightCorbel
            CMSEL,R,COMPA_insideDX
            AATT,1,,1,CS_bot,nthCS_FLOOR_right
            ! Bottom, walls
            CMSEL,S,COMPA_WALL_botCorbel
            CMSEL,R,COMPA_insideDX
            AATT,1,,1,nthWallCS,nthCS_WALL_bot
        *endif

        xWall=xWall+Eff_GalleryWidth
    *enddo
*endif

! Mesh
CMSEL,S,COMPA_Tunnel
AESIZE,ALL,ElementSize
AMESH,ALL



!!
!! 4.2 HEADWALL
!!
*get,parType,PARM,GEO_Headwall,TYPE
*if,parType,EQ,0,AND,GEO_Headwall,GT,0,THEN
    CMSEL,S,COMPA_HDW_LeftWall
    AATT,1,,1,CS_left,SEC_HDW_Wall

    CMSEL,S,COMPA_HDW_RightWall
    AATT,1,,1,CS_right,SEC_HDW_Wall

    CMSEL,S,COMPA_HDW_Footing
    AATT,1,,1,CS_bot,SEC_HDW_Footing

    CMSEL,S,COMPA_HDW_Slab
    AATT,1,,1,CS_bot,SEC_HDW_Slab

    CMSEL,S,COMPA_HDW_Stiffener
    AATT,1,,1,CS_bot,SEC_HDW_Stiffener

    CMSEL,S,COMPA_HDW_TopBeam
    CMSEL,A,COMPA_HDW_BotBeam
    CMSEL,A,COMPA_HDW_BackBeam
    AATT,1,,1,CS_tunnel,SEC_HDW_Beam

    ALLSEL
    AESIZE,ALL,FEM_HDW_ESize
    AMESH,ALL
*endif



!!
!! 4.3 SOIL
!!
! Attributes for soil springs
TYPE,2
REAL,1
CSYS,CoordSys
MAT,2

!! 4.3.1 FLOOR SLAB
!!
! Bottom supports
CMSEL,S,COMPA_Floor
NSLA,S,1
*get,NodeCount,NODE,0,COUNT

*do,NodeIndex,1,NodeCount,1

   *get,nthNode,NODE,0,NUM,MIN
   xVal=NX(nthNode)
   yVal=NY(nthNode)
   zVal=NZ(nthNode)

   *get,MaxNode,NODE,0,NUM,MAXD
   NewNode=MaxNode+1
   N,NewNode,xVal,yVal-SpringLength,zVal
   E,NewNode,nthNode

   D,NewNode,UX,0,,,,UY,UZ

   !!
   !NN2=MaxNode+2
   !NN3=MaxNode+3
   !N,NN2,xVal,yVal,zVal+SpringLength*5
   !N,NN3,xVal,yVal,zVal-SpringLength*5
   !E,NN2,nthNode
   !E,NN3,nthNode
   !D,NN2,UX,0,,,,UY,UZ
   !D,NN3,UX,0,,,,UY,UZ
   !!

   NSEL,U,NODE,,nthNode

*enddo

!! 4.3.2 LEFT WALL
!!
! Left wall supports
CMSEL,S,COMPA_LeftWall
NSLA,S,1
*get,NodeCount,NODE,0,COUNT

*do,NodeIndex,1,NodeCount,1

   *get,nthNode,NODE,0,NUM,MIN
   xVal=NX(nthNode)
   yVal=NY(nthNode)
   zVal=NZ(nthNode)

   *get,MaxNode,NODE,0,NUM,MAXD
   NewNode=MaxNode+1
   N,NewNode,xVal-SpringLength,yVal,zVal
   E,NewNode,nthNode

   D,NewNode,UX,0,,,,UY,UZ

   NSEL,U,NODE,,nthNode

*enddo

!! 4.3.3 RIGHT WALL
!!
! Right wall supports
CMSEL,S,COMPA_RightWall
NSLA,S,1
*get,NodeCount,NODE,0,COUNT

*do,NodeIndex,1,NodeCount,1

   *get,nthNode,NODE,0,NUM,MIN
   xVal=NX(nthNode)
   yVal=NY(nthNode)
   zVal=NZ(nthNode)

   *get,MaxNode,NODE,0,NUM,MAXD
   NewNode=MaxNode+1
   N,NewNode,xVal+SpringLength,yVal,zVal
   E,NewNode,nthNode

   D,NewNode,UX,0,,,,UY,UZ

   NSEL,U,NODE,,nthNode

*enddo

!! 4.3.4 TOP SLAB
!!
! Top supports
CMSEL,S,COMPA_Roof
NSLA,S,1
*get,NodeCount,NODE,0,COUNT

*do,NodeIndex,1,NodeCount,1

   *get,nthNode,NODE,0,NUM,MIN
   xVal=NX(nthNode)
   yVal=NY(nthNode)
   zVal=NZ(nthNode)

   *get,MaxNode,NODE,0,NUM,MAXD
   NewNode=MaxNode+1
   N,NewNode,xVal,yVal+SpringLength,zVal
   E,NewNode,nthNode

   D,NewNode,UX,0,,,,UY,UZ

   NSEL,U,NODE,,nthNode

*enddo

!! 4.3.5 TUNNEL BEGINNING
!!
!    ! Longitudinal supports at the beginning of the tunnel
!    CSYS,CS_Tunnel
!    CMSEL,S,COMPA_Tunnel
!    NSLA,S,1
!    NSEL,R,LOC,Z,0
!    *get,NodeCount,NODE,0,COUNT
!
!    *do,NodeIndex,1,NodeCount,1
!
!       *get,nthNode,NODE,0,NUM,MIN
!       xVal=NX(nthNode)
!       yVal=NY(nthNode)
!       zVal=NZ(nthNode)
!
!       *get,MaxNode,NODE,0,NUM,MAXD
!       NewNode=MaxNode+1
!       N,NewNode,xVal,yVal,zVal-LongitSpringLength
!       E,NewNode,nthNode
!
!       D,NewNode,UX,0,,,,UY,UZ
!
!       NSEL,U,NODE,,nthNode
!
!    *enddo
!    CSYS,0

!! 4.3.6 TUNNEL END
!!
!    ! Longitudinal supports at the end of the tunnel
!    CSYS,CS_Tunnel
!    CMSEL,S,COMPA_Tunnel
!    NSLA,S,1
!    NSEL,R,LOC,Z,GalleryLength
!    *get,NodeCount,NODE,0,COUNT
!
!    *do,NodeIndex,1,NodeCount,1
!
!       *get,nthNode,NODE,0,NUM,MIN
!       xVal=NX(nthNode)
!       yVal=NY(nthNode)
!       zVal=NZ(nthNode)
!
!       *get,MaxNode,NODE,0,NUM,MAXD
!       NewNode=MaxNode+1
!       N,NewNode,xVal,yVal,zVal+LongitSpringLength
!       E,NewNode,nthNode
!
!       D,NewNode,UX,0,,,,UY,UZ
!
!       NSEL,U,NODE,,nthNode
!
!    *enddo
!    CSYS,0
!
!    ALLSEL


!! 4.3.7 HEADWALLS
!!
*get,parType,PARM,GEO_Headwall,TYPE
*if,parType,EQ,0,AND,GEO_Headwall,GT,0,THEN
    TUNNEL_V0_HDWBC
*endif


!! 4.3.8 SOIL FRICTION
!!
*get,parmType,PARM,DeltaTemp,TYPE
*if,parmType,EQ,0,AND,DeltaTemp,NE,0,THEN
    ! Attributes for frictional soil springs
    TYPE,2
    REAL,2
    MAT,2

    CMSEL,S,COMPA_Tunnel
    ESLA,S
    NSLE,S
    CM,COMPN_temp,NODE
    CSYS,CS_tunnel
    *get,NC,NODE,,COUNT
    NodeNum=0

    *do,nthNode,1,NC,1
        CMSEL,S,COMPN_temp
        NodeNum=NDNEXT(NodeNum)
        x=NX(NodeNum)
        y=NY(NodeNum)
        z=NZ(NodeNum)
        *get,maxND,NODE,,NUM,MAXD
        NewNode=maxND+1
        N,NewNode,x,y,z+SpringLength
        E,NodeNum,NewNode
        D,NewNode,UZ,0
        CP,NEXT,UX,NewNode,NodeNum
        CP,NEXT,UY,NewNode,NodeNum
    *enddo
    CSYS,0
*endif
!
!
!
!!
!! 4.4 APPROACH SLAB
!!
*if,GEO_APR_status,eq,1,then
    csys,CS_Tunnel

    ! Mesh approach slab
    cmsel,s,COMPA_approach
    aatt,MAT_CONC,,TYPE_SHELL181,,SEC_APR
    aesize,all,FEM_TUN_esize
    amesh,all
    esla,s
    nsle,s
    cm,COMPN_approach,node

    ! Node selection components: approach slab interfaces
    lsel,s,loc,x,0
    lsel,a,loc,x,GEO_TUN_totEffW
    lsla,r
    nsll,s,1
    cm,COMPN_aprInterf,node
    nsel,r,loc,x,0
    cm,COMPN_aprInterfL,node
    cmsel,s,COMPN_aprInterf
    cmsel,u,COMPN_aprInterfL
    cm,COMPN_aprInterfR,node

    ! Node selection components: tunnel interfaces
    lsel,s,loc,x,0
    lsel,a,loc,x,GEO_TUN_totEffW
    lsel,r,loc,y,GEO_TUN_effH
    cmsel,s,COMPA_tunnel
    lsla,r
    nsll,s,1
    cm,COMPN_tunInterf,node
    nsel,r,loc,x,0
    cm,COMPN_tunInterfL,node
    cmsel,s,COMPN_tunInterf
    cmsel,u,COMPN_tunInterfL
    cm,COMPN_tunInterfR,node

    ! Pinned tunnel-slab connection: left
    cmsel,s,COMPN_aprInterfL
    *get,NC,node,,count
    nApr=0
    *do,nthNode,1,NC,1
        cmsel,s,COMPN_aprInterfL
        nApr=ndnext(nApr)
        nx=nx(nApr)
        ny=ny(nApr)
        nz=nz(nApr)
        cmsel,s,COMPN_tunInterfL
        nTun=node(nx,ny,nz)
        nsel,s,node,,nApr
        nsel,a,node,,nTun
        cp,next,ux,nApr,nTun
        cp,next,uy,nApr,nTun
        cp,next,uz,nApr,nTun
    *enddo

    ! Pinned tunnel-slab connection: right
    cmsel,s,COMPN_aprInterfR
    *get,NC,node,,count
    nApr=0
    *do,nthNode,1,NC,1
        cmsel,s,COMPN_aprInterfR
        nApr=ndnext(nApr)
        nx=nx(nApr)
        ny=ny(nApr)
        nz=nz(nApr)
        cmsel,s,COMPN_tunInterfR
        nTun=node(nx,ny,nz)
        nsel,s,node,,nApr
        nsel,a,node,,nTun
        cp,next,ux,nApr,nTun
        cp,next,uy,nApr,nTun
        cp,next,uz,nApr,nTun
    *enddo

    csys,0

    ! Elastic soil supports
    type,TYPE_LINK180
    real,REAL_normSprings
    mat,MAT_SOIL
    cmsel,s,COMPN_approach
    cmsel,u,COMPN_aprInterf
    cm,COMPN_temp,node
    *get,NC,node,,count
    nnum=0
    *do,nthNode,1,NC,1
        cmsel,s,COMPN_temp
        nnum=ndnext(nnum)
        nx=nx(nnum)
        ny=ny(nnum)
        nz=nz(nnum)
        *get,NDMX,node,,num,maxd
        newnode=NDMX+1
        n,newnode,nx,ny-FEM_SPR_normLength,nz
        e,nnum,newnode
        d,newnode,all,0
    *enddo

*endif
!
!
!
!!
!! 4.5 CAPTURE IMAGES
!!
ALLSEL
/eshape,1
VIEW4W
/pnum,SEC,1
/number,1
EPLOT
/graphics,FULL
TAKEPIC,'MESH'

/device,VECTOR,ON
/psymb,ESYS,ON
/eshape,0
ESEL,S,MAT,,1
EPLOT
/graphics,FULL
TAKEPIC,'ESYS',1200,1

/psymb,DEFA

ESEL,S,TYPE,,1
VIEW1W
/device,VECTOR,OFF
/eshape,1
/type,,3
/view,1,1,1,1
EPLOT

