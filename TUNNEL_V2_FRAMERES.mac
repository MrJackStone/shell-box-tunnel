! TUNNEL_V2_FRAMERES


pathDiv=100

/post1
set,last

! Element table: membrane & bending stresses
esel,s,type,,TYPE_SHELL181
nsle,s
etable,'SM_11',smisc,34
etable,'SM_22',smisc,35
etable,'SM_12',smisc,36

etable,'SB_11',smisc,37
etable,'SB_22',smisc,38
etable,'SB_12',smisc,39

etable,'M11',smisc,4

! Create frame area components
cmsel,s,COMPA_HDW_columns
cmsel,a,COMPA_HDW_TopBeam
cmsel,a,COMPA_HDW_BackBeam
cm,COMPA_temp,area

cmsel,s,COMPA_temp
asel,r,loc,z,-GEO_TUN_length,GEO_TUN_length/2
cm,COMPA_Frame1,area

cmsel,s,COMPA_temp
cmsel,u,COMPA_Frame1
cm,COMPA_Frame2,area

csys,CS_Tunnel

cmsel,s,COMPA_Frame1
cmsel,a,COMPA_Frame2
esla,s
nsle,s

! Virtual beam vertices coordinates
x1 = -GEO_FRAME_columnWidth
x2 = 0
x3 = GEO_TUN_totEffW
x4 = GEO_TUN_totEffW+GEO_FRAME_columnWidth
y1 = -GEO_HDW_footDepth
y2 = 0
y3 = GEO_TUN_effH
y4 = GEO_TUN_effH+GEO_FRAME_headBeamHeight

! Virtual beam geometric properties
TBEAM_A = GEO_FRAME_headBeamHeight*GEO_FRAME_headBeamWidth
TBEAM_W = GEO_FRAME_headBeamWidth*(GEO_FRAME_headBeamHeight**2)/6
BBEAM_A = GEO_HDW_footDepth*GEO_FRAME_backBeamWidth
BBEAM_W = GEO_FRAME_backBeamWidth*(GEO_HDW_footDepth**2)/6
COL_A   = GEO_FRAME_columnWidth*GEO_FRAME_columnThk
COL_W   = GEO_FRAME_columnThk*(GEO_FRAME_columnWidth**2)/6

! Frame 1, top beam
FR1_TBEAM_top_n1 = node(x3,y4,GEO_ExtraLength)
FR1_TBEAM_top_n2 = node(x2,y4,0)
FR1_TBEAM_bot_n1 = node(x3,y3,GEO_ExtraLength)
FR1_TBEAM_bot_n2 = node(x2,y3,0)

! Frame 1, bottom beam
FR1_BBEAM_top_n1 = node(x2,y1,0)
FR1_BBEAM_top_n2 = node(x3,y1,GEO_ExtraLength)
FR1_BBEAM_bot_n1 = node(x2,y2,0)
FR1_BBEAM_bot_n2 = node(x3,y2,GEO_ExtraLength)

! Frame 1, left column
FR1_LCOL_left_n1  = node(x1,y3,0)
FR1_LCOL_left_n2  = node(x1,y2,0)
FR1_LCOL_right_n1 = node(x2,y3,0)
FR1_LCOL_right_n2 = node(x2,y2,0)

! Frame 1, right column
FR1_RCOL_left_n1  = node(x4,y2,0)
FR1_RCOL_left_n2  = node(x4,y3,0)
FR1_RCOL_right_n1 = node(x3,y2,0)
FR1_RCOL_right_n2 = node(x3,y3,0)

! Frame 2, top beam
FR2_TBEAM_top_n1 = node(x3,y4,GEO_TUN_length)
FR2_TBEAM_top_n2 = node(x2,y4,GEO_TUN_length-GEO_ExtraLength)
FR2_TBEAM_bot_n1 = node(x3,y3,GEO_TUN_length)
FR2_TBEAM_bot_n2 = node(x2,y3,GEO_TUN_length-GEO_ExtraLength)

! Frame 2, bottom beam
FR2_BBEAM_top_n1 = node(x2,y1,GEO_TUN_length-GEO_ExtraLength)
FR2_BBEAM_top_n2 = node(x3,y1,GEO_TUN_length)
FR2_BBEAM_bot_n1 = node(x2,y2,GEO_TUN_length-GEO_ExtraLength)
FR2_BBEAM_bot_n2 = node(x3,y2,GEO_TUN_length)

! Frame 2, left column
FR2_LCOL_left_n1  = node(x1,y3,GEO_TUN_length)
FR2_LCOL_left_n2  = node(x1,y2,GEO_TUN_length)
FR2_LCOL_right_n1 = node(x2,y3,GEO_TUN_length)
FR2_LCOL_right_n2 = node(x2,y2,GEO_TUN_length)

! Frame 2, right column
FR2_RCOL_left_n1  = node(x4,y2,GEO_TUN_length)
FR2_RCOL_left_n2  = node(x4,y3,GEO_TUN_length)
FR2_RCOL_right_n1 = node(x3,y2,GEO_TUN_length)
FR2_RCOL_right_n2 = node(x3,y3,GEO_TUN_length)

! Selection components:
! Frame 1, top beam
cmsel,s,COMPA_Frame1
cmsel,r,COMPA_HDW_TopBeam
asel,r,loc,x,x2,x3
esla,s
nsle,s
cm,COMPE_FR1_tBeam,elem
cm,COMPN_FR1_tBeam,node
! Frame 1, bottom beam
cmsel,s,COMPA_Frame1
cmsel,r,COMPA_HDW_BackBeam
asel,r,loc,x,x2,x3
esla,s
nsle,s
cm,COMPE_FR1_bBeam,elem
cm,COMPN_FR1_bBeam,node
! Frame 1, left column
cmsel,s,COMPA_Frame1
cmsel,r,COMPA_HDW_Columns
asel,r,loc,x,x1,x2
asel,r,loc,y,y2,y3
esla,s
nsle,s
cm,COMPE_FR1_lCol,elem
cm,COMPN_FR1_lCol,node
! Frame 1, right column
cmsel,s,COMPA_Frame1
cmsel,r,COMPA_HDW_Columns
asel,r,loc,x,x3,x4
asel,r,loc,y,y2,y3
esla,s
nsle,s
cm,COMPE_FR1_rCol,elem
cm,COMPN_FR1_rCol,node
! Frame 2, top beam
cmsel,s,COMPA_Frame2
cmsel,r,COMPA_HDW_TopBeam
asel,r,loc,x,x2,x3
esla,s
nsle,s
cm,COMPE_FR2_tBeam,elem
cm,COMPN_FR2_tBeam,node
! Frame 2, bottom beam
cmsel,s,COMPA_Frame2
cmsel,r,COMPA_HDW_BackBeam
asel,r,loc,x,x2,x3
esla,s
nsle,s
cm,COMPE_FR2_bBeam,elem
cm,COMPN_FR2_bBeam,node
! Frame 2, left column
cmsel,s,COMPA_Frame2
cmsel,r,COMPA_HDW_Columns
asel,r,loc,x,x1,x2
asel,r,loc,y,y2,y3
esla,s
nsle,s
cm,COMPE_FR2_lCol,elem
cm,COMPN_FR2_lCol,node
! Frame 2, right column
cmsel,s,COMPA_Frame2
cmsel,r,COMPA_HDW_Columns
asel,r,loc,x,x3,x4
asel,r,loc,y,y2,y3
esla,s
nsle,s
cm,COMPE_FR2_rCol,elem
cm,COMPN_FR2_rCol,node

*del , RES_ENV_tBeam
*del , RES_ENV_bBeam
*del , RES_ENV_lCol
*del , RES_ENV_rCol

*do,nthSet,1,LoadStepCount,1

  set,nthSet,last
  etable,refl

  cmsel,s,COMPN_FR1_tBeam
  cmsel,s,COMPE_FR1_tBeam
  pth2beam, FR1_TBEAM_top_n1, FR1_TBEAM_top_n2, FR1_TBEAM_bot_n1, FR1_TBEAM_bot_n2, 'TBEAM', 1, pathDiv, TBEAM_W, TBEAM_A, 1, 'RES_ENV_tBeam'

  cmsel,s,COMPN_FR1_bBeam
  cmsel,s,COMPE_FR1_bBeam
  pth2beam, FR1_BBEAM_top_n1, FR1_BBEAM_top_n2, FR1_BBEAM_bot_n1, FR1_BBEAM_bot_n2, 'BBEAM', 1, pathDiv, BBEAM_W, BBEAM_A, 1, 'RES_ENV_bBeam'

  *if,GEO_HDW_type,ne,1,then
    cmsel,s,COMPN_FR1_lCol
    cmsel,s,COMPE_FR1_lCol
    pth2beam, FR1_LCOL_left_n1, FR1_LCOL_left_n2, FR1_LCOL_right_n1, FR1_LCOL_right_n2, 'LCOL',-1,pathDiv,COL_W, COL_A, 1, 'RES_ENV_lCol'

    cmsel,s,COMPN_FR1_rCol
    cmsel,s,COMPE_FR1_rCol
    pth2beam, FR1_RCOL_left_n1, FR1_RCOL_left_n2, FR1_RCOL_right_n1, FR1_RCOL_right_n2, 'RCOL',-1,pathDiv,COL_W, COL_A, 1, 'RES_ENV_rCol'

    cmsel,s,COMPN_FR2_lCol
    cmsel,s,COMPE_FR2_lCol
    pth2beam, FR2_LCOL_left_n1, FR2_LCOL_left_n2, FR2_LCOL_right_n1, FR2_LCOL_right_n2, 'LCOL',-1,pathDiv,COL_W, COL_A, 1, 'RES_ENV_lCol'

    cmsel,s,COMPN_FR2_rCol
    cmsel,s,COMPE_FR2_rCol
    pth2beam, FR2_RCOL_left_n1, FR2_RCOL_left_n2, FR2_RCOL_right_n1, FR2_RCOL_right_n2, 'RCOL',-1,pathDiv,COL_W, COL_A, 1, 'RES_ENV_rCol'
  *endif

  cmsel,s,COMPN_FR2_tBeam
  cmsel,s,COMPE_FR2_tBeam
  pth2beam, FR2_TBEAM_top_n1, FR2_TBEAM_top_n2, FR2_TBEAM_bot_n1, FR2_TBEAM_bot_n2, 'TBEAM',1, pathDiv, TBEAM_W, TBEAM_A, 1, 'RES_ENV_tBeam'

  cmsel,s,COMPN_FR2_bBeam
  cmsel,s,COMPE_FR2_bBeam
  pth2beam, FR2_BBEAM_top_n1, FR2_BBEAM_top_n2, FR2_BBEAM_bot_n1, FR2_BBEAM_bot_n2, 'BBEAM',1, pathDiv, BBEAM_W, BBEAM_A, 1, 'RES_ENV_bBeam'

*enddo

/device,text,1,50
cmsel,s,COMPA_Frame1
cmsel,a,COMPA_Frame2
esla,s
nsle,s

*del,PLPTHENV_ARR_paths
*dim,PLPTHENV_ARR_paths,char,1

/window,all,dele

! Bending moment (in-plane)
wind1 = 1
xmin1 = -1
xmax1 = 0.335
ymin1 = 0
ymax1 = 1
/window,wind1,xmin1,xmax1,ymin1+0.04,ymax1
! Axial force
wind2 = 2
xmin2 = 0.335
xmax2 = 1.67
ymin2 = 0
ymax2 = 1
/window,wind2,xmin2,xmax2,ymin2+0.04,ymax2
! Shear force
wind3 = 3
xmin3 = -1
xmax3 = 0.335
ymin3 = -1
ymax3 = 0
/window,wind3,xmin3,xmax3,ymin3+0.04,ymax3
! Deflection
wind4 = 4
xmin4 = 0.335
xmax4 = 1.67
ymin4 = -1
ymax4 = 0
/window,wind4,xmin4,xmax4,ymin4+0.04,ymax4
! Set up windows
wpcsys , 1 , CS_Tunnel
wpcsys , 2 , CS_Tunnel
wpcsys , 3 , CS_Tunnel
wpcsys , 4 , CS_Tunnel
clims
wincln,1
wincln,2
wincln,3
wincln,4
/auto,all
oplot,wind1,'E11'
/user,all
/dist,all,1.2,1

go2png,1800
  oplot,wind,'E11'
  /device,vector,off
  /noerase

  /window , 1 , off
  /window , 2 , off
  /window , 3 , off
  /window , 4 , off

  ! Bending moment (in-plane)
  wind=1
  /window,wind,on

  *if,GEO_HDW_type,ne,1,then
    PLPTHENV_ARR_paths(1)='TBEAM'
    plpthenv,wind1,xmin1,xmax1,ymin1,ymax1, 'MP', ' ', 1e-3, 10, 0, 2,,,,1
    PLPTHENV_ARR_paths(1)='BBEAM'
    plpthenv,wind1,xmin1,xmax1,ymin1,ymax1, 'MP', ' ', 1e-3, 10, 0, 1,,,,1
    PLPTHENV_ARR_paths(1)='LCOL'
    plpthenv,wind1,xmin1,xmax1,ymin1,ymax1, 'MP', ' ', 1e-3, 10, 0, 3,,,,1
    PLPTHENV_ARR_paths(1)='RCOL'
    plpthenv,wind1,xmin1,xmax1,ymin1,ymax1, 'MP', '(a)  M. FLETOR (PLANO) [kNm]', 1e-3, 10, 0, 4,,,,1
  *else
    PLPTHENV_ARR_paths(1)='TBEAM'
    plpthenv,wind1,xmin1,xmax1,ymin1,ymax1, 'MP', ' ', 1e-3, 10, 0, 2,,,,1
    PLPTHENV_ARR_paths(1)='BBEAM'
    plpthenv,wind1,xmin1,xmax1,ymin1,ymax1, 'MP', '(a)  M. FLETOR (PLANO) [kNm]', 1e-3, 10, 0, 1,,,,1
  *endif

  /window,wind,off

  ! Axial force
  wind=2
  /window,wind,on

  *if,GEO_HDW_type,ne,1,then
    PLPTHENV_ARR_paths(1)='TBEAM'
    plpthenv,wind2,xmin2,xmax2,ymin2,ymax2, 'N', ' ', 1e-3, 10, 0, 2,,,,1
    PLPTHENV_ARR_paths(1)='BBEAM'
    plpthenv,wind2,xmin2,xmax2,ymin2,ymax2, 'N', ' ', 1e-3, 10, 0, 1,,,,1
    PLPTHENV_ARR_paths(1)='LCOL'
    plpthenv,wind2,xmin2,xmax2,ymin2,ymax2, 'N', ' ', 1e-3, 10, 0, 3,,,,1
    PLPTHENV_ARR_paths(1)='RCOL'
    plpthenv,wind2,xmin2,xmax2,ymin2,ymax2, 'N', '(b) F. NORMAL [kN]', 1e-3, 10, 0, 4,,,,1
  *else
    PLPTHENV_ARR_paths(1)='TBEAM'
    plpthenv,wind2,xmin2,xmax2,ymin2,ymax2, 'N', ' ', 1e-3, 10, 0, 2,,,,1
    PLPTHENV_ARR_paths(1)='BBEAM'
    plpthenv,wind2,xmin2,xmax2,ymin2,ymax2, 'N', '(b) F. NORMAL [kN]', 1e-3, 10, 0, 1,,,,1
  *endif

  /window,wind,off

  ! Shear force
  wind=3
  /window,wind,on

  *if,GEO_HDW_type,ne,1,then
    PLPTHENV_ARR_paths(1)='TBEAM'
    plpthenv,wind3,xmin3,xmax3,ymin3,ymax3, 'V', ' ', 1e-3, 10, 0, 2,,,,1
    PLPTHENV_ARR_paths(1)='BBEAM'
    plpthenv,wind3,xmin3,xmax3,ymin3,ymax3, 'V', ' ', 1e-3, 10, 0, 1,,,,1
    PLPTHENV_ARR_paths(1)='LCOL'
    plpthenv,wind3,xmin3,xmax3,ymin3,ymax3, 'V', ' ', 1e-3, 10, 0, 3,,,,1
    PLPTHENV_ARR_paths(1)='RCOL'
    plpthenv,wind3,xmin3,xmax3,ymin3,ymax3, 'V', '(c) F. CORTANTE [kN]', 1e-3, 10, 0, 4,,,,1
  *else
    PLPTHENV_ARR_paths(1)='TBEAM'
    plpthenv,wind3,xmin3,xmax3,ymin3,ymax3, 'V', ' ', 1e-3, 10, 0, 2,,,,1
    PLPTHENV_ARR_paths(1)='BBEAM'
    plpthenv,wind3,xmin3,xmax3,ymin3,ymax3, 'V', '(c) F. CORTANTE [kN]', 1e-3, 10, 0, 1,,,,1
  *endif

  /window,wind,off

  ! Deflection
  wind=4
  /window,wind,on

  *if,GEO_HDW_type,ne,1,then
    PLPTHENV_ARR_paths(1)='TBEAM'
    plpthenv,wind4,xmin4,xmax4,ymin4,ymax4, 'DYB', ' ', 1e3, 10, 2, 2,,,,1
    PLPTHENV_ARR_paths(1)='BBEAM'
    plpthenv,wind4,xmin4,xmax4,ymin4,ymax4, 'DYB', ' ', 1e3, 10, 2, 1,,,,1
    PLPTHENV_ARR_paths(1)='LCOL'
    plpthenv,wind4,xmin4,xmax4,ymin4,ymax4, 'DYB', ' ', 1e3, 10, 2, 3,,,,1
    PLPTHENV_ARR_paths(1)='RCOL'
    plpthenv,wind4,xmin4,xmax4,ymin4,ymax4, 'DYB', '(d) FLECHA [mm]', 1e3, 10, 2, 4,,,,1
  *else
    PLPTHENV_ARR_paths(1)='TBEAM'
    plpthenv,wind4,xmin4,xmax4,ymin4,ymax4, 'DYB', ' ', 1e3, 10, 2, 2,,,,1
    PLPTHENV_ARR_paths(1)='BBEAM'
    plpthenv,wind4,xmin4,xmax4,ymin4,ymax4, 'DYB', '(d) FLECHA [mm]', 1e3, 10, 2, 1,,,,1
  *endif

  /window,wind,off

endpng,'RES_FRAME_all'

/erase
/device,text,1,100
