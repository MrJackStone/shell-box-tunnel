! TUNNEL_V2_FRAMERES


picRes    = 1800
pathDiv   = 100
baseScale = 10

/post1
set,last

cmsel,s,COMPA_frame1
cmsel,a,COMPA_frame2
esla,s
nsle,s

etable , 'SM_11' , smisc , 34
etable , 'SM_22' , smisc , 35
etable , 'SM_12' , smisc , 36
etable , 'SB_11' , smisc , 37
etable , 'SB_22' , smisc , 38
etable , 'SB_12' , smisc , 39
etable , 'M11'   , smisc , 4

*del , RES_ENV_FRM1_topBeam     ,  , nopr
*del , RES_ENV_FRM1_backBeam    ,  , nopr
*del , RES_ENV_FRM1_leftColumn  ,  , nopr
*del , RES_ENV_FRM1_rightColumn ,  , nopr
*del , RES_ENV_FRM2_topBeam     ,  , nopr
*del , RES_ENV_FRM2_backBeam    ,  , nopr
*del , RES_ENV_FRM2_leftColumn  ,  , nopr
*del , RES_ENV_FRM2_rightColumn ,  , nopr

*do,nthSet,1,LoadStepCount,1

  subset,nthSet,last
  etable,refl

  TUNNEL_V2_POSTA2BEAM , 'COMPA_HDW1_topBeam'     , 'TBEAM1' , 'RES_ENV_FRM1_topBeam'
  TUNNEL_V2_POSTA2BEAM , 'COMPA_HDW1_backBeam'    , 'BBEAM1' , 'RES_ENV_FRM1_backBeam'
  TUNNEL_V2_POSTA2BEAM , 'COMPA_HDW1_leftColumn'  , 'LCOL1'  , 'RES_ENV_FRM1_leftColumn'
  TUNNEL_V2_POSTA2BEAM , 'COMPA_HDW1_rightColumn' , 'RCOL1'  , 'RES_ENV_FRM1_rightColumn'

  TUNNEL_V2_POSTA2BEAM , 'COMPA_HDW2_topBeam'     , 'TBEAM2' , 'RES_ENV_FRM2_topBeam'
  TUNNEL_V2_POSTA2BEAM , 'COMPA_HDW2_backBeam'    , 'BBEAM2' , 'RES_ENV_FRM2_backBeam'
  TUNNEL_V2_POSTA2BEAM , 'COMPA_HDW2_leftColumn'  , 'LCOL2'  , 'RES_ENV_FRM2_leftColumn'
  TUNNEL_V2_POSTA2BEAM , 'COMPA_HDW2_rightColumn' , 'RCOL2'  , 'RES_ENV_FRM2_rightColumn'

*enddo

*del , path_names    ,      , nopr
*del , path_labels   ,      , nopr
*del , scale_factors ,      , nopr
*dim , path_names    , char , 8
*dim , path_labels   , char , 10
*dim , scale_factors ,      , 10

path_names(1)    = 'TBEAM1' , 'BBEAM1' , 'LCOL1'  , 'RCOL1'  , 'TBEAM2' , 'BBEAM2' , 'LCOL2' , 'RCOL2'
path_labels(1)   = 'MP_MIN' , 'MP_MAX' , 'MB_MIN' , 'MB_MAX' , 'N_MIN'  , 'N_MAX'  , 'V_MIN' , 'V_MAX' , 'DYB_MIN' , 'DYB_MAX'
scale_factors(1) = 1e-3     , 1e-3     , 1e-3     , 1e-3     , 1e-3     , 1e-3     , 1e-3    , 1e-3    , 1e3       , 1e3

*do,nthPath,1,8,1
  pthName = path_names(nthPath)
  path,pthName
  *do,nthLabel,1,10,1
    pthLabel    = path_labels(nthLabel)
    scaleFactor = scale_factors(nthLabel)
    pcalc,add,pthLabel,pthLabel,,scaleFactor
  *enddo
*enddo

*del , top_beam  ,      , nopr
*del , bot_beam  ,      , nopr
*del , left_col  ,      , nopr
*del , right_col ,      , nopr
*del , label_MP  ,      , nopr
*del , label_MB  ,      , nopr
*del , label_N   ,      , nopr
*del , label_V   ,      , nopr
*del , label_D   ,      , nopr
*dim , top_beam  , char , 2
*dim , bot_beam  , char , 2
*dim , left_col  , char , 2
*dim , right_col , char , 2
*dim , label_MP  , char , 2
*dim , label_MB  , char , 2
*dim , label_N   , char , 2
*dim , label_V   , char , 2
*dim , label_D   , char , 2

label_MP(1)  = 'MP_MIN'  , 'MP_MAX'
label_N(1)   = 'N_MIN'   , 'N_MAX'
label_V(1)   = 'V_MIN'   , 'V_MAX'
label_D(1)   = 'DYB_MIN' , 'DYB_MAX'

*do,nthFrame,1,2,1

  *if,nthFrame,eq,1,then
    frameCS = CS_begSkewed
  *else
    frameCS = CS_endSkewed
  *endif

  top_beam(1)  = 'TBEAM%nthFrame%' , 'TBEAM%nthFrame%'
  bot_beam(1)  = 'BBEAM%nthFrame%' , 'BBEAM%nthFrame%'
  left_col(1)  = 'LCOL%nthFrame%'  , 'LCOL%nthFrame%'
  right_col(1) = 'RCOL%nthFrame%'  , 'RCOL%nthFrame%'

  cmsel,s,COMPA_frame%nthFrame%
  esla,s
  nsle,s

  view4w
  clims
  boldfont,1
  /device,text,1,50
  g_picres,picRes
  wincln
  /plopts,leg3,on
  /angle,all,0
  /edge,all,0
  wpcsys,1,frameCS
  wpcsys,2,frameCS
  wpcsys,3,frameCS
  wpcsys,4,frameCS
  /auto,all

  axanno,1

  wintitle , 1 , 'FRAME #%nthFrame%' , 'M. FLETOR (MZ)' , 'ELU-Normal [kNm]' ,  , 1 , 13
  wintitle , 2 , 'FRAME #%nthFrame%' , 'F. NORMAL'      , 'ELU-Normal [kN]'  ,  , 1 , 13
  wintitle , 3 , 'FRAME #%nthFrame%' , 'F. CORTANTE'    , 'ELU-Normal [kN]'  ,  , 1 , 13
  wintitle , 4 , 'FRAME #%nthFrame%' , 'FLECHA'         , 'ELS-Freq. [mm]'   ,  , 1 , 13

  oplot,,'E11'
  /user,all
  /dist,all,1.2,1

  go2png,picRes
    oplot,,'E11'
    /device,vector,off
    /noerase
    /plopts,info,3

    /window , 1 , off
    /window , 2 , off
    /window , 3 , off
    /window , 4 , off

    ! Bending moment (in-plane)
    wind = 1
    /window,wind,on
    /gformat,f,,0

    /udoc,wind,cntr,top
    mpathplot , baseScale ,  , 'top_beam'  , 'label_MP'
    /udoc,wind,cntr,bottom
    mpathplot , baseScale ,  , 'bot_beam'  , 'label_MP'
    /udoc,wind,cntr,left
    mpathplot , baseScale ,  , 'left_col'  , 'label_MP'
    /udoc,wind,cntr,right
    mpathplot , baseScale ,  , 'right_col' , 'label_MP'

    /window,wind,off

    ! Axial force
    wind = 2
    /window,wind,on
    /gformat,f,,0

    /udoc,wind,cntr,top
    mpathplot , baseScale ,  , 'top_beam'  , 'label_N'
    /udoc,wind,cntr,bottom
    mpathplot , baseScale ,  , 'bot_beam'  , 'label_N'
    /udoc,wind,cntr,left
    mpathplot , baseScale ,  , 'left_col'  , 'label_N'
    /udoc,wind,cntr,right
    mpathplot , baseScale ,  , 'right_col' , 'label_N'

    /window,wind,off

    ! Shear force
    wind = 3
    /window,wind,on
    /gformat,f,,0

    /udoc,wind,cntr,top
    mpathplot , baseScale ,  , 'top_beam'  , 'label_V'
    /udoc,wind,cntr,bottom
    mpathplot , baseScale ,  , 'bot_beam'  , 'label_V'
    /udoc,wind,cntr,left
    mpathplot , baseScale ,  , 'left_col'  , 'label_V'
    /udoc,wind,cntr,right
    mpathplot , baseScale ,  , 'right_col' , 'label_V'

    /window,wind,off

    ! Deflection
    wind = 4
    /window,wind,on
    /gformat,f,,2

    /udoc,wind,cntr,top
    mpathplot , baseScale ,  , 'top_beam'  , 'label_D'
    /udoc,wind,cntr,bottom
    mpathplot , baseScale ,  , 'bot_beam'  , 'label_D'
    /udoc,wind,cntr,left
    mpathplot , baseScale ,  , 'left_col'  , 'label_D'
    /udoc,wind,cntr,right
    mpathplot , baseScale ,  , 'right_col' , 'label_D'

    /window,wind,off

  endpng,'RES_FRAME_%nthFrame%'

  /erase
  /annot,dele
  view1w
  windefa,1
  clims
  boldfont

*enddo
