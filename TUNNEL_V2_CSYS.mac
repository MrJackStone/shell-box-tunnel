! TUNNEL_V2_CSYS


! Global cartesian
clocal,CS_global,cart,0,0,0,0,0,0

!! A.  Tunnel                                                                                     !!
! Global
local  , CS_tunnel    , cart , 0               , 0 , 0              , 0 , -TunnelSlope , 0
clocal , CS_RevTunnel , cart , GEO_TUN_totEffW , 0 , GEO_TUN_length
clocal , CS_RevTunnel , cart , 0               , 0 , 0              ,   ,              , 180

csys   , CS_tunnel
clocal , CS_begSkewed , cart , 0 , 0 , 0 ,  ,  , -GEO_TUN_begSkewness
csys   , CS_revTunnel
clocal , CS_endSkewed , cart , 0 , 0 , 0 ,  ,  , -GEO_TUN_endSkewness

! Top slab
csys   , CS_tunnel
clocal , CS_roof    , cart , 0 , GEO_TUN_effH , 0 ,  , 90

local  , CS_top , CART , 0 , 0 , 0 , 0    , -90-TunnelSlope , 0
clocal , CS_top , CART , 0 , 0 , 0 , -180 , 0               , 0

! Bottom slab
csys   , CS_tunnel
clocal , CS_floor  , cart , 0 , 0 , 0 ,  , 90

local , CS_bot , cart , 0 , 0 , 0 , 0 , 90-TunnelSlope , 0

! Left wall
csys   , CS_tunnel
clocal , CS_leftWall , cart , 0 , 0 , 0 , 90 , 90

local  , CS_left , cart , 0 , 0 , 0 , 0   , -TunnelSlope , 0
clocal , CS_left , cart , 0 , 0 , 0 , 0   , 0            , -90
clocal , CS_left , cart , 0 , 0 , 0 , -90 , 0            , 0

! Right wall (also used for inner walls)
csys   , CS_tunnel
clocal , CS_rightWall , cart , GEO_TUN_totEffW , 0 , 0 , 90 , 90

local  , CS_right , cart , 0 , 0 , 0 , 0  , -TunnelSlope , 0
clocal , CS_right , cart , 0 , 0 , 0 , 0  , 0            , 90
clocal , CS_right , cart , 0 , 0 , 0 , 90 , 0            , 0

! Corbel coordinate systems
xWall = 0
*do,nthWall,1,GEO_TUN_modules+1,1
  nthCS = CS_BASE_corbelBot+nthWall
  mthCS = CS_BASE_corbelTop+nthWall

  csys,CS_Tunnel
  clocal,nthCS,CART,xWall,0,0,0,0,0

  csys,CS_Tunnel
  clocal,mthCS,CART,xWall,Eff_GalleryHeight,0,0,0,0

  xWall = xWall+GEO_TUN_effW
*enddo

! Inner wall coordinate systems
*if,GEO_TUN_modules,gt,1,then
  *do,nthWall,1,GEO_IWALL_count,1
    xWall = GEO_IWALL_x_coords(nthWall)
    zWall = GEO_IWALL_zBeg_coords(nthWall)
    csys   , CS_leftWall
    clocal , CS_innerWall%nthWall%    , cart , 0     , zWall , xWall
    csys   , CS_Tunnel
    clocal , CS_IWALL_beam%nthWall%   , cart , xWall , 0 , 0      ,    ,  , -90
    clocal , CS_IWALL_column%nthWall% , cart , 0     , 0 , 0      , 90
  *enddo
*endif


!! B.  Frames                                                                                     !!
csys   , CS_tunnel
wpcsys , -1
wprota , 90
cswpla , CS_HDW_columns , cart

csys   , CS_begSkewed
clocal , CS_FRM1_beams   , cart
clocal , CS_FRM1_columns , cart ,  ,  ,  , 90

csys   , CS_endSkewed
clocal , CS_FRM2_beams   , cart
clocal , CS_FRM2_columns , cart ,  ,  ,  , 90


!! C.  Inlets                                                                                     !!
csys,CS_tunnel
wpcsys,-1
wpoffs,GEO_TUN_totEffW/2
wprota,,,180
cswpla,CS_hdw1,cart

csys,CS_tunnel
wpcsys,-1
wpoffs,GEO_TUN_totEffW/2,,GEO_TUN_length
cswpla,CS_hdw2,cart

! Retaining walls
csys,CS_tunnel
wpcsys,-1
wprota,,,90+GEO_HDW_angle
cswpla,CS_HDW1_leftWall,cart

csys,CS_tunnel
wpcsys,-1
wpoffs,GEO_TUN_totEffW
wprota,,,-90-GEO_HDW_angle
cswpla,CS_HDW1_rightWall,cart

csys,CS_tunnel
wpcsys,-1
wpoffs,GEO_TUN_totEffW,,GEO_TUN_length
wprota,,,-90+GEO_HDW_angle
cswpla,CS_HDW2_leftWall,cart

csys,CS_tunnel
wpcsys,-1
wpoffs,,,GEO_TUN_length
wprota,,,90-GEO_HDW_angle
cswpla,CS_HDW2_rightWall,cart

! Footings
csys,CS_HDW1_leftWall
wpcsys,-1
wprota,,-90
wpoffs,,,-GEO_HDW_footDepth
cswpla,CS_HDW1_leftFoot,cart

csys,CS_HDW1_rightWall
wpcsys,-1
wprota,,-90
wpoffs,,,-GEO_HDW_footDepth
cswpla,CS_HDW1_rightFoot,cart

csys,CS_HDW2_leftWall
wpcsys,-1
wprota,,-90
wpoffs,,,-GEO_HDW_footDepth
cswpla,CS_HDW2_leftFoot,cart

csys,CS_HDW2_rightWall
wpcsys,-1
wprota,,-90
wpoffs,,,-GEO_HDW_footDepth
cswpla,CS_HDW2_rightFoot,cart


!! D.  Approach slabs                                                                             !!
csys,CS_roof
wpcsys,-1
wpoffs,,GEO_APR_positionLeft
wprota,,,180
cswpla,CS_approachLeft,cart

csys,CS_roof
wpcsys,-1
wpoffs,GEO_TUN_totEffW,GEO_APR_positionRight
cswpla,CS_approachRight,cart
