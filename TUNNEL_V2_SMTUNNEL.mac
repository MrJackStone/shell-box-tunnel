! TUNNEL_V2_SMTUNNEL


!! A.  Floor slab                                                                                 !!
csys,CS_floor

asel,none
wpcsys,-1
blc4,0,0,GEO_TUN_totEffW,GEO_TUN_length
cm,COMPA_floor,area


!! B.  External walls                                                                             !!
asel,none
csys,CS_leftWall
wpcsys,-1
blc4,0,0,GEO_TUN_effH,GEO_TUN_length
cm,COMPA_leftWall,area

asel,none
csys,CS_rightWall
wpcsys,-1
blc4,0,0,GEO_TUN_effH,GEO_TUN_length
cm,COMPA_rightWall,area


!! C.  Internal walls                                                                             !!
asel,none
*do,nthIwall,1,GEO_IWALL_count,1
  wallLength = GEO_IWALL_axial_lengths(nthIwall)
  csys,CS_innerWall%nthIwall%
  wpcsys,-1
  blc4,0,0,GEO_TUN_effH,wallLength
*enddo
cm,COMPA_innerWalls,area


!! D.  Roof slab                                                                                  !!
csys,CS_roof

asel,none
wpcsys,-1
blc4,0,0,GEO_TUN_totEffW,GEO_TUN_length
cm,COMPA_roof,area

! Slice roof gap
*if,GEO_ROOF_gapStatus,eq,1,then
  *do,nthGap,1,GEO_ROOF_gapCount,1
    zBeg = GEO_ROOF_gap_z_begs(nthGap)
    zEnd = GEO_ROOF_gap_z_ends(nthGap)

    cmsel,s,COMPA_roof
    csslicexz,zBeg
    csslicexz,zEnd
    cm,COMPA_roof,area
    asel,r,loc,y,zBeg,zEnd
    adele,all,,,1
  *enddo
  cmsel,s,COMPA_roof
  asel,r,loc,y,GEO_ROOF_gapBeg,GEO_ROOF_gapEnd
  cm,COMPA_struts,area
*endif

! NOTE:  This is necessary because using L2ACNT connection resulted in convergence issues
! N#4    As a consequence, connection between struts and walls ends up being fixed, when
!        ideally it should be pinned. Solving the L2ACNT convergence issues remains a
!        challenge.
! Glue struts to walls
*if,GEO_ROOF_gapStatus,eq,1,then
  csbackup
  csys,CS_leftWall
  cmsel,s,COMPA_struts
  *do,nthIWall,1,GEO_IWALL_count,1
    coordIWall = GEO_IWALL_x_coords(nthIWall)
    wpcsys,-1
    wpoffs,,,coordIWall
    asbw,all,,delete
  *enddo
  cm,COMPA_struts,area
  csrestore

  ! TUNNEL_V2_SMGLUE , CS_roof , 'COMPA_struts' , 'COMPA_leftWall'
  ! TUNNEL_V2_SMGLUE , CS_roof , 'COMPA_struts' , 'COMPA_rightWall'
  ! TUNNEL_V2_SMGLUE , CS_roof , 'COMPA_struts' , 'COMPA_innerWalls'
*endif

! Slice at precast girders
*if,GEO_ROOF_type,eq,1,then
  csys,CS_roof
  cmsel,s,COMPA_roof
  cmsel,u,COMPA_struts
  lsla
  ksll
  *do,nthSlice,1,GEO_girderCount-1,1
    long = GEO_girderZ(nthSlice)
    wpcsys,-1
    wpoffs,,long
    wprota,,90
    asbw,all,sepo,delete
  *enddo
  cm,COMPA_roof,area
*endif


!! E.  Oblique ends                                                                               !!
*del , end_cs ,  , nopr
*dim , end_cs ,  , 2
end_cs(1) = CS_begSkewed,CS_endSkewed
*do,nthEnd,1,2,1
  nthCS = end_cs(nthEnd)
  csys,nthCS
  wpcsys,-1
  cswpla,CS_dummy,cart

  TUNNEL_V2_SMSLICECM, 'COMPA_roof'
  TUNNEL_V2_SMSLICECM, 'COMPA_floor'
  TUNNEL_V2_SMSLICECM, 'COMPA_leftWall'
  TUNNEL_V2_SMSLICECM, 'COMPA_rightWall'
  TUNNEL_V2_SMSLICECM, 'COMPA_innerWalls'
*enddo


!! F.  Selection components                                                                       !!
TUNNEL_V2_BUILDCM  ! TODO  maybe remove this from here, place it in global SM?
