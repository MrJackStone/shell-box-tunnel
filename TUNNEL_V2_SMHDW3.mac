! TUNNEL_V2_SMHDW3 , hdwNumber
! TUNNEL_V2_SMHDW3 , ARG1


! Exported selection components:
! COMPA_HDW_slab
! COMPA_HDW[i]_slab
! COMPL_HDW[i]_slabInterface


! Prepare environment
csbackup

*if,ARG1,eq,1,then
  csSkewed = CS_begSkewed
  csAxial  = CS_tunnel
*elseif,ARG1,eq,2,then
  csSkewed = CS_endSkewed
  csAxial  = CS_revTunnel
*endif

! Get anchor positions in tunnel CS
csys,csSkewed

cmsel,s,COMPA_floor
lsla
ksll
ksel,r,loc,z,0
ksel,r,loc,y,0
*get,xMax,kp,,mxloc,x
kLeft  = kp(0    , 0 , 0)
kRight = kp(xMax , 0 , 0)

csys,csAxial

xLeft  = kx(kLeft)
yLeft  = ky(kLeft)
zLeft  = kz(kLeft)
xRight = kx(kRight)
yRight = ky(kRight)
zRight = kz(kRight)

! Draw floor slab
nextkp,'KP_slab',4

k , KP_slab(1) , xLeft  , yLeft  , zLeft
k , KP_slab(2) , xRight , yRight , zRight
k , KP_slab(3) , xLeft  , yLeft  , zLeft-GEO_HDW_length
k , KP_slab(4) , xRight , yRight , zRight-GEO_HDW_length

asel,none
a , KP_slab(1) , KP_slab(2) , KP_slab(4) , KP_slab(3)

! Create exported selection components
cm,COMPA_HDW%ARG1%_slab,area
cmpadd,'COMPA_HDW_slab'

csys,csSkewed
lsla
lsel,r,loc,z,0
cm,COMPL_HDW%ARG1%_slabInterface,line

! Clean up
csrestore
