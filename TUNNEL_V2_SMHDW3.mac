! TUNNEL_V2_SMHDW3 , hdwNumber , tunnelCS , endCS
! TUNNEL_V2_SMHDW3 , ARG1      , ARG2     , ARG3


! Exported selection components:
! COMPA_HDW[i]_slab
! COMPL_HDW[i]_slabInterface
! COMPA_HDW[i]_LEFT_wall
! COMPA_HDW[i]_LEFT_footing
! COMPL_HDW[i]_LEFT_wallInterface
! COMPK_HDW[i]_LEFT_wallTopEdge
! COMPA_HDW[i]_RIGHT_wall
! COMPA_HDW[i]_RIGHT_footing
! COMPL_HDW[i]_RIGHT_wallInterface
! COMPK_HDW[i]_RIGHT_wallTopEdge


! Prepare environment
csbackup

!! A.  Floor slab                                                                                 !!
! Get anchor positions in tunnel CS
csys,ARG3

cmsel,s,COMPA_floor
lsla
ksll
ksel,r,loc,z,0
ksel,r,loc,y,0
*get,xMax,kp,,mxloc,x
kLeft  = kp(0    , 0 , 0)
kRight = kp(xMax , 0 , 0)

csys,ARG2

xLeft  = kx(kLeft)
yLeft  = ky(kLeft)
zLeft  = kz(kLeft)
xRight = kx(kRight)
yRight = ky(kRight)
zRight = kz(kRight)

! Draw floor slab
nextkp,'KP_slab',4

k , KP_slab(1) , xLeft  , yLeft  , zLeft
k , KP_slab(2) , xRight , yRight , zRight
k , KP_slab(3) , xLeft  , yLeft  , zLeft-GEO_HDW%ARG1%_slabLength
k , KP_slab(4) , xRight , yRight , zRight-GEO_HDW%ARG1%_slabLength

asel,none
a , KP_slab(1) , KP_slab(2) , KP_slab(4) , KP_slab(3)

! Create exported selection components
cm,COMPA_HDW%ARG1%_slab,area

csys,ARG3
lsla
lsel,r,loc,z,0
cm,COMPL_HDW%ARG1%_slabTunnelInterface,line


!! B.  Draw retaining walls                                                                       !!
*if,GEO_HDW%ARG1%_LEFT_wallStatus,eq,1,then
  csLWall = CS_HDW%ARG1%_leftWall
  csLFoot = CS_HDW%ARG1%_leftFoot
  lLength = GEO_HDW%ARG1%_LEFT_wallLength
  lHeight = GEO_HDW%ARG1%_LEFT_wallHeight
  lTop    = GEO_HDW%ARG1%_LEFT_wallTopLength
  lRadius = GEO_HDW%ARG1%_LEFT_cornerRadius
  lWidth  = GEO_HDW%ARG1%_LEFT_footWidth
  lSign   = GEO_HDW%ARG1%_LEFT_xSign

  TUNNEL_V2_SMRETWALL , csLWall , lLength , lHeight , lTop , lRadius , csLFoot , lWidth , lSign , 'HDW%ARG1%_LEFT'
*endif

*if,GEO_HDW%ARG1%_RIGHT_wallStatus,eq,1,then
  csRWall = CS_HDW%ARG1%_rightWall
  csRFoot = CS_HDW%ARG1%_rightFoot
  rLength = GEO_HDW%ARG1%_RIGHT_wallLength
  rHeight = GEO_HDW%ARG1%_RIGHT_wallHeight
  rTop    = GEO_HDW%ARG1%_RIGHT_wallTopLength
  rRadius = GEO_HDW%ARG1%_RIGHT_cornerRadius
  rWidth  = GEO_HDW%ARG1%_RIGHT_footWidth
  rSign   = GEO_HDW%ARG1%_RIGHT_xSign

  TUNNEL_V2_SMRETWALL , csRWall , rLength , rHeight , rTop , rRadius , csRFoot , rWidth , rSign , 'HDW%ARG1%_RIGHT'
*endif


!! C.  Clean up                                                                                   !!
csrestore
*del,KP_slab,,nopr
