! TUNNEL_V2_SMCORBEL


! Roof slab
CMSEL,S,COMPA_Roof
*do,nthModule,0,GEO_TUN_modules,1
  xWall = nthModule*Eff_GalleryWidth
  CSSLICEYZ,xWall+GEO_corbelXOff_TOP
  CSSLICEYZ,xWall-GEO_corbelXOff_TOP
  CM,COMPA_temp,AREA

  ASEL,R,LOC,X,xWall,xWall+GEO_corbelXOff_TOP
  CMSEL,A,COMPA_ROOF_rightCorbel
  CM,COMPA_ROOF_rightCorbel,AREA

  CMSEL,S,COMPA_temp
  ASEL,R,LOC,X,xWall,xWall-GEO_corbelXOff_TOP
  CMSEL,A,COMPA_ROOF_leftCorbel
  CM,COMPA_ROOF_leftCorbel,AREA

  CMSEL,S,COMPA_temp
  ASEL,R,LOC,X,xWall-GEO_corbelXOff_TOP,xWall+GEO_corbelXOff_TOP
  CMSEL,U,COMPA_ROOF_leftCorbel
  CMSEL,U,COMPA_ROOF_rightCorbel
  CM,COMPA_ROOF_flat,AREA

  *get,compType,COMP,COMPA_Roof,TYPE
  *if,compType,EQ,0,THEN
    ASEL,U,AREA,,ALL
    CM,COMPA_Roof,AREA
  *else
    CMSEL,S,COMPA_Roof
  *endif
  CMSEL,A,COMPA_temp
  CM,COMPA_Roof,AREA
*enddo

! Floor slab
CMSEL,S,COMPA_Floor
*do,nthModule,0,GEO_TUN_modules,1
  xWall=nthModule*Eff_GalleryWidth
  CSSLICEYZ,xWall+GEO_corbelXOff_BOT
  CSSLICEYZ,xWall-GEO_corbelXOff_BOT
  CM,COMPA_temp,AREA

  ASEL,R,LOC,X,xWall,xWall+GEO_corbelXOff_BOT
  CMSEL,A,COMPA_FLOOR_rightCorbel
  CM,COMPA_FLOOR_rightCorbel,AREA

  CMSEL,S,COMPA_temp
  ASEL,R,LOC,X,xWall,xWall-GEO_corbelXOff_BOT
  CMSEL,A,COMPA_FLOOR_leftCorbel
  CM,COMPA_FLOOR_leftCorbel,AREA

  CMSEL,S,COMPA_temp
  ASEL,R,LOC,X,xWall-GEO_corbelXOff_BOT,xWall+GEO_corbelXOff_BOT
  CMSEL,U,COMPA_FLOOR_leftCorbel
  CMSEL,U,COMPA_FLOOR_rightCorbel
  CM,COMPA_FLOOR_flat,AREA

  *get,compType,COMP,COMPA_Floor,TYPE
  *if,compType,EQ,0,THEN
    asel,none
    CM,COMPA_Floor,AREA
  *else
    cmsel,s,COMPA_Floor
  *endif
  cmsel,a,compa_temp
  cm,COMPA_Floor,area
*enddo

! Walls
cmsel,s,COMPA_Walls
csslicexz,GEO_corbelYOff_BOT
csslicexz,Eff_GalleryHeight-GEO_corbelYOff_TOP
cm,COMPA_temp,area

asel,R,LOC,Y,GEO_corbelYOff_BOT,Eff_GalleryHeight-GEO_corbelYOff_TOP
cm,COMPA_WALL_flat,area

CMSEL,S,COMPA_temp
ASEL,R,LOC,Y,0,GEO_corbelYOff_BOT
CM,COMPA_WALL_botCorbel,AREA

CMSEL,S,COMPA_temp
ASEL,R,LOC,Y,Eff_GalleryHeight-GEO_corbelYOff_TOP,Eff_GalleryHeight
CM,COMPA_WALL_topCorbel,AREA

! Headwalls
*if,parType,EQ,0,AND,GEO_HDW_status,GT,0,THEN
  x_LEFT_Top  = GEO_corbelXOff_TOP
  x_LEFT_Bot  = GEO_corbelXOff_BOT
  x_RIGHT_Top = GEO_TUN_modules*Eff_GalleryWidth-GEO_corbelXOff_TOP
  x_RIGHT_Bot = GEO_TUN_modules*Eff_GalleryWidth-GEO_corbelXOff_BOT

  y_BOT = GEO_corbelYOff_BOT
  y_TOP = Eff_GalleryHeight-GEO_corbelYOff_TOP

  CMSEL,S,COMPA_HDW_Slab
  CSSLICEYZ,x_LEFT_Bot
  CSSLICEYZ,x_RIGHT_Bot

  CMSEL,S,COMPA_HDW_Stiffener
  CSSLICEYZ,x_LEFT_Bot
  CSSLICEYZ,x_RIGHT_Bot

  CMSEL,S,COMPA_HDW_BackBeam
  CSSLICEYZ,x_LEFT_Bot
  CSSLICEYZ,x_RIGHT_Bot

  CMSEL,S,COMPA_HDW_BotBeam
  CSSLICEYZ,x_LEFT_Bot
  CSSLICEYZ,x_RIGHT_Bot

  CMSEL,S,COMPA_HDW_TopBeam
  CSSLICEYZ,x_LEFT_Top
  CSSLICEYZ,x_RIGHT_Top

  CMSEL,S,COMPA_HDW_LeftWall
  CSSLICEXZ,y_BOT
  CSSLICEXZ,y_TOP

  CMSEL,S,COMPA_HDW_RightWall
  CSSLICEXZ,y_BOT
  CSSLICEXZ,y_TOP
*endif

! Rebuild selection components
TUNNEL_V2_BUILDCM
TUNNEL_V2_HDWBUILDCM
