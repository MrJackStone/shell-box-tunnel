! TUNNEL_V2_ETAB


! Output element tables:

! Auxiliary: ----------------------------------- 1
! ZEROS

! DOFs: --------------------------------------- 24
! DW_UX      EMN_UX     EMX_UX     EAB_UX
! DW_UY      EMN_UY     EMX_UY     EAB_UY
! DW_UZ      EMN_UZ     EMX_UZ     EAB_UZ
! DW_ROTX    EMN_ROTX   EMX_ROTX   EAB_ROTX
! DW_ROTY    EMN_ROTY   EMX_ROTY   EAB_ROTY
! DW_ROTZ    EMN_ROTZ   EMX_ROTZ   EAB_ROTZ

! SHELL181 (raw): ----------------------------- 35
! DW_MX      EMN_MX     EMX_MX     EAB_MX
! DW_MY      EMN_MY     EMX_MY     EAB_MY
! DW_NX      EMN_NX     EMX_NX     EAB_NX
! DW_NY      EMN_NY     EMX_NY     EAB_NY
! DW_QX      EMN_QX     EMX_QX     EAB_QX
! DW_QY      EMN_QY     EMX_QY     EAB_QY
! DW_Q       EMN_Q      EMX_Q      EAB_Q
! -          EPOS_MY    ENEG_MX    -
! -          EPOS_MX    ENEG_MY    -
! DLT_MX     DLT_MY     DLT_NX     DLT_NY
! DLT_Q      -          -          -

! SHELL181 (averaged by convolution): --------- 14
! ADW_MX     AEMN_MX    AEMX_MX    AEAB_MX
! ADW_MY     AEMN_MY    AEMX_MY    AEAB_MY
! ADW_Q      -          -          AEAB_Q
! -          AEPOS_MX   AENEG_MX   -
! -          AEPOS_MY   AENEG_MY   -

! Service limit state deflections: ------------ 12
! UX_gk      UX_qk      UX_k       UX_frq
! UY_gk      UY_qk      UY_k       UY_frq
! UZ_gk      UZ_qk      UZ_k       UZ_frq
!
! Legend:
!    U?_gk   -  dead loads, characteristic value
!    U?_qk   -  live loads, characteristic value
!    U?_k    -  dead + live loads, characteristic values
!    U?_frq  -  SLS frequent combination (uses PSI1)

! Reaction soil pressures: --------------------- 4
! SIGY_gk    SIGY_qk    SIGY_k     SIGY_a
!
! Legend:
!    SIGY_?? -  sigma (normal stress) in the Y direction
!    SIGY_gk -  dead loads, characteristic value
!    SIGY_qk -  live loads, characteristic value
!    SIGY_k  -  dead + live loads, characteristic values
!    SIGY_a  -  added pressure (total - dead)
!
! Note:
!    SIGY_qk seems to be useless. Because of nonlinear effects,
!    the best way to calculate added pressure is by subtracting
!    dead load (g) pressures from total pressure (g+k).

! Applied soil pressures: --------------------- (5)
! LS[i]_PX   LS[i]_PY   LS[i]_PZ   -
! NET[i]_PX  NET[i]_PY
!
! Note:
!    These element tables are immediately deleted after used
!    to generate plots, and therefore do not add to the overall
!    limit of 200 element tables.
!    Index 'i' stands for load step number, making the total
!    count of element tables of this kind dependent upon a
!    volatile input parameter (number of load train positions).
!    For i=1 (dead loads) the 'NET' ETABLEs are not created.

! NOTE: all characteristic values (k) include impact coefficient

! TOTAL element table count: ------------------ 90 (95)


/post1

allsel

! Auxiliary element tables
*vget  , element_numbers       , elem    ,         , elist
*get   , ec                    , elem    ,         , count
etable , 'DUMMY'               , volu
sadd   , 'ZEROS'               , 'DUMMY' , 'DUMMY' , 1     , -1
detab  , element_numbers(1:ec) , 'ZEROS' , 0
etable , 'DUMMY'               , eras


!! Dead weight results (first load step) element tables                                           !!
allsel
set,first

defetabs , 0   , 'DW_' , 1e3  , 1
defetabs , 181 , 'DW_' , 1e-3 , 1

! SHELL181 oblique shear force
etbsqsum , 'DW_Q' , 'DW_QX' , 'DW_QY'


!! Envelope including dead & live loads:                                                          !!
baseTableCount = 12

*del , etable_suffixes ,      , nopr
*del , etable_scales   ,      , nopr
*dim , etable_suffixes , char , baseTableCount
*dim , etable_scales   ,      , baseTableCount

etable_suffixes(1)  = 'UX' , 'UY' , 'UZ'  , 'ROTX' , 'ROTY' , 'ROTZ'
etable_scales(1)    = 1e3  , 1e3  , 1e3   , 1e3    , 1e3    , 1e3
etable_suffixes(7)  = 'MX' , 'MY' , 'NX'  , 'NY'   , 'QX'   , 'QY'
etable_scales(7)    = 1e-3 , 1e-3 , 1e-3  , 1e-3   , 1e-3   , 1e-3

! Create element tables that update with REFL
defetabs , 0   ,  , 1 , 0
defetabs , 181 ,  , 1 , 0

! Loop over load steps updating element tables
etenv,1,LoadStepCount,'etable_suffixes'

! Scale envelope element tables
*do,nthEtable,1,baseTableCount,1
  etSuffix = etable_suffixes(nthEtable)
  etScale  = etable_scales(nthEtable)
  sadd,'EMX_%etSuffix%','EMX_%etSuffix%',,etScale
  sadd,'EMN_%etSuffix%','EMN_%etSuffix%',,etScale
  sadd,'EAB_%etSuffix%','EAB_%etSuffix%',,etScale
*enddo

! Delete unused tables
*do,nthEtable,1,baseTableCount,1
  etSuffix = etable_suffixes(nthEtable)
  etable,'%etSuffix%',eras
*enddo

! Oblique shear force
etbsqsum , 'EAB_Q' , 'EAB_QX' , 'EAB_QY'
etbsqsum , 'EMX_Q' , 'EMX_QX' , 'EMX_QY'
etbsqsum , 'EMN_Q' , 'EMN_QX' , 'EMN_QY'

! Positive/negative-only bending moments
smax   , 'ENEG_MX' , 'EMX_MX' , 'ZEROS'
smax   , 'EPOS_MX' , 'EMN_MX' , 'ZEROS'  , -1
smax   , 'ENEG_MY' , 'EMX_MY' , 'ZEROS'
smax   , 'EPOS_MY' , 'EMN_MY' , 'ZEROS'  , -1

! Variation
difTableCount = 5

*del , diftable_suffixes ,      , nopr
*dim , diftable_suffixes , char , difTableCount
diftable_suffixes(1) = 'MX' , 'MY' , 'NX' , 'NY' , 'Q'

*do,nthEtable,1,difTableCount,1
  suffix = diftable_suffixes(nthEtable)
  sadd , 'MXDIF' , 'EMX_%suffix%' , 'DW_%suffix%' , 1 , -1
  sadd , 'MNDIF' , 'EMN_%suffix%' , 'DW_%suffix%' , 1 , -1
  sabs , 1
  smax , 'DLT_ELU' , 'MXDIF' , 'MNDIF'
  sadd , 'DLT_%suffix%' , 'DLT_ELU' ,  , 1/LOAD_gammaQ
*enddo

etable , 'MXDIF'   , erase
etable , 'MNDIF'   , erase
etable , 'DLT_ELU' , erase


!! Element tables calculated based on DW & ENV values                                             !!
! NOTE: because of the AREMA approach to calculate impact coefficient (depth-dependent value),
!       it is not possible to cleanly remove the influence of said impact coefficient in post.
!       Therefore, loads labeled as "characteristic" (k index) still include the effect of the
!       impact coefficient, if one was applied.

! Deflections
sadd   , 'UX_g'   , 'DW_UX'  ,         , 1
sadd   , 'UX_d'   , 'EMN_UX' ,         , 1
sadd   , 'UX_gk'  , 'UX_g'   ,         , 1/LOAD_gammaG
sadd   , 'UX_q'   , 'UX_d'   , 'UX_g'  , 1             , -1
sadd   , 'UX_qk'  , 'UX_q'   ,         , 1/LOAD_gammaQ
sadd   , 'UX_k'   , 'UX_g'   , 'UX_q'  , 1             , 1
sadd   , 'UX_frq' , 'UX_gk'  , 'UX_qk' , 1             , LOAD_psi1
etable , 'UX_g'   , eras
etable , 'UX_d'   , eras
etable , 'UX_q'   , eras

sadd   , 'UY_g'   , 'DW_UY'  ,         , 1
sadd   , 'UY_d'   , 'EMN_UY' ,         , 1
sadd   , 'UY_gk'  , 'UY_g'   ,         , 1/LOAD_gammaG
sadd   , 'UY_q'   , 'UY_d'   , 'UY_g'  , 1             , -1
sadd   , 'UY_qk'  , 'UY_q'   ,         , 1/LOAD_gammaQ
sadd   , 'UY_k'   , 'UY_g'   , 'UY_q'  , 1             , 1
sadd   , 'UY_frq' , 'UY_gk'  , 'UY_qk' , 1             , LOAD_psi1
etable , 'UY_g'   , eras
etable , 'UY_d'   , eras
etable , 'UY_q'   , eras

sadd   , 'UZ_g'   , 'DW_UZ'  ,         , 1
sadd   , 'UZ_d'   , 'EMN_UZ' ,         , 1
sadd   , 'UZ_gk'  , 'UZ_g'   ,         , 1/LOAD_gammaG
sadd   , 'UZ_q'   , 'UZ_d'   , 'UZ_g'  , 1             , -1
sadd   , 'UZ_qk'  , 'UZ_q'   ,         , 1/LOAD_gammaQ
sadd   , 'UZ_k'   , 'UZ_g'   , 'UZ_q'  , 1             , 1
sadd   , 'UZ_frq' , 'UZ_gk'  , 'UZ_qk' , 1             , LOAD_psi1
etable , 'UZ_g'   , eras
etable , 'UZ_d'   , eras
etable , 'UZ_q'   , eras


!! Soil pressure                                                                                  !!
Nmm2kNm = 1e-6

sadd , 'SIGY_gk' , 'UY_gk' ,  , MAT_SOIL_stiffness*Nmm2kNm*(-1)
sadd , 'SIGY_qk' , 'UY_qk' ,  , MAT_SOIL_stiffness*Nmm2kNm*(-1)
sadd , 'SIGY_k'  , 'UY_k'  ,  , MAT_SOIL_stiffness*Nmm2kNm*(-1)
sadd , 'SIGY_a' , 'SIGY_k' , 'SIGY_gk' , 1 , -1

smax , 'SIGY_gk' , 'SIGY_gk' , 'ZEROS'
smax , 'SIGY_qk' , 'SIGY_qk' , 'ZEROS'
smax , 'SIGY_k'  , 'SIGY_k'  , 'ZEROS'
smax , 'SIGY_a'  , 'SIGY_a'  , 'ZEROS'


!! Average results by performing convolution                                                      !!
TUNNEL_V2_ETCVL
