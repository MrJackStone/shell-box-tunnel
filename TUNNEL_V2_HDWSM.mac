! TUNNEL_V2_HDWSM, refCS, CS_HDW_LeftWall, CS_HDW_RightWall
! TUNNEL_V2_HDWSM,  arg1,            arg2,             arg3


! Exported solid model components:
! COMPA_HDW_BackBeam
! COMPA_HDW_BotBeam
! COMPA_HDW_leftColumn
! COMPA_HDW_rightColumn
! COMPA_HDW_Columns
! COMPA_HDW_Footing
! COMPA_HDW_LeftWall
! COMPA_HDW_RightWall
! COMPA_HDW_Slab
! COMPA_HDW_Stiffener
! COMPA_HDW_TopBeam
! COMPA_HDW_Walls
! COMPA_Headwalls


CS_HDW_LeftWall  = arg2
CS_HDW_RightWall = arg3

!!                                                                                                !!
!! A.  Geometric parameters                                                                       !!
!!                                                                                                !!
GEO_HDW_Height = Eff_GalleryHeight+GEO_FRAME_headBeamHeight

x1  = 0
x2  = GEO_HDW_stiffXOff
x3  = GEO_TUN_totEffW-GEO_HDW_stiffXOff
x4  = GEO_TUN_totEffW
x5  = -GEO_HDW_length*SIN(GEO_InletRadians)
x6  = x5+GEO_HDW_stiffXOff
x7  = x3+(SIN(GEO_InletRadians)*GEO_HDW_length)
x8  = x7+GEO_HDW_stiffXOff
x9  = x1-(GEO_HDW_footWidth/COS(GEO_InletRadians))
x10 = x1
x11 = x4
x12 = x11+(GEO_HDW_footWidth/COS(GEO_InletRadians))
x13 = x5-(GEO_HDW_footWidth/COS(GEO_InletRadians))
x14 = x5
x15 = x8
x16 = x15+(GEO_HDW_footWidth/COS(GEO_InletRadians))
x17 = x1
x18 = x4
x19 = x1-GEO_FRAME_columnWidth
x20 = x4+GEO_FRAME_columnWidth

yTop  = Eff_GalleryHeight
yFoot = -GEO_HDW_footDepth
yBeam = GEO_HDW_Height

zEnd   = GEO_TUN_length
zFront = GEO_TUN_length+GEO_HDW_length

csys,arg1
allsel

! Existing keypoints
KP1   = kp(x1,0,zEnd)
KP2   = kp(x2,0,zEnd)
KP3   = kp(x3,0,zEnd)
KP4   = kp(x4,0,zEnd)
KP27  = kp(x1,yTop,zEnd)
KP29  = kp(x4,yTop,zEnd)
KP101 = kp(x1,GEO_HDW_wingBottomOffset,zEnd)
KP102 = kp(x4,GEO_HDW_wingBottomOffset,zEnd)

! Existing keypoint collections for tunnels with more than one module
*if,GEO_TUN_modules,gt,1,then
  ksel,none
  cm,COMPK_IKP_2to3,kp
  cm,COMPK_IKP_BLtoBR,kp
  xi = x2
  *do,nthModule,1,GEO_TUN_modules-1,1
    xi = xi+GEO_TUN_effW

    ksel,s,loc,y,0
    KP_i = kp(xi,0,zEnd)
    ksel,s,kp,,KP_i
    cmsel,a,COMPK_IKP_2to3
    cm,COMPK_IKP_2to3,kp

    ksel,s,loc,y,GEO_TUN_effH
    KP_j = kp(xi,GEO_TUN_effH,zEnd)
    ksel,s,kp,,KP_j
    cmsel,a,COMPK_IKP_BLtoBR
    cm,COMPK_IKP_BLtoBR,kp
  *enddo
*else
  ksel,none
  cm , COMPK_IKP_2to3   , kp
  cm , COMPK_IKP_BLtoBR , kp
*endif
allsel

! New keypoints
*get,KPMaxD,kp,,num,maxd
KP5  = KPMaxD+1
KP6  = KPMaxD+2
KP7  = KPMaxD+3
KP8  = KPMaxD+4
KP9  = KPMaxD+5
KP10 = KPMaxD+6
KP11 = KPMaxD+7
KP12 = KPMaxD+8
KP13 = KPMaxD+9
KP14 = KPMaxD+10
KP15 = KPMaxD+11
KP16 = KPMaxD+12
KP17 = KPMaxD+13
KP18 = KPMaxD+14

Left_PI1  = KPMaxD+15
Left_AX1  = KPMaxD+16
Left_PI2  = KPMaxD+17
Left_AX2  = KPMaxD+18
Right_PI1 = KPMaxD+19
Right_AX1 = KPMaxD+20
Right_PI2 = KPMaxD+21
Right_AX2 = KPMaxD+22

KP28 = KPMaxD+23
KP30 = KPMaxD+24

KP21 = KPMaxD+25
KP25 = KPMaxD+26

KP19 = KPMaxD+27
KP20 = KPMaxD+28
KP22 = KPMaxD+29
KP23 = KPMaxD+30

! Keypoints for wall height at the front greater than zero
KP31 = KPMaxD+31
KP32 = KPMaxD+32

*if,GEO_HDW_type,eq,0,then

  !!                                                                                            !!
  !! B.  Full inlet                                                                             !!
  !!                                                                                            !!

  !! Keypoints                                                                                  !!
  k , KP5 , x5 , 0 , zFront
  k , KP6 , x6 , 0 , zFront
  k , KP7 , x7 , 0 , zFront
  k , KP8 , x8 , 0 , zFront

  k , KP9  , x9  , yFoot , zEnd
  k , KP10 , x10 , yFoot , zEnd
  k , KP11 , x11 , yFoot , zEnd
  k , KP12 , x12 , yFoot , zEnd

  k , KP13 , x13 , yFoot , zFront
  k , KP14 , x14 , yFoot , zFront
  k , KP15 , x15 , yFoot , zFront
  k , KP16 , x16 , yFoot , zFront

  k , KP17 , x17 , yBeam , zEnd
  k , KP18 , x18 , yBeam , zEnd

  *if,GEO_HDW_topLength,gt,2*GEO_FRAME_headBeamWidth,then

    x17b = -GEO_HDW_topLength*sin(GEO_InletRadians)
    x18b = x18+GEO_HDW_topLength*sin(GEO_InletRadians)

    zFlatTop = zEnd+(GEO_TUN_effH*sin(GEO_TUN_slopeRad))+GEO_HDW_topLength*cos(GEO_InletRadians)

    KP17b = KPMaxD+25
    KP18b = KPMaxD+26

    k , KP17b , x17b , yBeam , zFlatTop
    k , KP18b , x18b , yBeam , zFlatTop
  *endif

  *if,GEO_FRAME_columnWidth,gt,0,then

    *if,GEO_FRAME_columnWidth,ne,GEO_HDW_footWidth,then
      k , KP19 , x19 , yFoot , zEnd
      k , KP20 , x20 , yFoot , zEnd
    *else
      KP19 = KP9
      KP20 = KP12
    *endif

    k , KP22 , x19 , yBeam , zEnd
    k , KP23 , x20 , yBeam , zEnd

  *endif

  *if,GEO_HDW_endH,gt,0,then
    k , KP31 , x5 , GEO_HDW_endH , zFront
    k , KP32 , x8 , GEO_HDW_endH , zFront
    KFL = KP31
    KFR = KP32
  *else
    KFL = KP5
    KFR = KP8
  *endif

  ! Left wall
  cskp , CS_HDW_LeftWall , cart , KP5 , KP1 , KP17
  csys , CS_HDW_LeftWall

  KP17x    = kx(KP17)
  KP17y    = ky(KP17)
  xCenter1 = GEO_HDW_CornerRadius
  yCenter1 = GEO_HDW_endH
  xCenter2 = KP17x
  yCenter2 = KP17y-GEO_HDW_CornerRadius

  ThetaAngle         = atan((xCenter2-xCenter1-GEO_HDW_topLength)/(yCenter2-yCenter1))
  HorDelta           = GEO_HDW_CornerRadius*cos(ThetaAngle)
  VerDelta           = GEO_HDW_CornerRadius*sin(ThetaAngle)
  HorHalfDelta       = GEO_HDW_CornerRadius*cos(ThetaAngle/2)
  VerHalfDelta       = GEO_HDW_CornerRadius*sin(ThetaAngle/2)
  HorHalfDeltaPlus45 = GEO_HDW_CornerRadius*cos((PI/4)+(ThetaAngle/2))
  VerHalfDeltaPlus45 = GEO_HDW_CornerRadius*sin((PI/4)+(ThetaAngle/2))

  k , Left_PI1 , xCenter1-HorDelta                             , yCenter1+VerDelta           , 0
  k , Left_AX1 , xCenter1-HorHalfDelta                         , yCenter1+VerHalfDelta       , 0
  k , Left_PI2 , xCenter2-HorDelta-GEO_HDW_topLength           , yCenter2+VerDelta           , 0
  k , Left_AX2 , xCenter2-HorHalfDeltaPlus45-GEO_HDW_topLength , yCenter2+VerHalfDeltaPlus45 , 0

  ! Right wall
  cskp,CS_HDW_RightWall,CART,KP8,KP4,KP18
  csys,CS_HDW_RightWall
  k , Right_PI1 , xCenter1-HorDelta                             , yCenter1+VerDelta           , 0
  k , Right_AX1 , xCenter1-HorHalfDelta                         , yCenter1+VerHalfDelta       , 0
  k , Right_PI2 , xCenter2-HorDelta-GEO_HDW_topLength           , yCenter2+VerDelta           , 0
  k , Right_AX2 , xCenter2-HorHalfDeltaPlus45-GEO_HDW_topLength , yCenter2+VerHalfDeltaPlus45 , 0

  csys,arg1

  !! Lines                                                                                      !!
  allsel
  lsel,none

  ksel,s,kp,,KP1
  ksel,a,kp,,KP2
  lslk,s,1
  *get,L1,line,0,num,max
  lsel,none

  ksel,s,kp,,KP2
  ksel,a,kp,,KP3
  cmsel,a,COMPK_IKP_2to3
  lslk,s,1
  *get,L2,line,0,num,max
  cm,COMPL_IL_2,line
  lsel,none

  ksel,s,kp,,KP3
  ksel,a,kp,,KP4
  lslk,s,1
  *get,L3,line,0,num,max
  lsel,none
  ksel,all

  l,KP5,KP6
  *get,L4,line,0,num,max
  l,KP6,KP7
  *get,L5,line,0,num,max
  l,KP7,KP8
  *get,L6,line,0,num,max
  l,KP1,KP5
  *get,L7,line,0,num,max
  l,KP2,KP6
  *get,L8,line,0,num,max
  l,KP3,KP7
  *get,L9,line,0,num,max
  l,KP4,KP8
  *get,L10,line,0,num,max

  *if,GEO_FRAME_columnWidth,gt,GEO_HDW_footWidth,then
    l,KP9,KP10
    *get,L11,line,,num,max
    l,KP11,KP12
    *get,L12,line,,num,max
    l,KP9,KP19
    *get,L11c,line,,num,max
    l,KP12,KP20
    *get,L12c,line,,num,max
  *elseif,GEO_FRAME_columnWidth,gt,0,and,GEO_HDW_columnWidth,le,GEO_HDW_footWidth,then
    l,KP9,KP19
    *get,L11a,line,,num,max
    l,KP10,KP19
    *get,L11b,line,,num,max
    l,KP12,KP20
    *get,L12a,line,,num,max
    l,KP11,KP20
    *get,L12b,line,,num,max
  *else
    l,KP9,KP10
    *get,L11,line,,num,max
    l,KP11,KP12
    *get,L12,line,,num,max
  *endif

  l,KP13,KP14
  *get,L13,line,0,num,max
  l,KP15,KP16
  *get,L14,line,0,num,max
  l,KP9,KP13
  *get,L15,line,0,num,max
  l,KP10,KP14
  *get,L16,line,0,num,max
  l,KP11,KP15
  *get,L17,line,0,num,max
  l,KP12,KP16
  *get,L18,line,0,num,max
  l,KP10,KP1
  *get,L19,line,0,num,max
  l,KP11,KP4
  *get,L20,line,0,num,max
  l,KP14,KP5
  *get,L21,line,0,num,max
  l,KP15,KP8
  *get,L22,line,0,num,max

  l,KP1,KP17
  *get,L23,line,0,num,max
  l,KP4,KP18
  *get,L24,line,0,num,max
  larc,Left_PI1,KFL,Left_AX1
  *get,L25,line,0,num,max

  *if,GEO_HDW_topLength,gt,2*GEO_FRAME_headBeamWidth,then
    larc,KP17b,Left_PI2,Left_AX2
    *get,L26,line,0,num,max
    L,KP17,KP17b
    *get,L26b,line,0,num,max
  *else
    larc,KP17,Left_PI2,Left_AX2
    *get,L26,line,0,num,max
  *endif

  L,Left_PI1,Left_PI2
  *get,L27,line,0,num,max

  larc,Right_PI1,KFR,Right_AX1
  *get,L28,line,0,num,max

  *if,GEO_HDW_topLength,gt,2*GEO_FRAME_headBeamWidth,then
    larc,KP18b,Right_PI2,Right_AX2
    *get,L29,line,0,num,max
    L,KP18,KP18b
    *get,L29b,line,0,num,max
  *else
    larc,KP18,Right_PI2,Right_AX2
    *get,L29,line,0,num,max
  *endif

  l,Right_PI1,Right_PI2
  *get,L30,line,0,num,max

  *if,GEO_HDW_endH,gt,0,then
    l,KP5,KP31
    *get,FHL,line,,num,max
    l,KP8,KP32
    *get,FHR,line,,num,max
  *endif

  ! Columns
  *if,GEO_FRAME_columnWidth,gt,0,then
    ! Left column
    l,KP17,KP22
    *get,L31,line,,num,max
    l,KP22,KP19
    *get,L32,line,,num,max
    ! Right column
    l,KP18,KP23
    *get,L33,line,,num,max
    l,KP23,KP20
    *get,L34,line,,num,max
  *endif

  allsel


  !! Areas                                                                                      !!
  asel,none

  al,L7,L4,L8,L1
  al,L9,L6,L10,L3
  cmsel,a,COMPA_HDW_Stiffener
  cm,COMPA_HDW_Stiffener,area
  asel,none

  cmsel,s,COMPL_IL_2
  lsel,a,line,,L2
  lsel,a,line,,L5
  lsel,a,line,,L8
  lsel,a,line,,L9
  !AL,L8,L5,L9,L2
  al,all,L8
  cmsel,A,COMPA_HDW_Slab
  cm,COMPA_HDW_Slab,area
  asel,none
  lsel,all

  ! Footings
  *if,GEO_FRAME_columnWidth,gt,0,and,GEO_HDW_columnWidth,le,GEO_HDW_footWidth,then
    al,L15,L13,L16,L11a,L11b
    al,L17,L14,L18,L12a,L12b
  *else
    al,L15,L13,L16,L11
    al,L17,L14,L18,L12
  *endif

  cmsel,A,COMPA_HDW_Footing
  cm,COMPA_HDW_Footing,area
  asel,none

  ! Columns
  *if,GEO_FRAME_columnWidth,gt,0,then
    asel,none
    a,KP1,KP27,KP17,KP22,KP19,KP10
    csslicexz,GEO_TUN_effH
    csslicexz,0
    cmsel,a,COMPA_HDW_leftColumn
    cm,COMPA_HDW_leftColumn,area

    asel,none
    a,KP4,KP29,KP18,KP23,KP20,KP11
    csslicexz,GEO_TUN_effH
    csslicexz,0
    cmsel,a,COMPA_HDW_rightColumn
    cm,COMPA_HDW_rightColumn,area

    cmsel,s,COMPA_HDW_Columns
    cmsel,a,COMPA_HDW_leftColumn
    cmsel,a,COMPA_HDW_rightColumn
    cm,COMPA_HDW_Columns,area
    asel,none
  *endif

  *if,GEO_HDW_topLength,gt,2*GEO_FRAME_headBeamWidth,then
    al,L21,L7,L19,L16
    *if,GEO_HDW_endH,gt,0,then
      al,L27,L26,L26b,L23,L7,L25,FHL
    *else
      al,L27,L26,L26b,L23,L7,L25
    *endif
    cmsel,A,COMPA_HDW_RightWall
    cm,COMPA_HDW_RightWall,area
    asel,none
  *else
    al,L21,L7,L19,L16
    *if,GEO_HDW_endH,gt,0,then
      al,L27,L26,L23,L7,L25,FHL
    *else
      al,L27,L26,L23,L7,L25
    *endif
    cmsel,A,COMPA_HDW_RightWall
    cm,COMPA_HDW_RightWall,area
    asel,none
  *endif

  *if,GEO_HDW_topLength,GT,2*GEO_FRAME_headBeamWidth,THEN
    al,L22,L10,L20,L17
    *if,GEO_HDW_endH,gt,0,then
      al,L30,L29,L29b,L24,L10,L28,FHR
    *else
      al,L30,L29,L29b,L24,L10,L28
    *endif
    cmsel,A,COMPA_HDW_LeftWall
    cm,COMPA_HDW_LeftWall,area
  *else
    al,L22,L10,L20,L17
    *if,GEO_HDW_endH,gt,0,then
      al,L30,L29,L24,L10,L28,FHR
    *else
      al,L30,L29,L24,L10,L28
    *endif
    cmsel,A,COMPA_HDW_LeftWall
    cm,COMPA_HDW_LeftWall,area
  *endif

  cmsel,s,COMPA_HDW_RightWall
  lsla,s
  ksll,s
  csslicexz,Eff_GalleryHeight
  *if,GEO_HDW_topLength,gt,2*GEO_FRAME_headBeamWidth,then
    csslicexy,zFlatTop
  *endif
  cm,COMPA_HDW_RightWall,area

  cmsel,s,COMPA_HDW_LeftWall
  lsla,s
  ksll,s
  csslicexz,Eff_GalleryHeight
  *if,GEO_HDW_topLength,gt,2*GEO_FRAME_headBeamWidth,then
    csslicexy,zFlatTop
  *endif
  cm,COMPA_HDW_LeftWall,area

  cmsel,s,COMPA_HDW_RightWall
  cmsel,a,COMPA_HDW_LeftWall
  cmsel,a,COMPA_HDW_Walls
  cm,COMPA_HDW_Walls,area

  *if,GEO_HDW_topLength,gt,2*GEO_FRAME_headBeamWidth,then
    cmsel,s,COMPA_HDW_Footing
    lsla,s
    ksll,s
    csslicexy,zFlatTop
    cm,COMPA_HDW_Footing,area

    cmsel,s,COMPA_HDW_Slab
    lsla,s
    ksll,s
    csslicexy,zFlatTop
    cm,COMPA_HDW_Slab,area

    cmsel,s,COMPA_HDW_Stiffener
    lsla,s
    ksll,s
    csslicexy,zFlatTop
    cm,COMPA_HDW_Stiffener,area

    ! Draw second top beam
    !KSEL,S,LOC,Z,zFlatTop
    !KSEL,R,LOC,Y,YTop,yBeam
    !CM,COMPK_temp,KP
    !*get,xMin
  *endif

  ksel,s,loc,z,zEnd,zEnd+GEO_HDW_length
  nummrg,kp

  cmsel,s,COMPA_HDW_Walls
  lsla,s
  ksll,s
  asel,none
  KBL = kp(0,Eff_GalleryHeight,zEnd)
  KTL = kp(0,GEO_HDW_Height,zEnd)
  KBR = kp(Eff_GalleryWidth*GEO_TUN_modules,Eff_GalleryHeight,zEnd)
  KTR = kp(Eff_GalleryWidth*GEO_TUN_modules,GEO_HDW_Height,zEnd)
  cmsel,s,COMPK_IKP_BLtoBR
  ksel , a , kp ,  , KBL
  ksel , a , kp ,  , KTL
  ksel , a , kp ,  , KBR
  ksel , a , kp ,  , KTR
  lslk,s,1
  l,KTL,KTR
  !A,KBL,KBR,KTR,KTL
  al,all
  cmsel,A,COMPA_HDW_TopBeam
  cm,COMPA_HDW_TopBeam,area

  allsel
  asel,none
  a,KP14,KP15,KP8,KP7,KP6,KP5
  cmsel,A,COMPA_HDW_BotBeam
  cm,COMPA_HDW_BotBeam,area

  allsel
  asel,none
  cmsel,s,COMPK_IKP_2to3
  ksel , a , kp ,  , KP10
  ksel , a , kp ,  , KP11
  ksel , a , kp ,  , KP4
  ksel , a , kp ,  , KP3
  ksel , a , kp ,  , KP2
  ksel , a , kp ,  , KP1
  lslk,s,1
  l,KP10,KP11
  !A,KP10,KP11,KP4,KP3,KP2,KP1
  al,all
  cmsel,A,COMPA_HDW_BackBeam
  cm,COMPA_HDW_BackBeam,area
  allsel


*elseif,GEO_HDW_type,eq,1,then

  !!                                                                                            !!
  !! C.  Wings                                                                                  !!
  !!                                                                                            !!
  ksel,s,loc,x,0
  ksel,r,loc,y,0
  *get,ZMax,kp,,mxloc,z
  zEndLeft = ZMax

  ksel,s,loc,x,Eff_GalleryWidth
  ksel,r,loc,y,0
  *get,ZMax,kp,,mxloc,z
  zEndRight = ZMax

  zWingLeft  = zEndLeft-GEO_HDW_length*sin(GEO_InletRadians)
  zWingRight = zEndRight+GEO_HDW_length*sin(GEO_InletRadians)

  xWingLeft  = x1-GEO_HDW_length*cos(GEO_InletRadians)
  xWingRight = x4+GEO_HDW_length*cos(GEO_InletRadians)

  allsel
  KP1   = kp(x1,0,zEndLeft)
  KP4   = kp(x4,0,zEndRight)
  KP27  = kp(x1,yTop,zEndLeft)
  KP29  = kp(x4,yTop,zEndRight)
  KP101 = kp(x1,GEO_HDW_wingBottomOffset,zEndLeft)
  KP102 = kp(x4,GEO_HDW_wingBottomOffset,zEndRight)
  ksel,s,loc,x,x2
  KP2 = kp(x2,0,zEndLeft)
  ksel,s,loc,x,x3
  KP3 = kp(x3,0,zEndRight)
  allsel

  !! Keypoints                                                                                  !!
  k , KP10 , x10 , yFoot , zEndLeft
  k , KP11 , x11 , yFoot , zEndRight

  k , KP17 , x17 , yBeam , zEndLeft
  k , KP18 , x18 , yBeam , zEndRight

  k , KP21 , xWingLeft  , yBeam , zWingLeft
  k , KP25 , xWingRight , yBeam , zWingRight

  k , KP28 , xWingLeft  , yTop , zWingLeft
  k , KP30 , xWingRight , yTop , zWingRight

  *if,GEO_HDW_wingShape,eq,1,then
    ! Rectangular shape
    k , KP22 , xWingLeft  , GEO_HDW_wingBottomOffset , zWingLeft ! kp28
    k , KP23 , xWingRight , GEO_HDW_wingBottomOffset , zWingRight ! kp30
  *endif

  !! Areas                                                                                      !!
  asel,none
  a , KP17 , KP18 , KP29 , KP27
  a , KP17 , KP21 , KP28 , KP27
  a , KP18 , KP25 , KP30 , KP29
  cmsel,A,COMPA_HDW_TopBeam
  cm,COMPA_HDW_TopBeam,area

  asel,none
  *if,GEO_HDW_wingShape,eq,0,then
    ! Triangular
    a,KP27,KP28,KP101
  *elseif,GEO_HDW_wingShape,eq,1,then
    ! Rectangular
    a,KP27,KP28,KP22,KP101
  *endif
  cmsel,a,COMPA_HDW_LeftWall
  cm,COMPA_HDW_LeftWall,area

  asel,none
  *if,GEO_HDW_wingShape,eq,0,then
    ! Triangular
    a,KP29,KP30,KP102
  *elseif,GEO_HDW_wingShape,eq,1,then
    ! Rectangular
    a,KP29,KP30,KP23,KP102
  *endif
  cmsel,a,COMPA_HDW_RightWall
  cm,COMPA_HDW_RightWall,area

  cmsel,s,COMPA_HDW_RightWall
  cmsel,a,COMPA_HDW_LeftWall
  cmsel,a,COMPA_HDW_TopBeam
  cm,COMPA_HDW_Walls,area

  allsel
  asel,none
  a,KP10,KP11,KP4,KP3,KP2,KP1
  cmsel,a,COMPA_HDW_BackBeam
  cm,COMPA_HDW_BackBeam,area
  allsel

*endif

cmsel , s , COMPA_HDW_TopBeam
cmsel , a , COMPA_HDW_LeftWall
cmsel , a , COMPA_HDW_RightWall
cmsel , a , COMPA_HDW_Walls
cmsel , a , COMPA_HDW_BackBeam
cmsel , a , COMPA_HDW_BotBeam
cmsel , a , COMPA_HDW_Stiffener
cmsel , a , COMPA_HDW_Slab
cmsel , a , COMPA_HDW_Footing
cmsel , a , COMPA_HDW_Columns
cm,COMPA_Headwalls,area
allsel
