!!
!!
!! 2. DEFINIÇÕES PRELIMINARES
!!
!!
! Versão do código
Version=3


!!
!! 2.1 DADOS COMPLEMENTARES
!!
! Número do sistema de coordenadas
CoordSys=11
CS_top=12
CS_bot=13
CS_left=14
CS_right=15

! Dados da mola (solo)
SpringElasticModulus=1e9
SpringLength=1



!!
!! 2.2 PRÉ-DIMENSIONAMENTO DAS GALERIAS
!!
prodThk=RoofThk*FloorThk*WallThk

*if,prodThk,LE,0,THEN

    ! Largura de até 1,5m
    *if,GalleryWidth,LE,1.5,THEN
        Thk=0.2

    ! Largura de até 2,0m
    *elseif,GalleryWidth,LE,2,THEN
        *if,GalleryDepth,LE,5,THEN
            Thk=0.2
        *else
            Thk=0.25
        *endif

    ! Largura de até 2,5m
    *elseif,GalleryWidth,LE,2.5,THEN
        *if,GalleryDepth,LE,2.5,THEN
            *if,GalleryModules,EQ,1,THEN
                Thk=0.2
            *else
                Thk=0.25
            *endif
        *elseif,GalleryDepth,LE,5,THEN
            Thk=0.25
        *elseif,GalleryDepth,LE,7.5,THEN
            *if,GalleryModules,EQ,1,THEN
                Thk=0.25
            *else
                Thk=0.3
            *endif
        *elseif,GalleryDepth,LE,10,THEN
            Thk=0.35
        *elseif,GalleryDepth,LE,12.5,THEN
            *if,GalleryModules,EQ,1,THEN
                Thk=0.3
            *else
                Thk=0.35
            *endif
        *else
            Thk=0.35
        *endif

    ! Largura de até 3,0m
    *elseif,GalleryWidth,LE,3,THEN
        *if,GalleryDepth,LE,2.5,THEN
            Thk=0.25
        *elseif,GalleryDepth,LE,7.5,THEN
            *if,GalleryModules,EQ,1,THEN
                Thk=0.3
            *else
                Thk=0.25
            *endif
        *elseif,GalleryDepth,LE,12.5,THEN
            Thk=0.35
        *else
            Thk=0.4
        *endif

    ! Largura acima de 3,0m
    *else
        *if,GalleryDepth,LE,2.5,THEN
            Thk=0.3
        *elseif,GalleryDepth,LE,7.5,THEN
            *if,GalleryModules,EQ,1,THEN
                Thk=0.35
            *else
                Thk=0.4
            *endif
        *elseif,GalleryDepth,LE,12.5,THEN
            Thk=0.4
        *else
            Thk=0.45
        *endif
    *endif

    WallThk=Thk
    FloorThk=Thk
    RoofThk=Thk

*endif

! Calcular vãos efetivos
Eff_GalleryWidth=GalleryWidth+WallThk
Eff_GalleryHeight=GalleryHeight+(FloorThk+RoofThk)/2



!!
!! 2.3 VALORES CALCULADOS
!!
! Esconsidade
pi=ACOS(-1)
LineAngle=LineDirection*pi/180

! Módulo de elasticidade do concreto
ElasticModulus=5600*SQRT(fck*1e-6)*1e6

! Área do elemento
ElementArea=ElementSize**2

! Constante de mola
SpringConstant=SoilStiffness*ElementArea
SpringArea=SpringConstant*SpringLength/SpringElasticModulus

! Active Earth Pressure Coeficient (Ka)
ActiveEPCoefficient=TAN((45-(FrictionAngle/2))*pi/180)**2
! Rest Earth Pressure Coefficient (K0)
RestEPCoefficient=1-SIN(FrictionAngle*pi/180)

! Peso específico
Gammaw=WaterDensity*Gravity
Gammat=SoilDensity*Gravity
GammaSAT=Gammat+(SaturationIncrement*Gravity)
GammaSUB=GammaSAT-Gammaw

! Cota do nível d'água
WaterY=GalleryDepth-WaterDepth+Eff_GalleryHeight
WaterHeight=Eff_GalleryHeight+GalleryDepth-WaterDepth
*if,Waterheight,GT,Eff_GalleryHeight,THEN
    WaterHeight=Eff_GalleryHeight
*endif

! Tensão vertical devido à camada de cobrimento
*if,WaterDepth,LT,GalleryDepth,THEN
    SoilPressure=Gammat*WaterDepth+GammaSAT*(GalleryDepth-WaterDepth)
*else
    SoilPressure=Gammat*GalleryDepth
*endif
NodalSoilLoad=SoilPressure*ElementArea

! Tensão vertical de flutuação na laje de fundo
Buoyancy=WaterHeight*Gammaw
BuoyancyLoad=Buoyancy*ElementArea

! Ângulo dos taludes na direção longitudinal da galeria
*get,parType,PARM,GalleryThetai,TYPE
*if,parType,NE,0,THEN
    GalleryAlphai=0
*else
    GalleryAlphai=90-GalleryThetai
*endif
*get,parType,PARM,GalleryThetaf,TYPE
*if,parType,NE,0,THEN
    GalleryAlphaf=0
*else
    GalleryAlphaf=90-GalleryThetaf
*endif



!!
!! 2.4 ATRIBUTOS
!!
/prep7

! Tipos de elemento
ET,1,181
ET,2,180

! Constante real da mola
R,1,SpringArea,0,-1

! Material: concreto
MP,EX,1,ElasticModulus
MP,PRXY,1,Poisson
MP,DENS,1,MassDensity

! Material: solo
MP,EX,2,SpringElasticModulus
MP,PRXY,2,Poisson

! Seção transversal: parede
SECTYPE,1,SHELL,,PAREDE
SECDATA,WallThk,1

! Seção transversal: laje de piso
SECTYPE,2,SHELL,,PISO
SECDATA,FloorThk,1

! Seção transversal: laje de teto
SECTYPE,3,SHELL,,TETO
SECDATA,RoofThk,1

! Criar malha
CLOCAL,CoordSys,CART,0,0,0,0,0,0
CSYS,0
CLOCAL,CS_top,CART,0,0,0,0,-90,0
CSYS,0
CLOCAL,CS_bot,CART,0,0,0,0,90,0
CSYS,0
CLOCAL,CS_left,CART,0,0,0,0,90,-90
CSYS,0
CLOCAL,CS_right,CART,0,0,0,90,90,0
CSYS,0



