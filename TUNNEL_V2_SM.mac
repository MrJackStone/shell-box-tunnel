!!                                                                                                !!
!! 3.  Solid model                                                                                !!
!!                                                                                                !!

! Exported selection components:
! Area components:
!    COMPA_Tunnel
!    COMPA_Floor
!    COMPA_Roof
!    COMPA_Walls
!    COMPA_LeftWall
!    COMPA_RightWall
!    COMPA_InnerWalls
!    COMPA_ROOF_leftCorbel
!    COMPA_ROOF_rightCorbel
!    COMPA_ROOF_flat
!    COMPA_FLOOR_leftCorbel
!    COMPA_FLOOR_rightCorbel
!    COMPA_FLOOR_flat
!    COMPA_WALL_topCorbel
!    COMPA_WALL_botCorbel
!    COMPA_WALL_flat
!    COMPA_bBeam
!    COMPA_tBeam
!    COMPA_Columns
!    COMPA_HDW_1
!    COMPA_HDW_2
! Line components:
!    COMPL_Tunnel
!    COMPL_Embankment
!    COMPL_Railway
! Keypoint components:
!    COMPK_Tunnel

/prep7

/view,1,1,2,3
/angle,1
/auto,1

seltol,1e-8

! Activate cartesian coordinate to account for tunnel slope
csys,CS_Tunnel

!!                                                                                                !!
!! 3.1  Tunnel                                                                                    !!
!!                                                                                                !!

!! 3.1.0  Initialize selection components                                                         !!
asel,none
cm , COMPA_Tunnel     , area
cm , COMPA_Floor      , area
cm , COMPA_Roof       , area
cm , COMPA_Walls      , area
cm , COMPA_LeftWall   , area
cm , COMPA_RightWall  , area
cm , COMPA_InnerWalls , area
cm , COMPA_bBeam      , area
cm , COMPA_tBeam      , area
cm , COMPA_Columns    , area

lsel,none
cm , COMPL_Tunnel     , line
cm , COMPL_Embankment , line
cm , COMPL_Railway    , line

ksel,none
cm , COMPK_Tunnel , kp


!! 3.1.1  Draw areas                                                                              !!
! First wall
xVal = 0

KCounter      = 1
KP1           = KCounter
KP2           = KCounter+1
KP3           = KCounter+2
KP4           = KCounter+3
KCounter      = KP4+1
KTopBeginning = KP4

k , KP1 , xVal , 0                 , 0
k , KP2 , xVal , 0                 , GEO_TUN_length
k , KP3 , xVal , Eff_GalleryHeight , GEO_TUN_length
k , KP4 , xVal , Eff_GalleryHeight , 0

lsel,none
l,KP1,KP2
*get,L1,line,0,num,max
l,KP2,KP3
*get,L2,line,0,num,max
l,KP3,KP4
*get,L3,line,0,num,max
l,KP4,KP1
*get,L4,line,0,num,max

al,L1,L2,L3,L4

allsel

! Inner walls, floors, and ceilings
*do,GalleryModuleIndex,1,GEO_TUN_modules,1

  KPA = kp(xVal,0,0)
  KPB = kp(xVal,0,GEO_TUN_length)
  KPC = kp(xVal,Eff_GalleryHeight,GEO_TUN_length)
  KPD = kp(xVal,Eff_GalleryHeight,0)

  ksel , s , kp ,  , KPA
  ksel , a , kp ,  , KPB
  ksel , a , kp ,  , KPC
  ksel , a , kp ,  , KPD
  cm,LastKeypoints,kp

  xVal = xVal+Eff_GalleryWidth

  KP1      = KCounter
  KP2      = KCounter+1
  KP3      = KCounter+2
  KP4      = KCounter+3
  KCounter = KP4+1

  ksel,none
  K , KP1 , xVal , 0                 , 0
  K , KP2 , xVal , 0                 , GEO_TUN_length
  K , KP3 , xVal , Eff_GalleryHeight , GEO_TUN_length
  K , KP4 , xVal , Eff_GalleryHeight , 0
  cm,NewKeypoints,kp

  lsel,none
  L,KP1,KP2
  *get,L1,LINE,0,num,max
  l,KP2,KP3
  *get,L2,LINE,0,num,max
  l,KP3,KP4
  *get,L3,LINE,0,num,max
  l,KP4,KP1
  *get,L4,LINE,0,num,max
  cm,NewLines,line

  al,L1,L2,L3,L4

  cmsel,s,LastKeypoints
  lslk,s,1
  cm,LastLines,line

  lsel,r,loc,y,0
  *get,LA,LINE,0,NUM,MIN
  CMSEL,S,LastLines
  LSEL,R,LOC,Z,GEO_TUN_length
  *get,LB,LINE,0,NUM,MIN
  CMSEL,S,LastLines
  LSEL,R,LOC,Y,Eff_GalleryHeight
  *get,LC,LINE,0,NUM,MIN
  CMSEL,S,LastLines
  LSEL,R,LOC,Z,0
  *get,LD,LINE,0,NUM,MIN

  cmsel , s , NewKeypoints
  cmsel , a , LastKeypoints

  lsel,none
  l,KP1,KPA
  *get,L5,line,0,num,max
  l,KP2,KPB
  *get,L6,line,0,num,max
  l,KP3,KPC
  *get,L7,line,0,num,max
  l,KP4,KPD
  *get,L8,line,0,num,max
  cm,TransLines,line

  cmsel,s,NewLines
  cmsel,a,LastLines
  cmsel,a,TransLines

  al,LA,L6,L1,L5
  al,L3,L7,LC,L8

*enddo


!! 3.1.2  Oblique cut at tunnel ends                                                              !!
*if,GEO_HDW_type,EQ,1,AND,GEO_HDW_angle,NE,0,THEN

  *get,CSMax,CDSY,,NUM,MAX
  tmpCS=CSMax+1000

  ALLSEL
  wpcsys,1,CS_Tunnel
  WPROTA,,,-GEO_HDW_angle
  ASBW,ALL,,DELETE

  CSWPLA,tmpCS,CART
  ASEL,U,LOC,Z,0,1E3*GEO_TUN_length
  ADELE,ALL,,,1

  ALLSEL
  WPCSYS,1,CS_Tunnel
  WPOFFS,Eff_GalleryWidth,,GEO_TUN_length
  WPROTA,,,-GEO_HDW_angle
  ASBW,ALL,,DELETE

  CSWPLA,tmpCS,CART
  ASEL,S,LOC,Z,0,1E3*GEO_TUN_length
  ADELE,ALL,,,1

  allsel
  WPCSYS,1,CS_Tunnel
  CSYS,CS_Tunnel

*endif


!! 3.1.3  Slice at approach slabs                                                                  !!
*if,GEO_APR_status,eq,1,then
  csslicexy,GEO_APR_z1
  csslicexy,GEO_APR_z2
  csslicexy,GEO_APR_z3
  csslicexy,GEO_APR_z4
*endif


!! 3.1.4  Slice roof gap                                                                          !!
*if,GEO_ROOF_gapStatus,eq,1,then
  *do,nthGap,1,GEO_ROOF_gapCount,1
    zBeg = GEO_ROOF_gap_z_begs(nthGap)
    zEnd = GEO_ROOF_gap_z_ends(nthGap)

    allsel
    csslicexy,zBeg
    csslicexy,zEnd
    asel,s,loc,z,zBeg,zEnd
    asel,r,loc,y,GEO_TUN_effH
    adele,all,,,1
  *enddo
*endif


!! 3.1.5  Create components                                                                        !!
TUNNEL_V2_BUILDCM


!! 3.1.6  Slice at precast girders                                                                !!
*if,GEO_ROOF_type,eq,1,then
  cmsel,s,COMPA_Tunnel
  cmsel,u,COMPA_Roof
  *do,nthSlice,1,GEO_girderCount-1,1
    z = GEO_girderZ(nthSlice)
    csslicexy,z
  *enddo

  cmsel,s,COMPA_Roof
  lsla,s
  ksll,s
  wpcsys,,CS_Tunnel
  *do,nthSlice,1,GEO_girderCount-1,1
    z = GEO_girderZ(nthSlice)
    wpoffs,,,z
    asbw,all,sepo,delete
    wpoffs,,,-z
  *enddo

  ! Rebuild named selection components
  TUNNEL_V2_BUILDCM

*endif


!!                                                                                                !!
!! 3.2  Split wings                                                                               !!
!!                                                                                                !!
*if,GEO_HDW_type,EQ,1,AND,GEO_HDW_wingBottomOffset,GT,0,THEN
  cmsel,s,COMPA_Walls
  csslicexz,GEO_HDW_wingBottomOffset
  TUNNEL_V2_BUILDCM
*endif


!!                                                                                                !!
!! 3.3  Headwalls                                                                                 !!
!!                                                                                                !!
asel,none

cm , COMPA_headwalls           , area
cm , COMPA_HDW_backBeam        , area
cm , COMPA_HDW_botBeam         , area
cm , COMPA_HDW_columns         , area
cm , COMPA_HDW_footing         , area
cm , COMPA_HDW_slab            , area
cm , COMPA_HDW_stiffener       , area
cm , COMPA_HDW_topBeam         , area
cm , COMPA_HDW_walls           , area
cm , COMPA_HDW_left            , area
cm , COMPA_HDW_right           , area

cm , COMPA_HDW_leftWall        , area  $  cm , COMPA_HDW_rightWall       , area
cm , COMPA_HDW_leftFoot        , area  $  cm , COMPA_HDW_rightFoot       , area
cm , COMPA_HDW_leftStiffener   , area  $  cm , COMPA_HDW_rightStiffener  , area
cm , COMPA_HDW_leftColumn      , area  $  cm , COMPA_HDW_rightColumn     , area

cm , COMPA_HDW1_leftWall       , area  $  cm , COMPA_HDW1_rightWall      , area
cm , COMPA_HDW1_leftFoot       , area  $  cm , COMPA_HDW1_rightFoot      , area
cm , COMPA_HDW1_leftStiffener  , area  $  cm , COMPA_HDW1_rightStiffener , area
cm , COMPA_HDW1_leftColumn     , area  $  cm , COMPA_HDW1_rightColumn    , area

cm , COMPA_HDW_1               , area  $  cm , COMPA_HDW_2               , area

cm , COMPA_HDW1_backBeam       , area  $  cm , COMPA_HDW2_backBeam       , area
cm , COMPA_HDW1_botBeam        , area  $  cm , COMPA_HDW2_botBeam        , area
cm , COMPA_HDW1_topBeam        , area  $  cm , COMPA_HDW2_topBeam        , area
cm , COMPA_HDW1_slab           , area  $  cm , COMPA_HDW2_slab           , area

cm , COMPA_HDW2_leftWall       , area  $  cm , COMPA_HDW2_rightWall      , area
cm , COMPA_HDW2_leftFoot       , area  $  cm , COMPA_HDW2_rightFoot      , area
cm , COMPA_HDW2_leftStiffener  , area  $  cm , COMPA_HDW2_rightStiffener , area
cm , COMPA_HDW2_leftColumn     , area  $  cm , COMPA_HDW2_rightColumn    , area

*get,parType,parm,GEO_HDW_status,type

*if,parType,eq,0,and,GEO_HDW_status,gt,0,then

  cmsel,s,COMPA_Floor
  cssliceyz,GEO_HDW_stiffXOff
  cssliceyz,GEO_TUN_totEffW-GEO_HDW_stiffXOff
  cm,COMPA_Floor,area

  /color,cm,lgra,COMPA_Floor
  cmsel,s,COMPA_tunnel
  cmsel,a,COMPA_floor
  cm,COMPA_Tunnel,area
  allsel

  TUNNEL_V2_HDWSM , CS_Tunnel    , CS_HDW_LeftWallEnd , CS_HDW_RightWallEnd
  TUNNEL_V2_HDWSM , CS_RevTunnel , CS_HDW_LeftWallBeg , CS_HDW_RightWallBeg

  cmsel,s,COMPA_Headwalls
  lsla,s
  ksll,s
  nummrg,kp
  csys,CS_Tunnel

*endif
allsel

! Build inlet selection components
TUNNEL_V2_HDWBUILDCM


!!                                                                                                !!
!! 3.4  Split corbels                                                                             !!
!!                                                                                                !!
asel,none
cm , COMPA_ROOF_leftCorbel   , area
cm , COMPA_ROOF_rightCorbel  , area
cm , COMPA_ROOF_flat         , area
cm , COMPA_FLOOR_leftCorbel  , area
cm , COMPA_FLOOR_rightCorbel , area
cm , COMPA_FLOOR_flat        , area
cm , COMPA_WALL_topCorbel    , area
cm , COMPA_WALL_botCorbel    , area
cm , COMPA_WALL_flat         , area

*if,GEO_CORB_status,ge,1,then
  TUNNEL_V2_SMCORBEL
*endif


!!                                                                                                !!
!! 3.5  Approach slabs                                                                            !!
!!                                                                                                !!
asel,none
cm,COMPA_approach,area

*if,GEO_APR_status,eq,1,then
  TUNNEL_V2_SMAPPSLAB
*endif


!!                                                                                                !!
!! 3.6  Pin roof to walls                                                                         !!
!!                                                                                                !!
*if,GEO_ROOF_wallConnection,eq,1,then

  *if,GEO_ROOF_type,eq,0,then
    TUNNEL_V2_SMCOPY, 'COMPA_roof'

  *elseif,GEO_ROOF_type,eq,1,then
    *do,nthGirder,1,GEO_girderCount,1
      compName = STRCAT('COMPA_RG',CHRVAL(nthGirder))
      TUNNEL_V2_SMCOPY, compName
      cmsel,s,COMPA_Roof
      cmsel,a,%compName%
      cm,COMPA_Roof,area
    *enddo
  *endif

  *if,GEO_ROOF_gapStatus,eq,1,then
    TUNNEL_V2_SMCOPY, 'COMPA_struts'
  *endif

  cmsel,s,COMPA_Tunnel
  cmsel,a,COMPA_roof
  cmsel,a,COMPA_struts
  cm,COMPA_Tunnel,area
*endif


!!                                                                                                !!
!! 3.7  Open inner walls                                                                          !!
!!                                                                                                !!
TUNNEL_V2_SMIWALL


!!                                                                                                !!
!! 3.8  Piles                                                                                     !!
!!                                                                                                !!
*if,GEO_PILE_status,eq,1,then
  TUNNEL_V2_SMPILES
*endif


!!                                                                                                !!
!! 3.9  Area direction                                                                            !!
!!                                                                                                !!
cmsel , s , COMPA_Floor
aryori,-1

cmsel , s , COMPA_Roof
cmsel , a , COMPA_struts
cmsel , a , COMPA_approach
aryori,1

cmsel , s , COMPA_LeftWall
arxori,-1

cmsel , s , COMPA_RightWall
cmsel , a , COMPA_InnerWalls
arxori,1

cmsel , s , COMPA_HDW_Stiffener
cmsel , a , COMPA_HDW_Slab
cmsel , a , COMPA_HDW_Footing
aryori,-1

cmsel , s , COMPA_HDW_LeftWall
arxori,-1

cmsel , s , COMPA_HDW_RightWall
arxori,1

!cmsel,S,COMPA_HDW_TopBeam
!compa_HDW_BotBeam
!compa_HDW_BackBeam

allsel


!!                                                                                                !!
!! 3.10  Embankment                                                                               !!
!!                                                                                                !!
! NOTE: embankment is drawn for display purposes only.
CSYS,CS_Tunnel

! Get reference location for cover soil representation
KSEL,S,LOC,Z,GEO_TUN_length
KSEL,R,LOC,Y,Eff_GalleryHeight
*get,KRef,KP,,NUM,MIN

! Draw embankment contour in global cartesian CS
CSYS,0
yREF = KY(KRef)
zREF = KZ(KRef)

! Calculate vertex location for embankment keypoints
*afun,DEG
xPT          = GEO_TUN_totEffW/2
yPT1         = COS(TunnelSlope)*(Eff_GalleryHeight+(GEO_ROOF_thk/2))
yPT2         = yPT1+GEO_EMB_height
zPT1         = -SIN(TunnelSlope)*(Eff_GalleryHeight+(GEO_ROOF_thk/2))
zPT2         = zPT1+(GEO_EMB_height/TAN(GEO_EMB_slopeBeg))
yPT3         = yPT2
yPT4         = yREF+COS(TunnelSlope)*(GEO_ROOF_thk/2)
depthFromEnd = yPT2-yPT4
zPT4         = zREF-SIN(TunnelSlope)*(GEO_ROOF_thk/2)
zPT3         = zPT4-(depthFromEnd/TAN(GEO_EMB_slopeEnd))
*afun,RAD

! Draw embankment contour
lsel,none
*get,KPMax,KP,,NUM,MAXD
KPT1 = KPMax+1
KPT2 = KPMax+2
KPT3 = KPMax+3
KPT4 = KPMax+4
K , KPT1 , xPT  , yPT1 , zPT1
K , KPT2 , xPT  , yPT2 , zPT2
K , KPT3 , xPT  , yPT3 , zPT3
K , KPT4 , xPT  , yPT4 , zPT4
L , KPT1 , KPT2
L , KPT2 , KPT3
L , KPT3 , KPT4

! Create component
CM,COMPL_Embankment,LINE
/color,CM,WHIT,COMPL_Embankment


!!                                                                                                !!
!! 3.11  Railway lines                                                                            !!
!!                                                                                                !!
! NOTE: railwaylines (indicated as arrows) are drawn for display purposes only.

! Draw arrows marking each railway position
lsel,none
*do,nthRailway,1,LOAD_LTA_count,1

  zRailway = LOAD_LTA_offsets(nthRailway)

  *do,nthLoad,1,2,1

    gaugeOff = (-1**nthLoad)*LOAD_LTA_gauge

    *get,KPMax,KP,,NUM,MAXD
    KPRi = KPMax+1
    KPRj = KPMax+2
    KPRk = KPMax+3
    KPRl = KPMax+4
    K , KPRi , xPT  , yPT2     , zRailway+gaugeOff
    K , KPRj , xPT  , yPT2+2   , zRailway+gaugeOff
    K , KPRk , xPT  , yPT2+0.5 , zRailway-0.3+gaugeOff
    K , KPRl , xPT  , yPT2+0.5 , zRailway+0.3+gaugeOff
    L , KPRi , KPRj
    L , KPRi , KPRk
    L , KPRi , KPRl

  *enddo
*enddo

! Create component
CM,COMPL_Railway,LINE
/color,CM,RED,COMPL_Railway


!!                                                                                                !!
!! 3.12  Capture images                                                                           !!
!!                                                                                                !!
/color , CM , DGRA , COMPA_Walls
/color , CM , LGRA , COMPA_Floor
/color , CM , LGRA , COMPA_Roof
/color , cm , lgra , COMPA_struts
/color , CM , 14   , COMPA_Headwalls
/color , cm , oran , COMPA_approach
/color , cm , gree , COMPA_Headwalls

! Tunnel
ALLSEL
CMSEL,S,COMPA_Tunnel
CMSEL,S,COMPL_Tunnel
CMSEL,S,COMPK_Tunnel
/view,1,1,2,3
/angle,1
/auto,1
GPLOT
/graphics,FULL
TAKEPIC,'SM_Tunnel'

! Capture schematic side view (shows railway lines and embankment contour)
ALLSEL
ksel,none
/view,1,-1,0,0
/pnum,AREA,1
/number,1
GPLOT
TAKEPIC,'SM_Schematic',800
GPLOT

! Capture top view
allsel
/view,1,0,1,0
/auto,1
aplot
takepic,'SM_top',800
