!!                                                                                                !!
!! 3.  Solid model                                                                                !!
!!                                                                                                !!

! Exported selection components:
! Area components:
!    COMPA_Tunnel
!    COMPA_Floor
!    COMPA_Roof
!    COMPA_Walls
!    COMPA_LeftWall
!    COMPA_RightWall
!    COMPA_InnerWalls
!    COMPA_ROOF_leftCorbel
!    COMPA_ROOF_rightCorbel
!    COMPA_ROOF_flat
!    COMPA_FLOOR_leftCorbel
!    COMPA_FLOOR_rightCorbel
!    COMPA_FLOOR_flat
!    COMPA_WALL_topCorbel
!    COMPA_WALL_botCorbel
!    COMPA_WALL_flat
!    COMPA_bBeam
!    COMPA_tBeam
!    COMPA_Columns
!    COMPA_HDW_1
!    COMPA_HDW_2
! Line components:
!    COMPL_Tunnel
!    COMPL_Embankment
!    COMPL_Railway
! Keypoint components:
!    COMPK_Tunnel

/prep7

/view,1,1,2,3
/angle,1
/auto,1

seltol,1e-8

csys,CS_Tunnel

!!                                                                                                !!
!! 3.1  Tunnel                                                                                    !!
!!                                                                                                !!
! Initialize selection components
TUNNEL_V2_SMINITCM

! Draw tunnel
TUNNEL_V2_SMTUNNEL

! Draw frames
*if,GEO_FRM1_status,eq,1,then
  TUNNEL_V2_SMFRAME , 1
*endif

*if,GEO_FRM2_status,eq,1,then
  TUNNEL_V2_SMFRAME , 2
*endif

! Draw inlets
*if,GEO_HDW1_status,eq,1,then
  *if,GEO_HDW1_type,eq,0,then
    ! Full inlet with floor slab, footings, and headwalls
    TUNNEL_V2_SMHDW1 , 1 , CS_tunnel , CS_begSkewed
  *elseif,GEO_HDW1_type,eq,1,then
    ! Wings
    TUNNEL_V2_SMHDW2 , 1
  *elseif,GEO_HDW1_type,eq,2,then
    ! Floor slab & independent retaining walls
    TUNNEL_V2_SMHDW3 , 1 , CS_tunnel    , CS_begSkewed
  *endif
*endif

*if,GEO_HDW2_status,eq,1,then
  *if,GEO_HDW2_type,eq,0,then
    ! Full inlet with floor slab, footings, and headwalls
    TUNNEL_V2_SMHDW1 , 2 , CS_revTunnel , CS_endSkewed
  *elseif,GEO_HDW2_type,eq,1,then
    ! Wings
    TUNNEL_V2_SMHDW2 , 2
  *elseif,GEO_HDW2_type,eq,2,then
    ! Floor slab & independent retaining walls
    TUNNEL_V2_SMHDW3 , 2 , CS_revTunnel , CS_endSkewed
  *endif
*endif

! Build inlet selection components
TUNNEL_V2_HDWBUILDCM


!!                                                                                                !!
!! 3.4  Split corbels                                                                             !!
!!                                                                                                !!
*if,GEO_CORB_status,ge,1,then
  TUNNEL_V2_SMCORBEL
*endif


!!                                                                                                !!
!! 3.5  Approach slabs                                                                            !!
!!                                                                                                !!
*if,GEO_APR_status,eq,1,then
  TUNNEL_V2_SMAPPSLAB
*endif


!!                                                                                                !!
!! 3.7  Open inner walls                                                                          !!
!!                                                                                                !!
TUNNEL_V2_SMIWALL


!!                                                                                                !!
!! 3.8  Piles                                                                                     !!
!!                                                                                                !!
*if,GEO_PILE_status,eq,1,then
  TUNNEL_V2_SMPILES
*endif


!!                                                                                                !!
!! 3.9  Area direction                                                                            !!
!!                                                                                                !!
csys,CS_tunnel

cmsel , s , COMPA_Floor
aryori,-1

cmsel , s , COMPA_Roof
cmsel , a , COMPA_struts
cmsel , a , COMPA_approach
aryori,1

cmsel , s , COMPA_LeftWall
arxori,-1

cmsel , s , COMPA_RightWall
cmsel , a , COMPA_InnerWalls
arxori,1

cmsel , s , COMPA_HDW_Stiffener
cmsel , a , COMPA_HDW_Slab
cmsel , a , COMPA_HDW_Footing
aryori,-1

! TODO  temporary fix, improving ARZORI needed to account for every possible case
cmsel  , s , COMPA_HDW1_rightWall
cmsel  , a , COMPA_HDW2_rightWall
areverse,all

allsel


!!                                                                                                !!
!! 3.10  Embankment                                                                               !!
!!                                                                                                !!
! NOTE: embankment is drawn for display purposes only.
CSYS,CS_Tunnel

! Get reference location for cover soil representation
KSEL,S,LOC,Z,GEO_TUN_length
KSEL,R,LOC,Y,GEO_TUN_effH
*get,KRef,KP,,NUM,MIN

! Draw embankment contour in global cartesian CS
CSYS,0
yREF = KY(KRef)
zREF = KZ(KRef)

! Calculate vertex location for embankment keypoints
*afun,DEG
xPT          = GEO_TUN_totEffW/2
yPT1         = COS(TunnelSlope)*(GEO_TUN_effH+(GEO_ROOF_thk/2))
yPT2         = yPT1+GEO_EMB_height
zPT1         = -SIN(TunnelSlope)*(GEO_TUN_effH+(GEO_ROOF_thk/2))
zPT2         = zPT1+(GEO_EMB_height/TAN(GEO_EMB_slopeBeg))
yPT3         = yPT2
yPT4         = yREF+COS(TunnelSlope)*(GEO_ROOF_thk/2)
depthFromEnd = yPT2-yPT4
zPT4         = zREF-SIN(TunnelSlope)*(GEO_ROOF_thk/2)
zPT3         = zPT4-(depthFromEnd/TAN(GEO_EMB_slopeEnd))
*afun,RAD

! Draw embankment contour
lsel,none
*get,KPMax,KP,,NUM,MAXD
KPT1 = KPMax+1
KPT2 = KPMax+2
KPT3 = KPMax+3
KPT4 = KPMax+4
K , KPT1 , xPT  , yPT1 , zPT1
K , KPT2 , xPT  , yPT2 , zPT2
K , KPT3 , xPT  , yPT3 , zPT3
K , KPT4 , xPT  , yPT4 , zPT4
L , KPT1 , KPT2
L , KPT2 , KPT3
L , KPT3 , KPT4

! Create component
cm,COMPL_Embankment,line
/color,cm,whit,COMPL_Embankment


!!                                                                                                !!
!! 3.11  Railway lines                                                                            !!
!!                                                                                                !!
! NOTE: railwaylines (indicated as arrows) are drawn for display purposes only.

! Draw arrows marking each railway position
lsel,none
*do,nthRailway,1,LOAD_LTA_count,1

  zRailway = LOAD_LTA_offsets(nthRailway)

  *do,nthLoad,1,2,1

    gaugeOff = (-1**nthLoad)*LOAD_LTA_gauge

    *get,KPMax,KP,,NUM,MAXD
    KPRi = KPMax+1
    KPRj = KPMax+2
    KPRk = KPMax+3
    KPRl = KPMax+4
    K , KPRi , xPT  , yPT2     , zRailway+gaugeOff
    K , KPRj , xPT  , yPT2+2   , zRailway+gaugeOff
    K , KPRk , xPT  , yPT2+0.5 , zRailway-0.3+gaugeOff
    K , KPRl , xPT  , yPT2+0.5 , zRailway+0.3+gaugeOff
    L , KPRi , KPRj
    L , KPRi , KPRk
    L , KPRi , KPRl

  *enddo
*enddo

! Create component
cm,COMPL_Railway,line
/color,cm,red,COMPL_Railway


!!                                                                                                !!
!! 3.12  Capture images                                                                           !!
!!                                                                                                !!
/color , cm , dgra , COMPA_Walls
/color , cm , lgra , COMPA_Floor
/color , cm , lgra , COMPA_Roof
/color , cm , lgra , COMPA_struts
/color , cm , oran , COMPA_approach
/color , cm , gree , COMPA_headwalls

! Capture schematic side view (shows railway lines and embankment contour)
allsel
ksel,none
/view,1,-1,0,0
/auto,1
/type,1,3
/pnum,area,1
/number,1
gplot
takepic,'SM_Schematic',800

! Capture top view
allsel
/view,1,0,1,0
/auto,1
aplot
takepic,'SM_top',800
