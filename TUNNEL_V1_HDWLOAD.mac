! TUNNEL_V1_HDWLOAD

ALLSEL
CSYS,CS_Tunnel

*if,GEO_HDW_type,EQ,0,THEN

    *msg,UI,FILE_name
TUNNEL # %s %/&
APPLYING GRAVITATIONAL LOADS ON FOOTINGS...

    CMSEL,S,COMPA_HDW_Footing
    NSLA,S,1
    ESLN,S,1
    CM,COMPN_temp,NODE
    CM,COMPE_temp,ELEM
    *get,NodeCount,NODE,,COUNT

    *del,NodalLoads
    *dim,NodalLoads,ARRAY,NodeCount
    *del,NodeNumbers
    *dim,NodeNumbers,ARRAY,NodeCount

    NodeNum=0
    *do,NodeIndex,1,NodeCount,1
        CMSEL,S,COMPN_temp
        NodeNum=NDNEXT(NodeNum)
        NX=NX(NodeNum)
        NY=NY(NodeNum)
        NZ=NZ(NodeNum)

        tunnel_v1_soillevel,'soilLevel',NZ
        SoilHeightAbove=soilLevel-NY

        SoilPressure=Gammat*SoilHeightAbove

        CMSEL,S,COMPE_temp
        NSEL,S,NODE,,NodeNum
        ESLN,R
        nsle,s

        apportionedArea=arnode(NodeNum)

        NodalSoilLoad=SoilPressure*apportionedArea

        NodeNumbers(NodeIndex)=NodeNum
        NodalLoads(NodeIndex)=-NodalSoilLoad

    *enddo

    ALLSEL
    CSYS,0
    F,NodeNumbers(1:NodeCount),FY,NodalLoads(1:NodeCount)
    CSYS,CS_Tunnel


    !!
    *msg,UI,FILE_name
TUNNEL # %s %/&
APPLYING HORIZONTAL LOADS ON WALLS...

    CMSEL,S,COMPA_HDW_Walls
    NSLA,S,1
    NSEL,U,LOC,Y,-GEO_HDW_footDepth

    ESLN,S,1
    CM,COMPE_temp,ELEM
    CM,COMPN_temp,NODE

    *get,NodeCount,NODE,0,COUNT
    *del,NodeNumbers
    *dim,NodeNumbers,ARRAY,NodeCount
    *del,NodalXLoad
    *dim,NodalXLoad,ARRAY,NodeCount
    *del,NodalZLoad
    *dim,NodalZLoad,ARRAY,NodeCount

    NodeNum=0
    *do,NodeIndex,1,NodeCount,1

        CMSEL,S,COMPN_temp
        NodeNum=NDNEXT(NodeNum)

        NX=NX(NodeNum)
        NY=NY(NodeNum)
        NZ=NZ(NodeNum)

        tunnel_v1_soillevel,'soilLevel',NZ
        SoilHeightAbove=soilLevel-NY

        ! Tensão horizontal devido à camada de solo
        EffectivePressure=Gammat*SoilHeightAbove
        HorEffectivePressure=EffectivePressure*ActiveEPCoefficient-2*MAT_SOIL_cohesion*SQRT(ActiveEPCoefficient)

        NSEL,S,NODE,,NodeNum
        CMSEL,S,COMPE_temp
        ESLN,R,0
        nsle,s

        apportionedArea=arnode(NodeNum)

        HorLoad=HorEffectivePressure*apportionedArea

        *if,NX,LE,0,THEN
            xDir=1
        *else
            xDir=-1
        *endif
        *if,NZ,LE,0,THEN
            zDir=-1
        *else
            zDir=1
        *endif

        HorLoadX=xDir*HorLoad*COS(GEO_InletRadians)
        HorLoadZ=zDir*HorLoad*SIN(GEO_InletRadians)

        NodeNumbers(NodeIndex)=NodeNum
        NodalXLoad(NodeIndex)=HorLoadX
        NodalZLoad(NodeIndex)=HorLoadZ

    *enddo

    ALLSEL
    CSYS,0
    F,NodeNumbers(1:NodeCount),FX,NodalXLoad(1:NodeCount)
    F,NodeNumbers(1:NodeCount),FZ,NodalZLoad(1:NodeCount)
    CSYS,CS_Tunnel

*elseif,GEO_HDW_type,EQ,1,THEN

     *msg,UI,FILE_name
TUNNEL # %s %/&
APPLYING HORIZONTAL LOADS ON WINGS...

    CMSEL,S,COMPA_HDW_Walls
    CMSEL,A,COMPA_HDW_TopBeam
    NSLA,S,1

    ESLN,S,1
    CM,COMPE_temp,ELEM
    CM,COMPN_temp,NODE

    *get,NodeCount,NODE,0,COUNT
    *del,NodeNumbers
    *dim,NodeNumbers,ARRAY,NodeCount
    *del,NodalXLoad
    *dim,NodalXLoad,ARRAY,NodeCount
    *del,NodalZLoad
    *dim,NodalZLoad,ARRAY,NodeCount

    NodeNum=0
    *do,NodeIndex,1,NodeCount,1

        CMSEL,S,COMPN_temp
        NodeNum=NDNEXT(NodeNum)

        NX=NX(NodeNum)
        NY=NY(NodeNum)
        NZ=NZ(NodeNum)

        SoilHeightAbove=GEO_HDW_Height-NY                                                                                !!

        ! Tensão horizontal devido à camada de solo
        EffectivePressure=Gammat*SoilHeightAbove
        HorEffectivePressure=EffectivePressure*ActiveEPCoefficient-2*MAT_SOIL_cohesion*SQRT(ActiveEPCoefficient)

        NSEL,S,NODE,,NodeNum
        CMSEL,S,COMPE_temp
        ESLN,R,0
        nsle,s

        apportionedArea=arnode(NodeNum)

        HorLoad=HorEffectivePressure*apportionedArea

!        *if,NX,LE,1e-6,THEN
!            xDir=1
!        *else
!            xDir=-1
!        *endif
!        *if,NZ,LE,1e-6,THEN
!            zDir=-1
!        *else
!            zDir=1
!        *endif
!        HorLoadX=xDir*HorLoad*COS(GEO_InletRadians)
!        HorLoadZ=zDir*HorLoad*SIN(GEO_InletRadians)

        *if,NZ,LE,GEO_TUN_length/2,THEN
            xDir=1
            zDir=-1
        *else
            xDir=-1
            zDir=1
        *endif
        HorLoadX=xDir*HorLoad*SIN(GEO_InletRadians)
        HorLoadZ=zDir*HorLoad*COS(GEO_InletRadians)

        NodeNumbers(NodeIndex)=NodeNum
        NodalXLoad(NodeIndex)=HorLoadX
        NodalZLoad(NodeIndex)=HorLoadZ

    *enddo

    ALLSEL
    CSYS,0
    F,NodeNumbers(1:NodeCount),FX,NodalXLoad(1:NodeCount)
    F,NodeNumbers(1:NodeCount),FZ,NodalZLoad(1:NodeCount)
    CSYS,CS_Tunnel

*endif






