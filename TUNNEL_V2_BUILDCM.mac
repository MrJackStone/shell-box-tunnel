! TUNNEL_V2_BUILDCM

! Prepare environment
csbackup

! Tunnel
csys,CS_begSkewed
asel,s,loc,z,0,1e6
asel,u,loc,z,0
csys,CS_endSkewed
asel,r,loc,z,0,1e6
asel,u,loc,z,0
csys,CS_tunnel
asel,r,loc,x,0,GEO_TUN_totEffW
cm,COMPA_tunnel,area

! End frames
csys,CS_begSkewed
asel,s,loc,z,0
cm,COMPA_frame1,area

csys,CS_endSkewed
asel,s,loc,z,0
cm,COMPA_frame2,area

cmsel,s,COMPA_frame1
cmsel,a,COMPA_frame2
cm,COMPA_frames,area

! Tunnel walls
csys,CS_tunnel
cmsel , s , COMPA_tunnel
asel , u , loc , y , 0
asel , u , loc , y , GEO_TUN_effH
cm,COMPA_Walls,AREA

! Left tunnel wall
csys,CS_tunnel
cmsel,s,COMPA_walls
asel,r,loc,x,0
cm,COMPA_LeftWall,area

lsla
lsel,r,loc,y,0
cm,COMPL_LWALL_floorInterface,line
lsla
lsel,r,loc,y,GEO_TUN_effH
cm,COMPL_LWALL_roofInterface,line

csys,CS_begSkewed
cmsel,s,COMPA_leftWall
lsla
lsel,r,loc,z,0
cm,COMPL_LWALL_interfaceInlet1,line

csys,CS_endSkewed
cmsel,s,COMPA_leftWall
lsla
lsel,r,loc,z,0
cm,COMPL_LWALL_interfaceInlet2,line

! Right tunnel wall
csys,CS_tunnel
cmsel,s,COMPA_Walls
asel,r,loc,x,GEO_TUN_totEffW
cm,COMPA_RightWall,area

lsla
lsel,r,loc,y,0
cm,COMPL_RWALL_floorInterface,line
lsla
lsel,r,loc,y,GEO_TUN_effH
cm,COMPL_RWALL_roofInterface,line

csys,CS_begSkewed
cmsel,s,COMPA_rightWall
lsla
lsel,r,loc,z,0
cm,COMPL_RWALL_interfaceInlet1,line

csys,CS_endSkewed
cmsel,s,COMPA_rightWall
lsla
lsel,r,loc,z,0
cm,COMPL_RWALL_interfaceInlet2,line

! Inner tunnel walls
csys,CS_tunnel
cmsel,s,COMPA_Walls
cmsel,u,COMPA_LeftWall
cmsel,u,COMPA_RightWall
cm,COMPA_InnerWalls,area
lsla
lsel,r,loc,y,0
cm,COMPL_IWALL_floorInterface,line
lsla
lsel,r,loc,y,GEO_TUN_effH
cm,COMPL_IWALL_roofInterface,line

cmsel,s,COMPA_InnerWalls
lsla
ksll
asel,r,loc,y,0,GEO_IWALL_botBeamHeight
cm,COMPA_bBeam,area

cmsel,s,COMPA_InnerWalls
asel,r,loc,y,GEO_TUN_effH-GEO_IWALL_topBeamHeight,GEO_TUN_effH
cm,COMPA_tBeam,area

cmsel,s,COMPA_InnerWalls
cmsel,u,COMPA_bBeam
cmsel,u,COMPA_tBeam
cm,COMPA_Columns,area

*do,nthWall,1,GEO_IWALL_count,1
  csys,CS_innerWall%nthWall%

  openingWidth = GEO_IWALL_opening_widths(nthWall)

  cmsel,s,COMPA_innerWalls
  asel,r,loc,z,0
  cm,COMPA_IWALL_%nthWall%,area

  cmsel,s,COMPA_bBeam
  cmsel,r,COMPA_IWALL_%nthWall%
  cm,COMPA_IWALL%nthWall%_bBeam,area

  cmsel,s,COMPA_tBeam
  cmsel,r,COMPA_IWALL_%nthWall%
  cm,COMPA_IWALL%nthWall%_tBeam,area

  cmsel,s,COMPA_columns
  cmsel,r,COMPA_IWALL_%nthWall%
  cm,COMPA_IWALL%nthWall%_columns,area

  *do,nthOpening,1,GEO_IWALL_openingCount,1
    openingCenter = GEO_IWALL_opening_z_centers(nthWall, nthOpening)
    nthColIdx     = nthOpening
    nthCol2ndEdge = openingCenter-(openingWidth/2)
    nthCol1stEdge = nthCol2ndEdge-GEO_IWALL_columnWidth
    mthColIdx     = nthOpening+1
    mthCol2ndEdge = openingCenter+(openingWidth/2)
    mthCol1stEdge = mthCol2ndEdge+GEO_IWALL_columnWidth

    asel,s,loc,y,nthCol1stEdge,nthCol2ndEdge
    cmsel,r,COMPA_IWALL_%nthWall%
    cmsel,r,COMPA_columns
    cm,COMPA_IWALL%nthWall%_column%nthColIdx%,area

    asel,s,loc,y,mthCol1stEdge,mthCol2ndEdge
    cmsel,r,COMPA_IWALL_%nthWall%
    cmsel,r,COMPA_columns
    cm,COMPA_IWALL%nthWall%_column%mthColIdx%,area
  *enddo
*enddo

! Floor slab
csys,CS_tunnel
cmsel,s,COMPA_tunnel
asel,r,loc,y,0
cm,COMPA_floor,area

csys,CS_begSkewed
cmsel,s,COMPA_floor
lsla
lsel,r,loc,z,0
cm,COMPL_FLOOR_interfaceInlet1,line

csys,CS_endSkewed
cmsel,s,COMPA_floor
lsla
lsel,r,loc,z,0
cm,COMPL_FLOOR_interfaceInlet2,line

! Roof struts
csys,CS_tunnel
asel,none
*if,GEO_ROOF_gapStatus,eq,1,and,GEO_ROOF_gapStrutCount,gt,0,then
  asel , a , loc , z , GEO_ROOF_gap_z_ends(1:GEO_ROOF_gapStrutCount) , GEO_ROOF_gap_z_begs(2:GEO_ROOF_gapCount)
  asel , r , loc , y , GEO_TUN_effH
*endif
cm,COMPA_Struts,area

! Roof slab
csys,CS_tunnel
cmsel,s,COMPA_tunnel
asel,r,loc,y,GEO_TUN_effH
cmsel,u,COMPA_Struts
cm,COMPA_Roof,area

csys,CS_begSkewed
cmsel,s,COMPA_roof
lsla
lsel,r,loc,z,0
cm,COMPL_ROOF_interfaceInlet1,line

csys,CS_endSkewed
cmsel,s,COMPA_roof
lsla
lsel,r,loc,z,0
cm,COMPL_ROOF_interfaceInlet2,line

! Roof precast girders
csys,CS_tunnel
*if,GEO_ROOF_type,eq,1,then
  zi = 0
  *do,nthGirder,1,GEO_girderCount-1,1
    zf = GEO_girderZ(nthGirder)
    cmsel,s,COMPA_roof
    cmsel,u,COMPA_struts
    asel,r,loc,z,zi,zf
    cm,COMPA_RG%nthGirder%,area
    zi = zf
  *enddo
  cmsel,s,COMPA_roof
  cmsel,u,COMPA_struts
  asel,r,loc,z,zi,GEO_TUN_Length
  cm,COMPA_RG%GEO_girderCount%,area
*endif

! Tunnel
cmsel,s,COMPA_Tunnel
lsla
ksll
cm,COMPL_Tunnel,line
cm,COMPK_Tunnel,kp

! Clean up
csrestore
