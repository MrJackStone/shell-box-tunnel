! TUNNEL_V2_BUILDCM

! Prepare environment
csbackup
csys,CS_tunnel

! Tunnel walls
asel , s , loc , z , 0              , GEO_TUN_length
asel , u , loc , z , 0
asel , u , loc , z , GEO_TUN_length
asel , u , loc , y , 0
asel , u , loc , y , GEO_TUN_effH
cm,COMPA_Walls,AREA

! Left tunnel wall
asel,r,loc,x,0
cm,COMPA_LeftWall,area

! Right tunnel wall
cmsel,S,COMPA_Walls
asel,r,loc,x,GEO_TUN_totEffW
cm,COMPA_RightWall,area

! Inner tunnel walls
cmsel,s,COMPA_Walls
cmsel,u,COMPA_LeftWall
cmsel,u,COMPA_RightWall
cm,COMPA_InnerWalls,area

*do,nthWall,1,GEO_IWALL_count,1
  xCoord = GEO_IWALL_x_coords(nthWall)
  cmsel,s,COMPA_innerWalls
  asel,r,loc,x,xCoord
  cm,COMPA_IWALL_%nthWall%,area
*enddo

! Roof struts
asel,none
*if,GEO_ROOF_gapStatus,eq,1,and,GEO_ROOF_gapStrutCount,gt,0,then
  asel , a , loc , z , GEO_ROOF_gap_z_ends(1:GEO_ROOF_gapStrutCount) , GEO_ROOF_gap_z_begs(2:GEO_ROOF_gapCount)
  asel , r , loc , y , GEO_TUN_effH
*endif
cm,COMPA_Struts,area

! Floor slab
asel,s,loc,y,0
asel,r,loc,z,0,GEO_TUN_length
cm,COMPA_floor,area

! Roof slab
asel,s,loc,y,GEO_TUN_effH
asel,r,loc,z,0,GEO_TUN_length
asel,r,loc,x,0,GEO_TUN_totEffW
cmsel,u,COMPA_Struts
cm,COMPA_Roof,area

! Tunnel
cmsel , s , COMPA_Walls
cmsel , a , COMPA_Floor
cmsel , a , COMPA_Roof
cmsel , a , COMPA_Struts
cm , COMPA_Tunnel , area

lsla,s
ksll,s
cm,COMPL_Tunnel,line
cm,COMPK_Tunnel,kp

! Clean up
csrestore
