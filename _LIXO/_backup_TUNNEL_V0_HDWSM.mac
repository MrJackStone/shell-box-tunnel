! TUNNEL_V0_HDWSM, refCS, CS_HDW_LeftWall, CS_HDW_RightWall
! TUNNEL_V0_HDWSM,  arg1,            arg2,             arg3

CS_HDW_LeftWall=arg2
CS_HDW_RightWall=arg3

!!
!! GEOMETRIC PARAMETERS
!!
GEO_HDW_Height=Eff_GalleryHeight+GEO_HDW_HeadBeamH

x1=0
x2=GEO_HDW_stiffXOff
x3=Eff_GalleryWidth-GEO_HDW_stiffXOff
x4=Eff_GalleryWidth
x5=-GEO_InletLength*SIN(GEO_InletRadians)
x6=x5+GEO_HDW_stiffXOff
x7=x3+(SIN(GEO_InletRadians)*GEO_InletLength)
x8=x7+GEO_HDW_stiffXOff
x9=x1-(GEO_FootingWidth/COS(GEO_InletRadians))
x10=x1
x11=x4
x12=x11+(GEO_FootingWidth/COS(GEO_InletRadians))
x13=x5-(GEO_FootingWidth/COS(GEO_InletRadians))
x14=x5
x15=x8
x16=x15+(GEO_FootingWidth/COS(GEO_InletRadians))
x17=x1
x18=x4

yTop=Eff_GalleryHeight
yFoot=-GEO_FootingDepth
yBeam=GEO_HDW_Height

zEnd=GalleryLength
zFront=GalleryLength+GEO_InletLength

CSYS,arg1

!!
!! KEYPOINTS
!!
! Existing keypoints
KP1=KP(x1,0,zEnd)
KP2=KP(x2,0,zEnd)
KP3=KP(x3,0,zEnd)
KP4=KP(x4,0,zEnd)
KP27=KP(x1,yTop,zEnd)
KP29=KP(x4,yTop,zEnd)

! New keypoints
*get,KPMaxD,KP,,NUM,MAXD
KP5=KPMaxD+1
KP6=KPMaxD+2
KP7=KPMaxD+3
KP8=KPMaxD+4
KP9=KPMaxD+5
KP10=KPMaxD+6
KP11=KPMaxD+7
KP12=KPMaxD+8
KP13=KPMaxD+9
KP14=KPMaxD+10
KP15=KPMaxD+11
KP16=KPMaxD+12
KP17=KPMaxD+13
KP18=KPMaxD+14

Left_PI1=KPMaxD+15
Left_AX1=KPMaxD+16
Left_PI2=KPMaxD+17
Left_AX2=KPMaxD+18
Right_PI1=KPMaxD+19
Right_AX1=KPMaxD+20
Right_PI2=KPMaxD+21
Right_AX2=KPMaxD+22

KP28=KPMaxD+23
KP30=KPMaxD+24

K,KP5,x5,0,zFront
K,KP6,x6,0,zFront
K,KP7,x7,0,zFront
K,KP8,x8,0,zFront

K,KP9,x9,yFoot,zEnd
K,KP10,x10,yFoot,zEnd
K,KP11,x11,yFoot,zEnd
K,KP12,x12,yFoot,zEnd

K,KP13,x13,yFoot,zFront
K,KP14,x14,yFoot,zFront
K,KP15,x15,yFoot,zFront
K,KP16,x16,yFoot,zFront

K,KP17,x17,yBeam,zEnd
K,KP18,x18,yBeam,zEnd

! Left wall
CSKP,CS_HDW_LeftWall,CART,KP5,KP1,KP17
CSYS,CS_HDW_LeftWall

KP17x=KX(KP17)
KP17y=KY(KP17)
xCenter1=GEO_HDW_CornerRadius
yCenter1=0
xCenter2=KP17x
yCenter2=KP17y-GEO_HDW_CornerRadius

ThetaAngle=ATAN((xCenter2-xCenter1)/(yCenter2-yCenter1))
HorDelta=GEO_HDW_CornerRadius*COS(ThetaAngle)
VerDelta=GEO_HDW_CornerRadius*SIN(ThetaAngle)
HorHalfDelta=GEO_HDW_CornerRadius*COS(ThetaAngle/2)
VerHalfDelta=GEO_HDW_CornerRadius*SIN(ThetaAngle/2)
HorHalfDeltaPlus45=GEO_HDW_CornerRadius*COS((PI/4)+(ThetaAngle/2))
VerHalfDeltaPlus45=GEO_HDW_CornerRadius*SIN((PI/4)+(ThetaAngle/2))

K,Left_PI1,xCenter1-HorDelta,yCenter1+VerDelta,0
K,Left_AX1,xCenter1-HorHalfDelta,yCenter1+VerHalfDelta,0
K,Left_PI2,xCenter2-HorDelta,yCenter2+VerDelta,0
K,Left_AX2,xCenter2-HorHalfDeltaPlus45,yCenter2+VerHalfDeltaPlus45,0

! Right wall
CSKP,CS_HDW_RightWall,CART,KP8,KP4,KP18
CSYS,CS_HDW_RightWall
K,Right_PI1,xCenter1-HorDelta,yCenter1+VerDelta,0
K,Right_AX1,xCenter1-HorHalfDelta,yCenter1+VerHalfDelta,0
K,Right_PI2,xCenter2-HorDelta,yCenter2+VerDelta,0
K,Right_AX2,xCenter2-HorHalfDeltaPlus45,yCenter2+VerHalfDeltaPlus45,0

CSYS,arg1


!!
!! LINES
!!
ALLSEL
LSEL,U,LINE,,ALL

KSEL,S,KP,,KP1
KSEL,A,KP,,KP2
LSLK,S,1
*get,L1,LINE,0,NUM,MAX
LSEL,U,LINE,,ALL

KSEL,S,KP,,KP2
KSEL,A,KP,,KP3
LSLK,S,1
*get,L2,LINE,0,NUM,MAX
LSEL,U,LINE,,ALL

KSEL,S,KP,,KP3
KSEL,A,KP,,KP4
LSLK,S,1
*get,L3,LINE,0,NUM,MAX
LSEL,U,LINE,,ALL
KSEL,S,KP,,ALL

L,KP5,KP6
*get,L4,LINE,0,NUM,MAX
L,KP6,KP7
*get,L5,LINE,0,NUM,MAX
L,KP7,KP8
*get,L6,LINE,0,NUM,MAX
L,KP1,KP5
*get,L7,LINE,0,NUM,MAX
L,KP2,KP6
*get,L8,LINE,0,NUM,MAX
L,KP3,KP7
*get,L9,LINE,0,NUM,MAX
L,KP4,KP8
*get,L10,LINE,0,NUM,MAX

L,KP9,KP10
*get,L11,LINE,0,NUM,MAX
L,KP11,KP12
*get,L12,LINE,0,NUM,MAX
L,KP13,KP14
*get,L13,LINE,0,NUM,MAX
L,KP15,KP16
*get,L14,LINE,0,NUM,MAX
L,KP9,KP13
*get,L15,LINE,0,NUM,MAX
L,KP10,KP14
*get,L16,LINE,0,NUM,MAX
L,KP11,KP15
*get,L17,LINE,0,NUM,MAX
L,KP12,KP16
*get,L18,LINE,0,NUM,MAX
L,KP10,KP1
*get,L19,LINE,0,NUM,MAX
L,KP11,KP4
*get,L20,LINE,0,NUM,MAX
L,KP14,KP5
*get,L21,LINE,0,NUM,MAX
L,KP15,KP8
*get,L22,LINE,0,NUM,MAX

L,KP1,KP17
*get,L23,LINE,0,NUM,MAX
L,KP4,KP18

*get,L24,LINE,0,NUM,MAX
LARC,Left_PI1,KP5,Left_AX1
*get,L25,LINE,0,NUM,MAX
LARC,KP17,Left_PI2,Left_AX2
*get,L26,LINE,0,NUM,MAX
L,Left_PI1,Left_PI2
*get,L27,LINE,0,NUM,MAX

LARC,Right_PI1,KP8,Right_AX1
*get,L28,LINE,0,NUM,MAX
LARC,KP18,Right_PI2,Right_AX2
*get,L29,LINE,0,NUM,MAX
L,Right_PI1,Right_PI2
*get,L30,LINE,0,NUM,MAX

ALLSEL


!!
!! AREAS
!!
ASEL,U,AREA,,ALL

AL,L7,L4,L8,L1
AL,L9,L6,L10,L3
CMSEL,A,COMPA_HDW_Stiffener
CM,COMPA_HDW_Stiffener,AREA
ASEL,U,AREA,,ALL

AL,L8,L5,L9,L2
CMSEL,A,COMPA_HDW_Slab
CM,COMPA_HDW_Slab,AREA
ASEL,U,AREA,,ALL

AL,L15,L13,L16,L11
AL,L17,L14,L18,L12
CMSEL,A,COMPA_HDW_Footing
CM,COMPA_HDW_Footing,AREA
ASEL,U,AREA,,ALL

AL,L21,L7,L19,L16
AL,L27,L26,L23,L7,L25
CMSEL,A,COMPA_HDW_LeftWall
CM,COMPA_HDW_LeftWall,AREA
ASEL,U,AREA,,ALL

AL,L22,L10,L20,L17
AL,L30,L29,L24,L10,L28
CMSEL,A,COMPA_HDW_RightWall
CM,COMPA_HDW_RightWall,AREA

CMSEL,S,COMPA_HDW_LeftWall
LSLA,S
KSLL,S
CSSLICEXZ,Eff_GalleryHeight
CM,COMPA_HDW_LeftWall,AREA
CMSEL,S,COMPA_HDW_RightWall
LSLA,S
KSLL,S
CSSLICEXZ,Eff_GalleryHeight
CM,COMPA_HDW_RightWall,AREA
CMSEL,S,COMPA_HDW_RightWall
CMSEL,A,COMPA_HDW_LeftWall
CMSEL,A,COMPA_HDW_Walls
CM,COMPA_HDW_Walls,AREA
LSLA,S
KSLL,S
ASEL,U,AREA,,ALL
KBL=KP(0,Eff_GalleryHeight,zEnd)
KTL=KP(0,GEO_HDW_Height,zEnd)
KBR=KP(Eff_GalleryWidth*GalleryModules,Eff_GalleryHeight,zEnd)
KTR=KP(Eff_GalleryWidth*GalleryModules,GEO_HDW_Height,zEnd)
A,KBL,KBR,KTR,KTL
CMSEL,A,COMPA_HDW_TopBeam
CM,COMPA_HDW_TopBeam,AREA

ALLSEL
ASEL,U,AREA,,ALL
A,KP14,KP15,KP8,KP7,KP6,KP5
CMSEL,A,COMPA_HDW_BotBeam
CM,COMPA_HDW_BotBeam,AREA

ALLSEL
ASEL,U,AREA,,ALL
A,KP10,KP11,KP4,KP3,KP2,KP1
CMSEL,A,COMPA_HDW_BackBeam
CM,COMPA_HDW_BackBeam,AREA
ALLSEL

ALLSEL
NUMMRG,KP



