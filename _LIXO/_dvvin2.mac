CMSEL,S,COMPA_Roof
NSLA,S,1
NDLeftBeginning=NODE(0,Eff_GalleryHeight,0)
NDRightEnd=NODE(Eff_GalleryWidth,Eff_GalleryHeight,GalleryLength)

*do,nthSect,1,sectCount-1,1

    *msg,UI,nthSect,sectCount-1
SECTION: %I / %I

    zSect_b=(nthSect-1)*deltaZSect
    zSect_e=zSect_b+deltaZSect

    zMid=(GEO_ExtraLength/2)+(zSect_b+zSect_e)/2

    zSect_bR=zSect_b+GEO_ExtraLength
    zSect_eR=zSect_e+GEO_ExtraLength

    !!
    CSYS,CS_Tunnel
    *if,GEO_RetMode,EQ,1,THEN
        TUNNEL_V0_OBLQSECT
    *else
        NSEL,S,LOC,Z,zSect_b,zSect_e
    *endif
    CMSEL,S,COMPA_Tunnel
    NSLA,R,1
    !!

    ESLN,S,0
    ESLA,R
    CM,COMPE_Section,ELEM
    CSYS,0


    *do,nthLSType,1,2,1
        LSKey=LSOptions(nthLSType)

        !!
        !! CROSS-SECTIONAL RESULTS
        !!
        *cfopen,'RES_SECT',TXT,,APPEND

            *vwrite,zMid
            (F8.2,'    ',\b)

            !! ROOF SLAB
            !!
            CMSEL,S,COMPA_Roof
            ESLA,S
            CMSEL,R,COMPE_Section
            NSLE,S
            TUNNEL_V0_SECRES,LSKey,0
            *vwrite, MSup/1000, VSup/1000, MSpan/1000, NSpan/1000, DFL*1000
            (F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'    ',\b)

            !! FLOOR SLAB
            !!
            CMSEL,S,COMPA_Floor
            ESLA,S
            CMSEL,R,COMPE_Section
            NSLE,S
            TUNNEL_V0_SECRES,LSKey,0
            *vwrite, MSup/1000, VSup/1000, MSpan/1000, NSpan/1000, DFL*1000
            (F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'    ',\b)

            !! WALLS
            !!

            !! LEFT WALL
            CMSEL,S,COMPA_LeftWall
            ESLA,S
            CMSEL,R,COMPE_Section
            NSLE,S
            TUNNEL_V0_SECRES,LSKey,1
            LW_MSup=MSup
            LW_VSup=VSup
            LW_MSpan=MSpan
            LW_NSpan=NSpan
            LW_DFL=DFL

            !! RIGHT WALL
            CMSEL,S,COMPA_RightWall
            ESLA,S
            CMSEL,R,COMPE_Section
            NSLE,S
            TUNNEL_V0_SECRES,LSKey,1
            RW_MSup=MSup
            RW_VSup=VSup
            RW_MSpan=MSpan
            RW_NSpan=NSpan
            RW_DFL=DFL

            !! MAXIMUM VALUES
            COMPARE,'MSup','MAX',2,LW_MSup,RW_MSup
            COMPARE,'MSpan','MIN',2,LW_MSpan,RW_MSpan
            COMPARE,'VSup','MAX',2,LW_VSup,RW_VSup
            COMPARE,'NSpan','MIN',2,LW_NSpan,RW_NSpan
            COMPARE,'DFL','MAX',2,LW_DFL,RW_DFL
            *vwrite, MSup/1000, VSup/1000, MSpan/1000, NSpan/1000, DFL*1000
            (F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2)

        *cfclos


        !!
        !! LONGITUDINAL RESULTS
        !!
        *cfopen,'RES_LONG',txt,,APPEND

            *vwrite,zMid
            (F8.2,'    ',\b)

            !! ROOF SLAB
            !!
            CMSEL,S,COMPA_Roof
            ESLA,S
            CMSEL,R,COMPE_Section
            NSLE,S
            TUNNEL_V0_LONGRES,LSKey
            *vwrite, Mmin/1000, Nmin/1000, Mmax/1000, VMax/1000, NMax/1000
            (F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'    ',\b)

            !! FLOOR SLAB
            !!
            CMSEL,S,COMPA_Floor
            ESLA,S
            CMSEL,R,COMPE_Section
            NSLE,S
            TUNNEL_V0_LONGRES,LSKey
            *vwrite, Mmin/1000, Nmin/1000, Mmax/1000, VMax/1000, NMax/1000
            (F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'    ',\b)

            !! WALLS
            !!
            CMSEL,S,COMPA_Walls
            ESLA,S
            CMSEL,R,COMPE_Section
            NSLE,S
            TUNNEL_V0_LONGRES,LSKey
            *vwrite, Mmin/1000, Nmin/1000, Mmax/1000, VMax/1000, NMax/1000
            (F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2,'  ',F8.2)

        *cfclos


    *enddo

*enddo

