! TUNNEL_V1_IWRES

/post1

allsel
set,first
esel,s,type,,TYPE_SHELL181
nsle,s
etable,'SM_11',smisc,34
etable,'SM_22',smisc,35
etable,'SM_12',smisc,36

etable,'SB_11',smisc,37
etable,'SB_22',smisc,38
etable,'SB_12',smisc,39

! Initialize path envelope arrays
*del,RES_TBEAM_M_max
*del,RES_TBEAM_M_min
*del,RES_TBEAM_N_max
*del,RES_TBEAM_N_min
*del,RES_TBEAM_V_max
*del,RES_TBEAM_V_min
*del,RES_BBEAM_M_max
*del,RES_BBEAM_M_min
*del,RES_BBEAM_N_max
*del,RES_BBEAM_N_min
*del,RES_BBEAM_V_max
*del,RES_BBEAM_V_min
*dim,RES_TBEAM_M_max,,101
*dim,RES_TBEAM_M_min,,101
*dim,RES_TBEAM_N_max,,101
*dim,RES_TBEAM_N_min,,101
*dim,RES_TBEAM_V_max,,101
*dim,RES_TBEAM_V_min,,101
*dim,RES_BBEAM_M_max,,101
*dim,RES_BBEAM_M_min,,101
*dim,RES_BBEAM_N_max,,101
*dim,RES_BBEAM_N_min,,101
*dim,RES_BBEAM_V_max,,101
*dim,RES_BBEAM_V_min,,101
*vfill,RES_TBEAM_M_max,ramp,0,0
*vfill,RES_TBEAM_M_min,ramp,0,0
*vfill,RES_TBEAM_N_max,ramp,0,0
*vfill,RES_TBEAM_N_min,ramp,0,0
*vfill,RES_TBEAM_V_max,ramp,0,0
*vfill,RES_TBEAM_V_min,ramp,0,0
*vfill,RES_BBEAM_M_max,ramp,0,0
*vfill,RES_BBEAM_M_min,ramp,0,0
*vfill,RES_BBEAM_N_max,ramp,0,0
*vfill,RES_BBEAM_N_min,ramp,0,0
*vfill,RES_BBEAM_V_max,ramp,0,0
*vfill,RES_BBEAM_V_min,ramp,0,0
*del,RES_COL_M_max
*del,RES_COL_M_min
*del,RES_COL_N_max
*del,RES_COL_N_min
*del,RES_COL_V_max
*del,RES_COL_V_min
*del,RES_COL_D_max
*del,RES_COL_D_min
*dim,RES_COL_M_max,,101, GEO_TUN_iCols
*dim,RES_COL_M_min,,101, GEO_TUN_iCols
*dim,RES_COL_N_max,,101, GEO_TUN_iCols
*dim,RES_COL_N_min,,101, GEO_TUN_iCols
*dim,RES_COL_V_max,,101, GEO_TUN_iCols
*dim,RES_COL_V_min,,101, GEO_TUN_iCols
*dim,RES_COL_D_max,,101, GEO_TUN_iCols
*dim,RES_COL_D_min,,101, GEO_TUN_iCols
*do,i,1,GEO_TUN_iCols,1
    *vfill,RES_COL_M_max(1,i),ramp,0,0
    *vfill,RES_COL_M_min(1,i),ramp,0,0
    *vfill,RES_COL_N_max(1,i),ramp,0,0
    *vfill,RES_COL_N_min(1,i),ramp,0,0
    *vfill,RES_COL_V_max(1,i),ramp,0,0
    *vfill,RES_COL_V_min(1,i),ramp,0,0
    *vfill,RES_COL_D_max(1,i),ramp,0,0
    *vfill,RES_COL_D_min(1,i),ramp,0,0
*enddo
*del,RES_TBEAM_D_max
*del,RES_TBEAM_D_min
*del,RES_BBEAM_D_max
*del,RES_BBEAM_D_min
*dim,RES_TBEAM_D_max,,101, GEO_TUN_iCols-1
*dim,RES_TBEAM_D_min,,101, GEO_TUN_iCols-1
*dim,RES_BBEAM_D_max,,101, GEO_TUN_iCols-1
*dim,RES_BBEAM_D_min,,101, GEO_TUN_iCols-1
*do,i,1,GEO_TUN_iCols-1,1
    *vfill,RES_TBEAM_D_max(1,i),ramp,0,0
    *vfill,RES_TBEAM_D_min(1,i),ramp,0,0
    *vfill,RES_BBEAM_D_max(1,i),ramp,0,0
    *vfill,RES_BBEAM_D_min(1,i),ramp,0,0
*enddo

! Calculate geometric cross-sectional properties
IWTB_A=GEO_TUN_tBeamH*GEO_TUN_tbB
IWTB_W=GEO_TUN_tbB*(GEO_TUN_tBeamH**2)/6
IWBB_A=GEO_TUN_bBeamH*GEO_TUN_bbB
IWBB_W=GEO_TUN_bbB*(GEO_TUN_bBeamH**2)/6
IWCOL_A=GEO_TUN_colW*GEO_TUN_colB
IWCOL_W=GEO_TUN_colB*(GEO_TUN_colW**2)/6

xWall=0
*do,nthWall,1,GEO_TUN_modules-1,1
    xWall=xWall+GEO_TUN_effW
    beamCS=CS_IWALL_beams(nthWall)
    csys,CS_Tunnel

    cmsel,s,COMPA_InnerWalls
    asel,r,loc,x,xWall
    cm,COMPA_temp,area

    csys,beamCS

    *do,nthLS,1,LoadStepCount,1

        allsel
        set,nthLS,last
        etable,refl

        ! Top beam
        cmsel,s,COMPA_temp
        cmsel,r,COMPA_tBeam
        esla,s
        nsle,s
        top_n1=node(             0 ,                GEO_TUN_effH , 0)
        top_n2=node(GEO_TUN_length ,                GEO_TUN_effH , 0)
        bot_n1=node(             0 , GEO_TUN_effH-GEO_TUN_tBeamH , 0)
        bot_n2=node(GEO_TUN_length , GEO_TUN_effH-GEO_TUN_tBeamH , 0)
        pths2mn, top_n1, top_n2, bot_n1, bot_n2,   'IWTB', 'SM_11', 100, IWTB_W, IWTB_A, 1
        pths2v,  top_n1, top_n2, bot_n1, bot_n2, 'IWTB_Z', 'SM_12', 100,       , IWTB_A, 1
        *voper,RES_TBEAM_M_max, RES_TBEAM_M_max, max, PTHS2MN_ARR_bendingMoment
        *voper,RES_TBEAM_M_min, RES_TBEAM_M_min, min, PTHS2MN_ARR_bendingMoment
        *voper,RES_TBEAM_N_max, RES_TBEAM_N_max, max, PTHS2MN_ARR_axialForce
        *voper,RES_TBEAM_N_min, RES_TBEAM_N_min, min, PTHS2MN_ARR_axialForce
        *voper,RES_TBEAM_V_max, RES_TBEAM_V_max, max, PTHS2V_ARR_shear
        *voper,RES_TBEAM_V_min, RES_TBEAM_V_min, min, PTHS2V_ARR_shear

        ! Bottom beam
        cmsel,s,COMPA_temp
        cmsel,r,COMPA_bBeam
        esla,s
        nsle,s
        top_n1=node(             0 , GEO_TUN_bBeamH , 0)
        top_n2=node(GEO_TUN_length , GEO_TUN_bBeamH , 0)
        bot_n1=node(             0 ,              0 , 0)
        bot_n2=node(GEO_TUN_length ,              0 , 0)
        pths2mn, top_n1, top_n2, bot_n1, bot_n2,   'IWBB', 'SM_11', 100, IWBB_W, IWBB_A, 1
        pths2v,  top_n1, top_n2, bot_n1, bot_n2, 'IWBB_Z', 'SM_12', 100,       , IWBB_A, 1
        *voper,RES_BBEAM_M_max, RES_BBEAM_M_max, max, PTHS2MN_ARR_bendingMoment
        *voper,RES_BBEAM_M_min, RES_BBEAM_M_min, min, PTHS2MN_ARR_bendingMoment
        *voper,RES_BBEAM_N_max, RES_BBEAM_N_max, max, PTHS2MN_ARR_axialForce
        *voper,RES_BBEAM_N_min, RES_BBEAM_N_min, min, PTHS2MN_ARR_axialForce
        *voper,RES_BBEAM_V_max, RES_BBEAM_V_max, max, PTHS2V_ARR_shear
        *voper,RES_BBEAM_V_min, RES_BBEAM_V_min, min, PTHS2V_ARR_shear

        ! Columns
        cmsel,s,COMPA_temp
        cmsel,r,COMPA_Columns
        esla,s
        nsle,s
        xLoc=0
        *do,nthCol,1,GEO_TUN_iCols,1
            top_n1=node(             xLoc ,              GEO_TUN_bBeamH , 0)
            top_n2=node(             xLoc , GEO_TUN_effH-GEO_TUN_tBeamH , 0)
            bot_n1=node(xLoc+GEO_TUN_colW ,              GEO_TUN_bBeamH , 0)
            bot_n2=node(xLoc+GEO_TUN_colW , GEO_TUN_effH-GEO_TUN_tBeamH , 0)
            pthRootMN=strcat('ICOL',chrval(nthCol))
            pthRootV=strcat('ICOLZ',chrval(nthCol))
            pthRootD=strcat('ICOLD',chrval(nthCol))
            pths2mn, top_n1, top_n2, bot_n1, bot_n2, pthRootMN, 'SM_11', 100, IWCOL_W, IWCOL_A, 1
            pths2v,  top_n1, top_n2, bot_n1, bot_n2,  pthRootV, 'SM_12', 100,        , IWCOL_A, 1
            pth2dfl, bot_n1, bot_n2, , , pthRootD, 'Z', 100, , 0, 1
            !*del,mmax_tmp
            !*vfun,mmax_temp,copy,RES_COL_M_max(1,nthCol)
            !*voper,mmax_temp,mmax_temp,max,PTHS2MN_ARR_bendingMoment
            !*vfun,RES_COL_M_max(1,nthCol),copy,mmax_temp
            *voper,RES_COL_M_max(1,nthCol),RES_COL_M_max(1,nthCol),max,PTHS2MN_ARR_bendingMoment
            *voper,RES_COL_M_min(1,nthCol),RES_COL_M_min(1,nthCol),min,PTHS2MN_ARR_bendingMoment
            *voper,RES_COL_N_max(1,nthCol),RES_COL_N_max(1,nthCol),max,PTHS2MN_ARR_axialForce
            *voper,RES_COL_N_min(1,nthCol),RES_COL_N_min(1,nthCol),min,PTHS2MN_ARR_axialForce
            *voper,RES_COL_V_max(1,nthCol),RES_COL_V_max(1,nthCol),max,PTHS2V_ARR_shear
            *voper,RES_COL_V_min(1,nthCol),RES_COL_V_min(1,nthCol),min,PTHS2V_ARR_shear
            *voper,RES_COL_D_max(1,nthCol),RES_COL_D_max(1,nthCol),max,PTH2DFL_ARR_defl
            *voper,RES_COL_D_min(1,nthCol),RES_COL_D_min(1,nthCol),min,PTH2DFL_ARR_defl

            xLoc=xLoc+GEO_TUN_colE
        *enddo

        ! Beam deflection
        cmsel,s,COMPA_temp
        cmsel,u,COMPA_Columns
        esla,s
        nsle,s
        xLoc=GEO_TUN_colW
        *do,nthBeam,1,GEO_TUN_iCols-1,1
            ! Top beam
            n1=node(            xLoc , GEO_TUN_effH-GEO_TUN_tBeamH , 0)
            n2=node(xLoc+GEO_TUN_opW , GEO_TUN_effH-GEO_TUN_tBeamH , 0)
            pthRootD=strcat('IWTB',chrval(nthBeam))
            pth2dfl, n1, n2, , , pthRootD, 'Y', 100, , 0, 1
            *voper,RES_TBEAM_D_max(1,nthBeam),RES_TBEAM_D_max(1,nthBeam),max,PTH2DFL_ARR_defl
            *voper,RES_TBEAM_D_min(1,nthBeam),RES_TBEAM_D_min(1,nthBeam),min,PTH2DFL_ARR_defl
            ! Bottom beam
            n1=node(            xLoc , GEO_TUN_bBeamH , 0)
            n2=node(xLoc+GEO_TUN_opW , GEO_TUN_bBeamH , 0)
            pthRootD=strcat('IWBB',chrval(nthBeam))
            pth2dfl, n1, n2, , , pthRootD, 'Y', 100, , 0, 1
            *voper,RES_BBEAM_D_max(1,nthBeam),RES_BBEAM_D_max(1,nthBeam),max,PTH2DFL_ARR_defl
            *voper,RES_BBEAM_D_min(1,nthBeam),RES_BBEAM_D_min(1,nthBeam),min,PTH2DFL_ARR_defl

            xLoc=xLoc+GEO_TUN_colE
        *enddo

    *enddo

*enddo

csys,CS_Tunnel
/view,1,-1,0,0
cmsel,s,COMPA_InnerWalls
asel,r,loc,x,GEO_TUN_totEffW-GEO_TUN_effW
cm,COMPA_temp,area
esla,s
nsle,s

! Map envelopes onto paths
! Top beam
path,'IWTB_B'
arr2pth,'RES_TBEAM_M_max','MMAX',5
arr2pth,'RES_TBEAM_M_min','MMIN',6
arr2pth,'RES_TBEAM_N_max','NMAX',7
arr2pth,'RES_TBEAM_N_min','NMIN',8
arr2pth,'RES_TBEAM_V_max','VMAX',9
arr2pth,'RES_TBEAM_V_min','VMIN',10

! Bottom beam
path,'IWBB_T'
arr2pth,'RES_BBEAM_M_max','MMAX',5
arr2pth,'RES_BBEAM_M_min','MMIN',6
arr2pth,'RES_BBEAM_N_max','NMAX',7
arr2pth,'RES_BBEAM_N_min','NMIN',8
arr2pth,'RES_BBEAM_V_max','VMAX',9
arr2pth,'RES_BBEAM_V_min','VMIN',10

! Columns
*do,nthCol,1,GEO_TUN_iCols,1
    pthName=strcat('ICOL',strcat(chrval(nthCol),'_T'))
    path,pthName
    *del,mmax
    *del,mmin
    *del,nmax
    *del,nmin
    *del,vmax
    *del,vmin
    *del,dmax
    *del,dmin
    *vfun,mmax,copy,RES_COL_M_max(1,nthCol)
    *vfun,mmin,copy,RES_COL_M_min(1,nthCol)
    *vfun,nmax,copy,RES_COL_N_max(1,nthCol)
    *vfun,nmin,copy,RES_COL_N_min(1,nthCol)
    *vfun,vmax,copy,RES_COL_V_max(1,nthCol)
    *vfun,vmin,copy,RES_COL_V_min(1,nthCol)
    *vfun,dmax,copy,RES_COL_D_max(1,nthCol)
    *vfun,dmin,copy,RES_COL_D_min(1,nthCol)
    arr2pth,'mmax','MMAX',5
    arr2pth,'mmin','MMIN',6
    arr2pth,'nmax','NMAX',7
    arr2pth,'nmin','NMIN',8
    arr2pth,'vmax','VMAX',9
    arr2pth,'vmin','VMIN',10
    arr2pth,'dmax','DMAX',11
    arr2pth,'dmin','DMIN',12
*enddo

! Beam deflections
*do,nthBeam,1,GEO_TUN_iCols-1,1
    ! Top beam
    pthName=strcat('IWTB',chrval(nthBeam))
    path,pthName
    *del,dmax
    *del,dmin
    *vfun,dmax,copy,RES_TBEAM_D_max(1,nthBeam)
    *vfun,dmin,copy,RES_TBEAM_D_min(1,nthBeam)
    arr2pth,'dmax','DMAX',5
    arr2pth,'dmin','DMIN',6
    ! Bottom beam
    pthName=strcat('IWBB',chrval(nthBeam))
    path,pthName
    *del,dmax
    *del,dmin
    *vfun,dmax,copy,RES_BBEAM_D_max(1,nthBeam)
    *vfun,dmin,copy,RES_BBEAM_D_min(1,nthBeam)
    arr2pth,'dmax','DMAX',5
    arr2pth,'dmin','DMIN',6
*enddo

! Plot path envelopes:
/gformat,f,1,0
! Internal reactions:
pthCount=2*(2+GEO_TUN_iCols)
*del,MPATHPLOT_ARR_pathNames
*del,MPATHPLOT_ARR_labels
*dim,MPATHPLOT_ARR_pathNames,char,pthCount
*dim,MPATHPLOT_ARR_labels,char,pthCount
MPATHPLOT_ARR_pathNames(1)='IWTB_B', 'IWTB_B', 'IWBB_T', 'IWBB_T'
*do,nthCol,1,GEO_TUN_iCols,1
    pthName=strcat('ICOL',strcat(chrval(nthCol),'_T'))
    MPATHPLOT_ARR_pathNames(4+(2*nthCol-1))=pthName
    MPATHPLOT_ARR_pathNames(4+(2*nthCol))=pthName
*enddo

! Bending moment
*do,i,1,pthCount,2
    MPATHPLOT_ARR_labels(i)='MMAX'
    MPATHPLOT_ARR_labels(i+1)='MMIN'
*enddo
TUNNEL_V1_MPATHPLOT, 'M. FLETOR [kNm]', 'IW_ENV_M', 1e-3

! Axial force
*do,i,1,pthCount,2
    MPATHPLOT_ARR_labels(i)='NMAX'
    MPATHPLOT_ARR_labels(i+1)='NMIN'
*enddo
TUNNEL_V1_MPATHPLOT, 'F. NORMAL [kN]', 'IW_ENV_N', 1e-3

! Shear force
*do,i,1,pthCount,2
    MPATHPLOT_ARR_labels(i)='VMAX'
    MPATHPLOT_ARR_labels(i+1)='VMIN'
*enddo
TUNNEL_V1_MPATHPLOT, 'F. CORTANTE [kN]', 'IW_ENV_V', 1e-3


! Deflections:
/gformat,F,0,3
pthCount=2*GEO_TUN_iCols+4*(GEO_TUN_iCols-1)
*del,MPATHPLOT_ARR_pathNames
*del,MPATHPLOT_ARR_labels
*dim,MPATHPLOT_ARR_pathNames,char,pthCount
*dim,MPATHPLOT_ARR_labels,char,pthCount

*do,nthBeam,1,GEO_TUN_iCols-1,1
    pthNameTB=strcat('IWTB',chrval(nthBeam))
    pthNameBB=strcat('IWBB',chrval(nthBeam))

    MPATHPLOT_ARR_pathNames(4*nthBeam-3)=pthNameTB
    MPATHPLOT_ARR_pathNames(4*nthBeam-2)=pthNameTB
    MPATHPLOT_ARR_pathNames(4*nthBeam-1)=pthNameBB
    MPATHPLOT_ARR_pathNames(4*nthBeam-0)=pthNameBB
*enddo

*do,nthCol,1,GEO_TUN_iCols,1
    pthName=strcat('ICOL',strcat(chrval(nthCol),'_T'))
    MPATHPLOT_ARR_pathNames(4*(GEO_TUN_iCols-1)+(2*nthCol-1))=pthName
    MPATHPLOT_ARR_pathNames(4*(GEO_TUN_iCols-1)+(2*nthCol))=pthName
*enddo

*do,i,1,pthCount,2
    MPATHPLOT_ARR_labels(i)='DMAX'
    MPATHPLOT_ARR_labels(i+1)='DMIN'
*enddo
TUNNEL_V1_MPATHPLOT, 'FLECHA [mm]', 'IW_ENV_D', 1e3


/gformat,defa
/annot,dele
windefa


