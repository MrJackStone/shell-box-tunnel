! TUNNEL_V1_HORBOUSSINESQ, loadDirection, xMin, xMax

!!
!!
!! ****** FIX HERE **********
!! PROBLEM: which one is LEFT WALL and which one is RIGHT WALL?
!! Seem to be different depending on GEO_RetMode 0 or 1
!!
!! ****** UPDATE 04/08/21 ******
!! Couldn't replicate the problem. Works fine as is.
!!
*if,arg1,GE,0,THEN
    direction=-1
*else
    direction=1
*endif
!!
!!
!!
!!
!!
!!
!/nopr
!/prep7
!
!lsel,u,line,,all

!!
*get,parType,parm,arg2,type
*if,parType,eq,0,then
    checkXMin=1
*else
    checkXMin=0
*endif

*get,parType,parm,arg3,type
*if,parType,eq,0,then
    checkXMax=1
*else
    checkXMax=0
*endif
!!

QX_0=GEO_TUN_totEffW/2
QZ_0=LOAD_z0

*get,NodeCount,NODE,0,COUNT
nthNode=0

*del,ARRN_Loaded
*dim,ARRN_Loaded,ARRAY,NodeCount
*del,ARRFX_Load
*dim,ARRFX_Load,ARRAY,NodeCount

*do,NodeIndex,1,NodeCount,1

    nthNode=NDNEXT(nthNode)

    NX=NX(nthNode)
    NY=NY(nthNode)
    NZ=NZ(nthNode)

    cm,COMPN_temp,node
    nsel,s,node,,nthNode
    esln,s,0
    esel,r,type,,TYPE_SHELL181
    nsle,s
    apportionedArea=arnode(nthNode)
    cmsel,s,COMPN_temp

    Depth=GalleryDepth+(Eff_GalleryHeight-NY)
    *del,AllStressIncreases
    *dim,AllStressIncreases,ARRAY,LOAD_lineCount

    *do,nthLine,1,LOAD_lineCount,1

        *del,StressIncreases
        *dim,StressIncreases,ARRAY,LOAD_LTPtCount

        *do,nthPointLoad,1,LOAD_LTPtCount,1

            LoadValue=LOADTRAIN_ARR_loads(nthPointLoad)
            LoadPosition=LOADTRAIN_ARR_positions(nthPointLoad)

            QX_i=LoadPosition+xOffset
            QY=Eff_GalleryHeight+GalleryDepth
            QZ_i=zOffsets(nthLine)
            TUNNEL_V1_LTPOS, QX_i, QZ_i, QX_0, QZ_0, -LOAD_skewRad, 0

            !!
            calculateLoad=1
            *if,checkXMin,eq,1,then
                *if,QX,lt,arg2,then
                    calculateLoad=0
                *endif
            *endif

            *if,checkXMax,eq,1,then
                *if,QX,gt,arg3,then
                    calculateLoad=0
                *endif
            *endif
            !!

            *if,calculateLoad,eq,1,then
                PerpDistance=ABS(QX-NX)
                FullDistance=SQRT(((NX-QX)**2)+((NZ-QZ)**2)+((NY-QY)**2))
                RelativeDepth=QY-NY

                *if,QX*direction,LE,NX*direction,OR,FullDistance,EQ,0,THEN
                    HorBoussinesqLoad=0
                *else
                    ! Fórmula do acréscimo de tensão horizontal devido a força concentrada
                    !   Valor muliplicado por 2 (dois) para compensar pela hipótese de semi-espaço infinito
                    !   na situação de repouso.
                    HorBoussinesqLoad=2*(3*LoadValue*(PerpDistance**2)*RelativeDepth/(2*pi*(FullDistance**5)))
                *endif

            *else
                HorBoussinesqLoad=0
!                *cfopen,suspicious,txt,,append
!                    *vwrite,nthNode
!                    (F20.0)
!                *cfclos

            *endif

            StressIncreases(nthPointLoad)=HorBoussinesqLoad

        *enddo

        *vscfun,RowStressIncrease,SUM,StressIncreases
        AllStressIncreases(nthLine)=RowStressIncrease

    *enddo

    *vscfun,StressIncrease,SUM,AllStressIncreases
    HorLoad=StressIncrease*apportionedArea

    ARRN_Loaded(NodeIndex)=nthNode
    ARRFX_Load(NodeIndex)=-direction*HorLoad

*enddo

F,ARRN_Loaded(1:NodeCount),FX,ARRFX_Load(1:NodeCount)

!compName=strcat('LTHx_',chrval(xOffIndex))
!cm,%compName%,line
!
!*del,hcolors
!*dim,hcolors,array,4
!vcolors(1)=8, 9, 10, 11
!
!cnum=mod(xOffIndex,4)+1
!/color,cm,cnum,%compName%
!
!cmsel,a,COMPL_ltpos
!cm,COMPL_ltpos,line
!
!/solu
!/gopr







