! TUNNEL_V1_VERBOUSSINESQ

/nopr
/prep7

lsel,u,line,,all
QX_0=GEO_TUN_totEffW/2
QZ_0=LOAD_z0

*get,NodeCount,NODE,0,COUNT

*do,nthLine,1,LOAD_lineCount,1

    *do,nthPointLoad,1,LOAD_LTPtCount,1

        LoadValue=LOADTRAIN_ARR_loads(nthPointLoad)
        LoadPosition=LOADTRAIN_ARR_positions(nthPointLoad)

        QX_i=LoadPosition+xOffset
        QY=Eff_GalleryHeight+GalleryDepth
        QZ_i=zOffsets(nthLine)
        TUNNEL_V0_LTPOS, QX_i, QZ_i, QX_0, QZ_0, -LOAD_skewRad, 1

        *if,GalleryDepth,LT,MinimumSoilCover,THEN

            APPLY_LOAD = 0

            *if,QX,GE,0,AND,QX,LE,GalleryModules*Eff_GalleryWidth,THEN
                ! CASE: load over tunnel
                *if,QZ,GE,0,AND,QZ,LE,GEO_TUN_length,THEN
                    APPLY_LOAD = 1
                *endif

            *else
                ! CASE: load over approach slab
                *if,GEO_APR_status,eq,1,then
                    *if,QX,ge,0-GEO_APR_length,and,QX,lt,0,then
                        APPLY_LOAD = 1
                    *elseif,QX,gt,GEO_TUN_totEffW,and,QX,le,GEO_TUN_totEffW+GEO_APR_length,then
                        APPLY_LOAD = 1
                    *endif
                *endif
            *endif

            *if,APPLY_LOAD,eq,1,then
                !ClosestNode=NODE(QX,QY,QZ)
                !F,ClosestNode,FY,-ABS(LoadValue)
                LeftTrackNode=NODE(QX,QY,QZ-(LOAD_gauge/2))
                RightTrackNode=NODE(QX,QY,QZ+(LOAD_gauge/2))
                F,LeftTrackNode,FY,-ABS(LoadValue/2)
                F,RightTrackNode,FY,-ABS(LoadValue/2)
            *endif

        *else

            *del,ARRN_Loaded
            *dim,ARRN_Loaded,ARRAY,NodeCount
            *del,ARRFY_Load
            *dim,ARRFY_Load,ARRAY,NodeCount

            nthNode=0
            *do,NodeIndex,1,NodeCount,1

                nthNode=NDNEXT(nthNode)

                NX=NX(nthNode)
                NY=NY(nthNode)
                NZ=NZ(nthNode)

                cm,COMPN_temp,node
                nsel,s,node,,nthNode
                esln,s,0
                esel,r,type,,TYPE_SHELL181
                nsle,s
                apportionedArea=arnode(nthNode)
                cmsel,s,COMPN_temp

                RadialDistance=SQRT(((NX-QX)**2)+((NZ-QZ)**2))
                RelativeDepth=QY-NY

                BoussinesqLoad=-3*LoadValue/(2*pi*(RelativeDepth**2)*((1+(RadialDistance/RelativeDepth)**2)**(5/2)))
                AdditionalLoad=BoussinesqLoad*apportionedArea

                ARRN_Loaded(NodeIndex)=nthNode
                ARRFY_Load(NodeIndex)=AdditionalLoad
                !F,nthNode,FY,AdditionalLoad

            *enddo

            ! Aply loads
            F,ARRN_Loaded(1:NodeCount),FY,ARRFY_Load(1:NodeCount)

        *endif

    *enddo

*enddo

compName=strcat('LTx_',chrval(xOffIndex))
cm,%compName%,line

*del,vcolors
*dim,vcolors,array,4
vcolors(1)=12, 1, 2, 3

cnum=mod(xOffIndex,4)+1
/color,cm,cnum,%compName%

cmsel,a,COMPL_ltpos
cm,COMPL_ltpos,line

/solu
/gopr



/eof

allsel
*vget,nn,node,1,nlist
*get,nc,parm,nn,dim,x
*del,apportNode
*dim,apportNode,,nc
*do,i,1,nc,1
    nnum=nn(i)
    apportNode(i)=arnode(nnum)
*enddo


F=360e3

*del,x
*del,y
*del,z
*del,deltaX
*del,deltaY
*del,deltaZ
*del,dxSq
*del,dzSq
*del,Rsq
*del,R
*del,tmp1
*del,tmp2
*del,tmp3
*del,tmp4
*del,tmp5
*del,tmp6
*del,tmp7
*del,tmp8
*del,pressure

*vget,X,node,1,loc,x,,,0
*vget,Y,node,1,loc,y,,,0
*vget,Z,node,1,loc,z,,,0

*voper,deltaX,QX,sub,X
*voper,deltaY,QY,sub,Y
*voper,deltaZ,QZ,sub,Z

*vfun,dxSq,pwr,deltaX,2
*vfun,dzSq,pwr,deltaZ,2

*voper,Rsq,dxSq,add,dzSq
*vfun,R,sqrt,Rsq

*voper,tmp1,R,div,deltaY
*vfun,tmp2,pwr,tmp1,2
*voper,tmp3,tmp2,add,1
*vfun,tmp4,pwr,tmp3,5/2
*voper,tmp5,tmp4,mult,deltaY
*voper,tmp6,tmp5,mult,deltaY
*voper,tmp7,tmp6,mult,2*PI
*vfun,tmp8,pwr,tmp7,-1
*voper,pressure,tmp8,mult,-3*F

*voper,allNodalF,pressure,mult,apportNode

*vget,nsele,node,1,nsel
*vmask,nsele
*vfun,appliedF,comp,allNodalF

*vget,nodes,node,,nlist
!*vmask,nsele
!*vfun,nodes,comp,nnums
*get,nc,parm,nodes,dim,x

f,nodes(1:nc),fy,appliedF(1:nc)
