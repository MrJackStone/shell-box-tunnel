! TUNNEL_V2_FEM


*msg,UI,FILE_name,'CREATING FINITE ELEMENT MODEL...'
TUNNEL # %s %/&
%35C

/prep7

CSYS,0

!!                                                                                                !!
!! 4.1  Tunnel                                                                                    !!
!!                                                                                                !!
TUNNEL_V2_FEMTUNNEL


!!                                                                                                !!
!! 4.2  Headwall                                                                                  !!
!!                                                                                                !!
*if,GEO_HDW_status,gt,0,then
  TUNNEL_V2_FEMINLETS
*endif


!!                                                                                                !!
!! 4.3  Approach slab                                                                             !!
!!                                                                                                !!
*if,GEO_APR_status,eq,1,then
  cmsel,s,COMPA_approach
  aatt,MAT_conc,REAL_default,TYPE_shell181,CS_top,SEC_apr
  aesize,all,FEM_HDW_esize
  amesh,all
  l2acnt , 'COMPA_leftWall'  , 'COMPL_APR_leftInterface'  ,  , 1
  l2acnt , 'COMPA_rightWall' , 'COMPL_APR_rightInterface' ,  , 1
*endif


!!                                                                                                !!
!! 4.4  Piles                                                                                     !!
!!                                                                                                !!
*if,GEO_PILE_status,eq,1,then
  TUNNEL_V2_FEMPILE
*endif


!!                                                                                                !!
!! 4.5  Soil                                                                                      !!
!!                                                                                                !!
esel,s,type,,TYPE_shell181
nsle,s,active
apportionedstats,,'FEM_nodes_apportioned'

! Floor slab
TUNNEL_V2_ELASTICBASE , 'COMPA_floor'     , CS_tunnel , '-Y' , 'floor'

! Left wall
TUNNEL_V2_ELASTICBASE , 'COMPA_leftWall'  , CS_tunnel , '-X' , 'leftWall'

! Right wall
TUNNEL_V2_ELASTICBASE , 'COMPA_rightWall' , CS_tunnel , '+X' , 'rightWall'

! Top slab
TUNNEL_V2_ELASTICBASE , 'COMPA_roof'      , CS_tunnel , '+Y' , 'roof'


!! 4.5.7  Headwalls                                                                               !!
TUNNEL_V2_HDWBC


!! 4.5.8  Approach slabs                                                                          !!
*if,GEO_APR_status,eq,1,then
  TUNNEL_V2_ELASTICBASE , 'COMPA_approachSup' , CS_tunnel , '-Y' , 'approach'
  TUNNEL_V2_ELASTICBASE , 'COMPA_approachSup' , CS_tunnel , '+Y' , 'approach'
*endif


!!                                                                                                !!
!! 4.6  Soil friction                                                                             !!
!!                                                                                                !!
*get,parmType,PARM,LOAD_thermal,TYPE
*if,parmType,EQ,0,AND,LOAD_thermal,NE,0,THEN
  TUNNEL_V2_FEMTHERMSPRINGS
*endif


!!                                                                                                !!
!! 4.7  Capture images                                                                            !!
!!                                                                                                !!
TUNNEL_V2_FEMPICS


!!                                                                                                !!
!! 4.8  Create special selection components                                                       !!
!!                                                                                                !!
! Tunnel roof elements that should be considered for shear force design
csys,CS_tunnel

cmsel,s,COMPA_roof
esla,s
nsle,s

! Elements near external walls
nsel , u , loc , x , 0                            , GEO_ROOF_thk
nsel , u , loc , x , GEO_TUN_totEffW-GEO_ROOF_thk , GEO_TUN_totEffW

! Elements near internal walls
*do,nthIWall,1,GEO_IWALL_count,1
  xIWall = GEO_IWALL_x_coords(nthIWall)
  nsel , u , loc , x , xIWall-GEO_ROOF_thk , xIWall+GEO_ROOF_thk
*enddo

! Define element component for shear foce results
esln,r,1
cm,COMPE_ROOF_shear,elem
cmsel,s,COMPA_roof
esla,s
cmsel,u,COMPE_ROOF_shear
cm,COMPE_ROOF_noShear,elem

allsel
csys,0

! Clean up
*msg,ui,
Mesh created!
