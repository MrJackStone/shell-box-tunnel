! TUNNEL_V2_HDWBUILDCM


! Exported solid model selection components:
!
! COMPA_headwalls
!
! COMPA_HDW_backBeam
! COMPA_HDW_botBeam
! COMPA_HDW_columns
! COMPA_HDW_footing
! COMPA_HDW_slab
! COMPA_HDW_stiffener
! COMPA_HDW_topBeam
! COMPA_HDW_walls
! COMPA_HDW_wallsBuried
!
! COMPA_HDW_left
! COMPA_HDW_right
!
! COMPA_HDW_leftWall
! COMPA_HDW_rightWall
! COMPA_HDW_leftFoot
! COMPA_HDW_rightFoot
! COMPA_HDW_leftStiffener
! COMPA_HDW_rightStiffener
! COMPA_HDW_leftColumn
! COMPA_HDW_rightColumn
!
! COMPA_HDW_1
! COMPA_HDW_2
!
! COMPA_HDW1_leftWall
! COMPA_HDW1_rightWall
! COMPA_HDW1_leftFoot
! COMPA_HDW1_rightFoot
! COMPA_HDW1_leftStiffener
! COMPA_HDW1_rightStiffener
! COMPA_HDW1_leftColumn
! COMPA_HDW1_rightColumn
! COMPA_HDW1_backBeam
! COMPA_HDW1_botBeam
! COMPA_HDW1_topBeam
! COMPA_HDW1_slab
!
! COMPA_HDW2_leftWall
! COMPA_HDW2_rightWall
! COMPA_HDW2_leftFoot
! COMPA_HDW2_rightFoot
! COMPA_HDW2_leftStiffener
! COMPA_HDW2_rightStiffener
! COMPA_HDW2_leftColumn
! COMPA_HDW2_rightColumn
! COMPA_HDW2_backBeam
! COMPA_HDW2_botBeam
! COMPA_HDW2_topBeam
! COMPA_HDW2_slab


*del,sides,,nopr
*dim,sides,char,2
sides(1)='LEFT','RIGHT'

!! A.  Global, multi-part components                                                              !!
! First inlet
csys,CS_begSkewed

asel,s,loc,z,0
cm,COMPA_frame1,area

asel,s,loc,z,-1e6,0
cmsel,u,COMPA_frame1
cm,COMPA_HDW_1,area

! Second inlet
csys,CS_endSkewed

asel,s,loc,z,0
cm,COMPA_frame2,area

asel,s,loc,z,-1e6,0
cmsel,u,COMPA_frame2
cm,COMPA_HDW_2,area

! All frame areas
cmsel,s,COMPA_frame1
cmsel,a,COMPA_frame2
cm,COMPA_frames,area

! All headwall areas
cmsel,s,COMPA_HDW_1
cmsel,a,COMPA_HDW_2
cm,COMPA_headwalls,area

! Left side
csys,CS_hdw1

cmsel,s,COMPA_HDW_1
asel,r,loc,x,0,1e6
cm,COMPA_HDW_left,area

cmsel,s,COMPA_frame1
asel,r,loc,x,0,1e6
cm,COMPA_FRM_left,area

csys,CS_hdw2

cmsel,s,COMPA_HDW_2
asel,r,loc,x,0,1e6
cmpadd,'COMPA_HDW_left'

cmsel,s,COMPA_frame2
asel,r,loc,x,0,1e6
cmpadd,'COMPA_FRM_left'

! Right side
csys,CS_hdw1

cmsel,s,COMPA_HDW_1
asel,r,loc,x,0,-1e6
cm,COMPA_HDW_right,area

cmsel,s,COMPA_frame1
asel,r,loc,x,0,-1e6
cm,COMPA_FRM_right,area

csys,CS_hdw2

cmsel,s,COMPA_HDW_2
asel,r,loc,x,0,-1e6
cmpadd,'COMPA_HDW_right'

cmsel,s,COMPA_frame2
asel,r,loc,x,0,-1e6
cmpadd,'COMPA_FRM_right'


!! B.  Components by structural part                                                              !!
! Walls
asel,none
*do,nthInlet,1,2,1
  *do,nthSide,1,2,1
    side = sides(nthSide)
    csys,CS_HDW%nthInlet%_%side%Wall
    asel,a,loc,z,0
  *enddo
*enddo
cmsel,r,COMPA_headwalls
cm,COMPA_HDW_walls,area

! Buried portion of walls
asel,none
*do,nthInlet,1,2,1
  *do,nthSide,1,2,1
    side = sides(nthSide)
    csys,CS_HDW%nthInlet%_%side%Wall
    asel,a,loc,y,-1e6,0
  *enddo
*enddo
cmsel,r,COMPA_HDW_walls
cm,COMPA_HDW_wallsBuried,area

! Top edge of walls
*do,nthInlet,1,2,1
  *do,nthSide,1,2,1
    side = sides(nthSide)
    csys,CS_HDW%nthInlet%_%side%Wall
    cmsel,s,COMPA_headwalls
    asel,r,loc,z,0
    lsla
    lsel,u,loc,y,0,-1e6
    lsel,u,loc,x,0
    lsel,u,loc,x,GEO_HDW1_LEFT_wallLength
    lsel,u,loc,x,-GEO_HDW1_LEFT_wallLength
    ksll
    cm,COMPK_HDW%nthInlet%_%side%_wallTopEdge,kp
  *enddo
*enddo

! Slab stiffeners
asel,none
*do,nthInlet,1,2,1
  *do,nthSide,1,2,1
    side = sides(nthSide)
    csys,CS_HDW%nthInlet%_%side%Wall
    asel,a,loc,z,0,GEO_HDW%nthInlet%_slabStfWidth
  *enddo
*enddo
csys,CS_tunnel
asel,r,loc,y,0
cmsel,r,COMPA_headwalls
cm,COMPA_HDW_stiffener,area

! Floor slabs
csys,CS_tunnel
cmsel,s,COMPA_headwalls
cmsel,u,COMPA_HDW_stiffener
asel,r,loc,y,0
cm,COMPA_HDW_slab,area

! Footings
asel,none
cm,COMPA_HDW_footing,area

csys,CS_HDW1_leftFoot
cmsel,s,COMPA_HDW_1
asel,r,loc,z,0
cmpadd,'COMPA_HDW_footing'

csys,CS_HDW1_rightFoot
cmsel,s,COMPA_HDW_1
asel,r,loc,z,0
cmpadd,'COMPA_HDW_footing'

csys,CS_HDW2_leftFoot
cmsel,s,COMPA_HDW_2
asel,r,loc,z,0
cmpadd,'COMPA_HDW_footing'

csys,CS_HDW2_rightFoot
cmsel,s,COMPA_HDW_2
asel,r,loc,z,0
cmpadd,'COMPA_HDW_footing'

! ! Front bottom beam
! ! TODO  rewrite -- this approach does not work for skewed inlets/tunnels
! asel,none
! csys,CS_hdw1
! asel,a,loc,z,GEO_HDW_length
! csys,CS_hdw2
! asel,a,loc,z,GEO_HDW_length
! csys,CS_tunnel
! cmsel,r,COMPA_headwalls
! cm,COMPA_HDW_botBeam,area

! Auxiliary points: skewed tunnel ends
csys,CS_begSkewed
cmsel,s,COMPA_tunnel
lsla
ksll
ksel,r,loc,z,0
*get,xMinBegSkew,kp,,mnloc,x
*get,xMaxBegSkew,kp,,mxloc,x

csys,CS_endSkewed
cmsel,s,COMPA_tunnel
lsla
ksll
ksel,r,loc,z,0
*get,xMinEndSkew,kp,,mnloc,x
*get,xMaxEndSkew,kp,,mxloc,x

! Frame bottom beam
asel,none
csys,CS_begSkewed
asel,a,loc,z,0
asel,r,loc,x,xMinBegSkew,xMaxBegSkew
cm,COMPA_temp,area
csys,CS_endSkewed
asel,a,loc,z,0
asel,r,loc,x,xMinEndSkew,xMaxEndSkew
cmsel,a,COMPA_temp
csys,CS_tunnel
asel,r,loc,y,-GEO_FRAME_backBeamHeight,0
cmsel,r,COMPA_frames
cm,COMPA_HDW_backBeam,area

! Frame top beam
asel,none
csys,CS_begSkewed
asel,a,loc,z,0
asel,r,loc,x,xMinBegSkew,xMaxBegSkew
cm,COMPA_temp,area
csys,CS_endSkewed
asel,a,loc,z,0
asel,r,loc,x,xMinEndSkew,xMaxEndSkew
cmsel,a,COMPA_temp
csys,CS_tunnel
asel,r,loc,y,GEO_TUN_effH,GEO_TUN_effH+GEO_FRAME_headBeamHeight
cmsel,r,COMPA_frames
cm,COMPA_HDW_topBeam,area

! Frame columns
asel,none
csys,CS_begSkewed
asel,a,loc,z,0
csys,CS_endSkewed
asel,a,loc,z,0
csys,CS_tunnel
cmsel,u,COMPA_HDW_backBeam
cmsel,u,COMPA_HDW_topBeam
cm,COMPA_HDW_columns,area


!! C.  Left/right part split                                                                      !!
! Walls
cmsel,s,COMPA_HDW_walls
cmsel,r,COMPA_HDW_left
cm,COMPA_HDW_leftWall,area

cmsel,s,COMPA_HDW_walls
cmsel,r,COMPA_HDW_right
cm,COMPA_HDW_rightWall,area

cmsel,s,COMPA_HDW_wallsBuried
cmsel,r,COMPA_HDW_left
cm,COMPA_HDW_leftWallBuried,area

cmsel,s,COMPA_HDW_wallsBuried
cmsel,r,COMPA_HDW_right
cm,COMPA_HDW_rightWallBuried,area

! Footings
cmsel,s,COMPA_HDW_footing
cmsel,r,COMPA_HDW_left
cm,COMPA_HDW_leftFoot,area

cmsel,s,COMPA_HDW_footing
cmsel,r,COMPA_HDW_right
cm,COMPA_HDW_rightFoot,area

! Slab stiffeners
cmsel,s,COMPA_HDW_stiffener
cmsel,r,COMPA_HDW_left
cm,COMPA_HDW_leftStiffener,area

cmsel,s,COMPA_HDW_stiffener
cmsel,r,COMPA_HDW_right
cm,COMPA_HDW_rightStiffener,area

! Columns
cmsel,s,COMPA_HDW_columns
cmsel,r,COMPA_FRM_left
cm,COMPA_HDW_leftColumn,area

cmsel,s,COMPA_HDW_columns
cmsel,r,COMPA_FRM_right
cm,COMPA_HDW_rightColumn,area


!! D.  1st/2nd part split                                                                         !!
! Split headwall components
cmRootCount = 9
*del,cmname_roots,,nopr
*dim,cmname_roots,string,248,cmRootCount

cmname_roots(1,1) = 'leftWall'
cmname_roots(1,2) = 'leftWallBuried'
cmname_roots(1,3) = 'rightWall'
cmname_roots(1,4) = 'rightWallBuried'
cmname_roots(1,5) = 'leftFoot'
cmname_roots(1,6) = 'rightFoot'
cmname_roots(1,7) = 'leftStiffener'
cmname_roots(1,8) = 'rightStiffener'
! cmname_roots(1,12) = 'botBeam'
cmname_roots(1,9) = 'slab'

*do,nthCM,1,cmRootCount,1
  root = cmname_roots(1,nthCM)
  *do,nthHDW,1,2,1
    cmsel,s,COMPA_HDW_%root%
    cmsel,r,COMPA_HDW_%nthHDW%
    cm,COMPA_HDW%nthHDW%_%root%,area
  *enddo
*enddo

! Split frame components
cmRootCount = 4
*del,cmname_roots,,nopr
*dim,cmname_roots,string,248,cmRootCount

cmname_roots(1,1) = 'leftColumn'
cmname_roots(1,2) = 'rightColumn'
cmname_roots(1,3) = 'backBeam'
cmname_roots(1,4) = 'topBeam'

*do,nthCM,1,cmRootCount,1
  root = cmname_roots(1,nthCM)
  *do,nthFRM,1,2,1
    cmsel,s,COMPA_HDW_%root%
    cmsel,r,COMPA_frame%nthFRM%
    cm,COMPA_HDW%nthFRM%_%root%,area
  *enddo
*enddo
